/* Documentation for this module is in the subdirectory prolog_db_doc
in file prolog_db.pdf, which can be re-generated by getting into that
directory, and running:

% xsb
| ?- [xsbdoc], xsbdoc(prolog_db_format,pdf).

************/

:- comment(title,"Trie Terms for Sets and Prolog Databases").
:- comment(subtitle,"Efficient Representation of Sets of Clauses and Terms").
:- comment(author,"David S. Warren").

:- comment(summary, " The predicates of the @tt{prolog_db} module
support an abstract datatype for sets of Prolog clauses.  The
underlying implementation uses a @em{ground} Prolog term that encodes
an indexed ``trie.''  The trie is built using the pre-order traversals
of the heads of clauses of the set and thus provides complete indexing
for terms that are bound on an initial sequence of symbols in their
pre-order traversal.  The representation is @em{canonical} in that the
same set of clauses is always represented by the same trie term, no
matter how it is constructed.

These trie terms can be used to represent a set of Prolog clauses
(a.k.a., a Prolog Database.)  We call a set of clauses a ``trie-term
database.''  Operations are provided to assert clauses to and retract
clauses from a given trie-term database to generate a new one
reflecting the change.  Also there is an operation to evaluate a goal
using the clauses of a given trie-term database.  Trie-terms do not
support duplicates or clause order, so cuts (and duplicate clauses)
cannot be used in trie-term databases.  Trie-term databases can
support Prolog meta-programming in a pure way, since trie-terms are
simply Prolog terms and so earlier trie-term database states are
automatically recovered on backtracking (unlike with Prolog's built-in
assert and retract operations).
	   
Trie terms can be used to represent sets of ground Prolog terms, or
sets of relations over Prolog terms.  Operations are provided for
adding elements to sets/relations generating new sets/relations, and
for deleting and lookup.  Also several set operations, e.g. union,
intersection, and difference, are provided.  These operations take
advantage of the indexed structure of a trie-term to sometimes provide
much better performance than more naive tuple-at-a-time
implementations.  This module also provides several restricted
relational operators that can take advantage of the indexed data
structure.

The complexity of adding, deleting, and looking up single elements in
a trie-term is, in general, log of the the number of symbols in the
trie-term (with minor caveats).
	   
Since trie terms are ground, they can be interned (a.k.a.
hash-cons-ed) in which case they are copied to a global space and all
subterms are represented only once.  In this case the interned trie
terms are represented in Prolog as specially named atoms, which in
fact encode pointers to the global term representations.  For every
predicate that manipulates these trie terms, there is a corresponding
predicate that manipulates the interned form of the trie terms.  Most
applications will want to use these interned versions.  One can think
of the interned versions as passing around ``names'' of the clause sets,
instead of the clause sets themselves.  The interned trie terms can be
efficiently tabled since only the atomic name of a trie term will be
copied to a table.  Also since these trie terms are canonical, two
trie-term names are equal if and only if the sets they name are equal.
Also using answer subsumption with the subset operation on interned
trie terms may prove useful for some applications.

In the implementation, every trie term is wrapped with a functor
symbol that indicates whether the representation is a regular trie
term or an interned trie term.  All predicates take either form and
produce a result in the same form.  The predicate @pred{new_db/1}
returns an empty database in regular Prolog term form;
@pred{new_dbi/1} returns an empty database in interned form.  One of
the predicates must be used to create an initial empty database.
(There is also @pred{assert_in_dbi(+ClauseList,-DB)}, which takes a
list of clauses and returns a database, in interned form, containing
just those clauses; also @pred{load_in_dbi(+FileName,-DB)}, which
reads clauses from a file to initiate a database in interned form.)
").

:- comment(module," The @tt{prolog_db} module defines a data type that
supports a very general facility for manipulating sets of Prolog
clauses represented as a single Prolog term that can be useful for a
variety of purposes.  We will describe the module's functionality
though a series of examples that use progressively more complicated
aspects of the data type.

@section{Sets of Atoms (or Terms)}

In its simplest form, the @tt{prolog_db} data type can be used to
manipulate sets of (ground) atoms efficiently.  For many applications
basic Prolog nondeterminism (perhaps with tabling) will effectively
handle sets of terms, but occasionally there are applications in which
a programmer wishes to maintain a term that explicitly represents a
set of atoms.  Often this is done using a sorted list in Prolog;
sorting eliminates duplicates and canonicalizes a list to a set, so
then list equality implements set equality.  This works well for small
sets but, when sets get larger than a few 10's of items, performance
can suffer signicantly.  This can be avoided by using the
@tt{prolog_db} data type.  A new empty set, @var{Set0}, is initialized
with @pred{new_dbi(Set0)}; an element, say 'George', is added to it
with @pred{assert_in_db('George',Set0,Set1)} creating the set with
'George' added in @var{Set1}.  For example,

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db('George',_S0,_S1),dump_db(_S1,userout).
'George'.
@end{verbatim}

@noindent Dumping out the contents of @var{_S1}, we see it contains
just the atom 'George'.

Sets can be compared for identity with the Prolog operation @pred{==}
(or @pred{=}.)

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db('George',_S0,_S1),assert_in_db('Mary',_S1,_S2),
     assert_in_db('Mary',_S0,_S1B),assert_in_db('George',_S1B,_S2B),
     _S2 == _S2B, dump_db(_S2,userout).
'Mary'.
'George'.
@end{verbatim}

@noindent Here we have added 'George' to the empty set and then added
'Mary' to that set to obtain @var{_S2}.  Then we added 'Mary' to the
empty set and added 'George' to that set to obtain @var{_S2B}, then
checked that those two sets were equal and dumped the contents of one.

A set can be checked to see if it contains a constant 'George' using
@pred{fact_in_db('George',Set1)}.

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db('George',_S0,_S1),assert_in_db('Mary',_S1,_S2),
   (fact_in_db('George',_S2) -> writeln('contains George') ; writeln(oops)),
   (fact_in_db('William',_S2) -> writeln(oops) ; writeln('but not William')).
contains George
but not William
@end{verbatim}

@var{Set1} can be checked to see if it is a subset of @var{Set2} using
@pred{subset_db(Set1,Set2)}.  For example,

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db('George',_S0,_S1),assert_in_db('Mary',_S1,_S2),
   (subset_db(_S1,_S2) -> writeln('correctly subset') ; writeln(oops)),
   (subset_db(_S2,_S0) -> writeln('oops') ; writeln('correctly not subset')).
correctly subset
correctly not subset
@end{verbatim}

The union of sets @var{Set1} and @var{Set2} can be computed into
@var{Set3} using @pred{union_db(Set1,Set2,Set3)}.  For example,

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db('George',_S0,_S1),assert_in_db('Mary',_S0,_S2),
     union_db(_S1,_S2,_S3),dump_db(_S3,userout).
'Mary'.
'George'.
@end{verbatim}

Other similar set operations are also provided.  See the
@em{Documentation of exports} section below for details.  The time and
space complexities of all these operations are at or near optimal.
Note also that if we use the interned trie form (as we showed here by
the use of @pred{new_dbi(Set0)}), then ``names'' of sets, rather than
the explicit trie terms themselves, become the values of the Prolog
set variables.  This means that they can be efficiently tabled, since
only the names are copied into and out of a table, rather than the
explicit trie-terms themselves.  To see the names of the sets, we can
remove the underscores that prefix the set variable names in our
previous queries.  So repeating the union query above, we get:

@begin{verbatim}
| ?- new_dbi(S0),assert_in_db('George',S0,S1),assert_in_db('Mary',S0,S2),
     union_db(S1,S2,S3),dump_db(S3,userout).
'Mary'.
'George'.

S0 = $TRIETERMI([])
S1 = $TRIETERMI(}]]}00000204151FDC71)
S2 = $TRIETERMI(}]]}00000204151FDCC1)
S3 = $TRIETERMI(}]]}00000204151FDC99)
@end{verbatim}

@noindent We see that a named set is represented as a term whose main
functor symbol is @tt{'$TRIETERMI'}, indicating a trie-term that is
interned.  The single subfield of that term is a string whose name
encodes the address of the trie term structure in interned space.
(The prefix '}]]}' is to try to avoid name conflicts with user
strings.)  The empty set is a special case, since it is not a
structured term, but just the constant [ ].

We could also have used the explicit trie-term representation, by
using @pred{new_db(S0)} to create the explicit trie representation for
the empty set:

@begin{verbatim}
| ?- new_db(S0),assert_in_db('George',S0,S1),assert_in_db('Mary',S0,S2),
     union_db(S1,S2,S3),dump_db(S3,userout).
'Mary'.
'George'.

S0 = $TRIETERM([])
S1 = $TRIETERM(ht(2,1,h([],[],[],[George - [true]])))
S2 = $TRIETERM(ht(2,1,h([],[Mary - [true]],[],[])))
S3 = $TRIETERM(ht(2,2,h([],[Mary - [true]],[],[George - [true]])))
@end{verbatim}

@noindent We see the explicit trie-term representation, now wrapped
with functor symbol @tt{'$TRIETERM'} (without the trailing 'I') and we
see the explicit atoms 'Mary' and 'George' down in the trie-term
representation.

Of course, elements can be deleted from sets.  This is done using
@tt{retractall_in_db(Atom,Set0,Set)}.  For example:

@begin{verbatim}
| ?- new_dbi(_S0),assert_in_db(['George','Mary'],_S0,_S1),dump_db(_S1,userout),
     retractall_in_db('George',_S1,_S2),nl,dump_db(_S2,userout).
'Mary'.
'George'.

'Mary'.
@end{verbatim}

@noindent Here we insert both 'George' and 'Mary' into the empty set
(@pred{assert_in_db/3} will accept a list in its first argument), print
out that set, then remove 'George' from that set, and print out the
result.

The functionality of the @tt{prolog_db} data type that is required to
efficiently support sets of atoms is the indexing it provides at a
single point in a trie, and not in the multiple levels of the trie.
Since for a set of atoms, there is no trie.

@subsection{Example: Paths in Graphs}

We can use @tt{prolog_db} term sets to collect path edges when
computing paths in a graph.  Consider the following program:

@begin{verbatim}
:- import subset_db/2, assert_in_db/3, assert_in_dbi/2, dump_db/2 from prolog_db.

:- table path(_,_,po(subset_db(_,_))).
path(X,Y,S) :- edge(X,Y), assert_in_dbi((X->Y),S).
path(X,Y,S) :- path(X,Z,S1),edge(Z,Y),assert_in_db((Z->Y),S1,S).

edge(a,b).    edge(e,f).
edge(a,c).    edge(d,f).
edge(c,d).    edge(f,c).
edge(b,e).    edge(f,g).
@end{verbatim}

Here @pred{path(X,Y,S)} is defined for paths in the @pred{edge/2}
graph where node @var{Y} is reachable from node @var{X} along a path
consisting of minimal sets of edges in set @var{S}.  The @tt{table}
declaration uses answer subsumption to ensure that only the shortest
paths (those with the minimal edge sets) are kept.  So this will work
for all graphs returning, for desired nodes, all minimal paths between
them, e.g., those containing no loops.  As an example, to find the
minimal paths from @tt{a} to @tt{d} in the indicated graph, we can do:

@begin{verbatim}
| ?- X=a,Y=d,path(X,Y,S),nl,writeln((X-->Y)),prolog_db:dump_db(S,userout),fail.

(a --> d)
->(a,c).
->(c,d).

(a --> d)
->(a,b).
->(e,f).
->(b,e).
->(f,c).
->(c,d).

no
| ?- 
@end{verbatim}

@noindent Note that we have two such minimal paths from @tt{a} to
@tt{d} in this graph.  There are, of course, infinitely many distinct
paths from @tt{a} to @tt{d} of the form @tt{acd(fcd)*} in this graph.

By changing the answer subsumption aggregation predicate from
@pred{subset_db/2} to, say, @pred{smaller_db/2} (which we could write
using @pred{size_db/2}), we could compute just the shortest path(s).

@subsection{Example: Too Many Parses} There are some context-free
grammars which for some input strings have many distinct parses,
exponentially many in the length of the input string.  If we are not
careful, in trying to compute the parses, we may take exponential time
constructing parses for substrings of the input string when the input
string is not itself in the language.  

Consider the grammar:
@begin{verbatim}
a --> a, a.
a --> [a].
@end{verbatim}

@noindent This grammar recognizes all strings consisting of one or
more @tt{a}'s.  The natural way to construct parses for this grammar
is with the following Prolog program:

@begin{verbatim}
:- table a/3.
a(a(P1,P2)) --> a(P1), a(P2).
a(a) --> [a].
@end{verbatim}

@noindent in which we've added an argument to hold the Prolog term
that represents the parse.  The exponential behavior arises from the
first rule: P1 may have @tt{m} different values for a given substring
it covers, and P2 may have @tt{n} different values for its substring;
thus a(P1,P2), the parse of the full string, would take on @tt{m * n}
different values.

We can short-circuit this combinatoric explosion by, instead of
representing the parses explicitly, representing the set of parses at
a position, and using that set in larger parses, and thus avoiding the
multiplication that causes the combinatorial explosion.  In this
representation, in the above program @var{P1} and @var{P2} would
take on as values @em{names} of sets of terms.

We can use answer subsumption to carry this out.  Consider the
following program:
@begin{verbatim}
:- import assert_in_db/3, new_dbi/1, is_db/1, fact_in_db/2 from prolog_db.

:- table a(fold(collect/3,new_dbi/1),_,_).
a(a(P1,P2)) --> a(P1), a(P2).
a(a) --> [a].

collect(DB0,Parse,DB) :- assert_in_db(Parse,DB0,DB).

expand(Term,ETerm) :-
    (is_db(Term)
     ->	fact_in_db(RTerm,Term),
        expand(RTerm,ETerm)
     ; atomic(Term)
     ->	ETerm = Term
     ;	Term =.. [F|Args],
        expand_list(Args,EArgs),
        ETerm =.. [F|EArgs]
    ).

expand_list([],[]).
expand_list([T|Ts],[ET|ETs]) :-
    expand(T,ET),
    expand_list(Ts,ETs).
@end{verbatim}

@noindent The answer subsumptive aggregation predicate is
@pred{collect/3}, which simply adds a newly computed parse to the
current set.  The predicate @pred{expand(+Term,-ETerm)} takes a term,
@var{Term}, that may contain named trie terms as subcomponents, and
replaced each such set term instance with the terms in that set, in
turn.  So now we can parse a string and generate its parses as
follows:

@begin{verbatim}
| ?- a(_T,[a,a,a,a],[]),expand(_T,P).
P = a(a,a(a,a(a,a)));
P = a(a,a(a(a,a),a));
P = a(a(a,a),a(a,a));
P = a(a(a,a(a,a)),a);
P = a(a(a(a,a),a),a);
no
| ?- 
@end{verbatim}

@noindent So here, the generation of the term @var{_T} by calling the
grammar rule @pred{a/3} takes polynomial time; it is the
@pred{expand(_T,P)} that will take exponential time to produce the
combinatorially many parses.

@section{Sets of Relations}

Another way the @tt{prolog_db} data type can be used is to store an
extensional relational database in a Prolog variable.  A set of ground
terms can be interpreted as defining a set of relations by
interpreting the main functor symbol of each term as a relation name,
and the subterms of that structure as the field values of a tuple of
that relation.  This is exactly how Prolog would treat such a set of
terms were they to be asserted into Prolog's clause database.

For example, one can create a term containing two r/2 tuples and three
q/3 tuples in @var{_DB} with
@begin{verbatim}
| ?- new_dbi(_DB0),assert_in_db([r(a,b),r(b,c),q(a,2,3),q(b,3,4),q(b,4,5)],_DB0,_DB),
     dump_db(_DB,userout).
q(a,2,3).
q(b,4,5).
q(b,3,4).
r(a,b).
r(b,c).
@end{verbatim}
Given a database term (or name, since we here again used
@pred{new_dbi/1} to generate an empty database and return its name), we can
ask queries of that database.  For example,
@begin{verbatim}
| ?- new_dbi(_DB0),assert_in_db([r(a,b),r(b,c),q(a,2,3),q(b,3,4),q(b,4,5)],_DB0,_DB),
     call_in_db(q(b,X,Y),_DB).

X = 4
Y = 5;

X = 3
Y = 4;

no
| ?- 
@end{verbatim}

@noindent creates the desired database trie name and then
evaluates the query @pred{q(b,X,Y)} in the indicated database and
nondeterministically return two pairs as seen.  
	   
We can also ask more complex queries, as seen with the following join:
@begin{verbatim}
| ?- new_dbi(_DB0),assert_in_db([r(a,b),r(b,c),q(a,2,3),q(b,3,4),q(b,4,5)],_DB0,_DB),
     call_in_db((r(a,Y),q(Y,Z,W)),_DB).

Y = b
Z = 4
W = 5;

Y = b
Z = 3
W = 4;

no
| ?- 
@end{verbatim}

The set operations of @pred{union_db/3}, @pred{intersect_db/3},
etc. can be used with sets representing relational databases.  For
example the union of two relational databases can be directly
computed.  And the operations can be very efficient.  For example, if
the two databases do not contain a table name in common, then their
union is computed with a complexity that is dependent on the number of
distinct tables but independent of their sizes.

The @tt{prolog_db} data type provides operations designed to be used
with extensional relational databases stored as @tt{prolog_db} trie terms.
These operations take an input database, probably containing multiple
relations, and return an output database that (usually) contains a new
relation added by the operation.  These operations depend on, and take
advantage of, the structure of the tries that represent sets of
(tuples of) terms for their efficient implementation.

Just as the data type contains set operations on full databases, it
contains set operations on tables in a single database.  So, for
example:

@begin{verbatim}
| ?- assert_in_dbi([r(a,b),r(b,c),s(1,2),s(2,3)],_DB1),
     union_in_db(r/2,s/2,t/2,_DB1,_DB), dump_db(_DB,userout).
r(a,b).
r(b,c).
s(1,2).
s(2,3).
t(1,2).
t(a,b).
t(2,3).
t(b,c).
@end{verbatim}

@noindent initializes a @var{DB1} with tables @pred{r/2} and
@pred{s/2}, then takes the two tables @pred{r/2} and @pred{s/2} in
database @var{DB1} and produces a new database in @var{DB} that
contains all tables in @var{DB1} plus a new table @pred{t/2} that
contains the union of tables @pred{r/2} and @pred{s/2}.  If @pred{t/2}
had been in DB1, it would have been replaced with the new definition.

@subsection{Example: Semi-Naive Transitive Closure}

We'll introduce several other single database operators that are
supported by the @tt{prolog_db} abstract data type with the following
more complicated example.  We will assume we have a trie-term
relational database that contains an edge relation @pred{e/2}.  We
want to compute the (converse of the) transitive closure of this
@pred{e/2} relation into a new relation @pred{p/2}.  It turns out that
with the operations we have, the converse of the transitive closure is
easier to compute directly.

Consider the program:
@begin{verbatim}
:- import retractall_in_db/3, reorder_in_db/4, difference_in_db/5, is_empty_in_db/2,
    union_in_db/5, join_in_db/6, project_in_db/5 from prolog_db.
:- import assert_in_dbi/2, dump_db/2 from prolog_db.

test(Edges) :-
    assert_in_dbi(Edges,DB0),
    transclose(DB0,DB),
    dump_db(DB,userout).


transclose -->
    retractall_in_db(p(_,_)),
    reorder_in_db(e(A,B),delta(B,A)),
    transclose_loop.

transclose_loop -->
    difference_in_db(delta/2,p/2,delta/2),
    (is_empty_in_db(delta/2)
     ->	[]
     ;  union_in_db(delta/2,p/2,p/2),
        join_in_db(e/2,delta/2,1,delta/3),
        project_in_db(delta/3,1,delta/2),
        retractall_in_db(delta(_,_,_)),
        transclose_loop
    ).

is_empty_in_db(Pref,DB,DB) :- is_empty_in_db(Pref,DB).
@end{verbatim}

The imports give us access to the @tt{prolog_db} predicates we
need. The predicate @pred{test/1} is a simple driver that takes a list
of @pred{e/2} terms, initializes a @var{DB0} with them, calls
@pred{transclose/2} to computed the (converse of the) transitive
closure, and then prints out the @var{DB} returned.  The work of
computing the transitive closure of @pred{e/2} is all done in
predicates @pred{transclose/2} and @pred{transclose_loop/2}.

Note first that we use the DCG transformation in our definitions of
these two predicates, i.e., the rules are defined using the operator
@pred{-->}.  The DCG transformation adds two arguments to each subgoal
in the body of a DCG rule and chains those added variables.  This in
effect treats each subgoal in a DCG rule as a DB transformer, which
takes a DB as input and produces a DB as output.  This is exactly what
each of these @tt{prolog_db} predicates does, and so the DCG notation
is natural for this specification.

So @pred{transclose/2} transforms its input DB by first deleting the
p/2 table (if any).  It then uses @pred{reorder_in_db/4} to generate a
new table for @pred{delta/2} consisting of the @pred{e/2} tuples with
their arguments reordered (i.e., the converse of @pred{e/2}.)  This in
effect generates a new version of the @pred{e/2} table with different
indexing.  Then it calls @pred{transclose_loop/2} to iterate to
compute the transitive closure of @pred{e/2}.

The predicate @pred{transclose_loop/2} iteratively transforms the
database by accumulating the @pred{delta/2} table into the @pred{p/2}
table and joining the @pred{e/2} table to the new delta table at each
iteration to get a new set of pairs in the transitive closure of
@pred{e/2}.  So the @pred{difference_in_db(delta/2,p/2,delta/2)}
transform removes from @pred{delta/2} those pairs that are already in
the transitive closure, since we want to continue with @pred{delta/2}
containing only new ones.  Then if there are no new ones, the
computation has converged and it returns the current DB.  (Note that
we need a rule to define @pred{is_empty_in_db/3} using the
@tt{prolog_db} predicate @pred{is_empty_in_db/2}, so that it could be
used as an identity DB transformer in the DCG rule.)  But if there are
new tuples in @pred{delta/2}, we union (accumulate) them into
@pred{p/2}.  Then we perform a join of those tuples with the
@pred{e/2} tuples to create the new @pred{delta} tuples.  The
@pred{join_in_db/6} predicate takes two tables to join and performs an
equi-join on them using an initial sequence of their arguments (here,
just 1) and produces a table with one copy of the join argument(s) and
all the other arguments of the input tables.  This is a very
restricted form of join and it is supported because, with this form of
join, the join algorithm can take advantage of the trie-term data
structure to perform efficiently.  Other join forms (and sometimes
even this one) can be more efficiently implemented using
@pred{call_in_db/2}.  After the join, the
@pred{project_in_db(delta/3,1,delta/2)} projects out the first
argument of the @pred{delta/3} table generating the new @pred{delta/2}
table.  Finally the @pred{delta/3} table is deleted, and the loop is
again invoked with the updated @pred{p/2} table and the new
@pred{delta/2} table.

We can see the results of the computation on a given edge relation:
@begin{verbatim}
| ?- test([e(1,2),e(2,3),e(3,4),e(3,1)]).
p(4,1).
p(4,2).
p(4,3).
p(1,1).
p(1,2).
p(1,3).
p(2,1).
p(2,2).
p(2,3).
p(3,1).
p(3,2).
p(3,3).
e(1,2).
e(2,3).
e(3,4).
e(3,1).
@end{verbatim}
@noindent Remember that @pred{p/2} contains the @em{converse} of the
transitive closure of @pred{e/2}, so e.g., @pred{p(4,1)} indicates
that the pair @tt{[1,4]} is in the transitive closure of @pred{e/2}.

@section{Full Prolog Databases}

So far we have only stored ground terms in @tt{prolog_db} trie-term
databases.  But one can add rules to these databases as well.
Consider the following example:

@begin{verbatim}
| ?- assert_in_dbi([e(1,2),e(2,3),e(3,4),e(3,1)],DB0),
     assert_in_db([(p(X,Y) :- e(X,Y)), (p(X,Y) :- table(p(X,Z)),e(Z,Y))],DB0,DB1),
     call_in_db(table(p(X,Y)),DB1),writeln(p(X,Y)),fail.
p(3,3)
p(3,2)
p(3,1)
p(3,4)
p(2,2)
p(2,1)
p(2,4)
p(2,3)
p(1,1)
p(1,4)
p(1,3)
p(1,2)
@end{verbatim}

@noindent Here we initially created a database with four @pred{e/2}
facts.  Then we added two rules to it, to define @pred{p/2} as the
transitive closure of @pred{e/2}.  The recursive rule is
left-recursive, so we indicate that calls to that left-recursive goal
should be table-d.  Then in this database with the facts and the
rules, we call the goal @pred{table(p(X,Y))}, which calls @pred{p/2},
tabling the call.  This query then returns the answers to the
rule-defined @pred{p(X,Y)}.

A few caveats: As seen in this example, tabling must be specified at
the call level by wrapping the call of a goal to be tabled with the
functor @tt{table}.  The trie-term databases of @tt{prolog_db} do not
support ordering or duplicates, so rules whose evalutation depend on
those facilities are not supported.  In particular, cuts (!) in rules
are not allowed.  See the detailed documentation on
@pred{call_in_db/2} for more particulars.

@subsection{Example: STRIPS Planning}

As an example of search through a graph of databases, consider a block
planning problem as represented in STRIPS (ref?).  The situation is a
set of same-sized blocks in a configuration on a table, with some of
the blocks perhaps stacked on top of others in towers.  The problem
is: given an initial state of the blocks configuration and a final
state, what are the operations of moving the blocks that can turn the
initial state into a final state.  A block configuration is described
by a set of facts using the predicates: @pred{on(X,Y)} indicating
block @var{X} is immediately on top of block @var{Y}; @pred{onTable(X)}
indicating that block @var{X} is resting directly on the table;
@pred{clear(X)} indicating that block has no block resting on top of
it; @pred{holds(X)} indicating that the hand is holding block @var{X};
and @pred{handEmpty} indicating that the hand is empty.  We note that
@pred{handEmpty} can be defined in terms of @pred{holds(X)}.  The hand
has four actions it can perform: pickup a block from the table, put
down a block on the table, stack a block on top of another block, and
unstack a block from the top of another block.

Consider the following program:

@begin{verbatim}
:- import call_in_db/2, assert_in_db/3, retractall_in_db/3
    dump_db/2, assert_in_dbi/2 from prolog_db.

pickup(X) -->
    call_in_db((clear(X),onTable(X),handEmpty)),
    assert_in_db([holds(X)]),
    retractall_in_db([handEmpty,clear(X),onTable(X)]).

putdown(X) -->
    call_in_db((holds(X))),
    assert_in_db([handEmpty,clear(X),onTable(X)]),
    retractall_in_db([holds(X)]).

stack(X,Y) -->
    call_in_db((holds(X),clear(Y))),
    assert_in_db([clear(X),on(X,Y),handEmpty]),
    retractall_in_db([clear(Y),holds(X)]).

unstack(X,Y) -->
    call_in_db((clear(X),on(X,Y),handEmpty)),
    assert_in_db([clear(Y),holds(X)]),
    retractall_in_db([clear(X),on(X,Y),handEmpty]).

action --> pickup(_X).
action --> putdown(_X).
action --> stack(_X,_Y).
action --> unstack(_X,_Y).

:- table action_sequence/2.
action_sequence --> action, dump.
action_sequence --> action_sequence, action, dump.

call_in_db(G,DB,DB) :- call_in_db(G,DB).

dump(DB,DB) :- dump_db(DB,userout),nl.

test(Source,Target) :-
    assert_in_dbi([(handEmpty :- \+ holds(_))|Source],SDB),
    assert_in_dbi([(handEmpty :- \+ holds(_))|Target],TDB),
    action_sequence(SDB,TDB).
@end{verbatim}

Actions can transform the current state to a new state, but actions
can be performed only if certain preconditions hold.  So each action
is associated with 1) a precondition, 2) a set of facts to add to the
current state, and 3) a set of facts to remove from the current state.
So, for example the @pred{pickup(X)} action, as defined in this
program, requires that block @var{X} be clear in the curent state,
that it be on the table, and that the hand be empty.  If these are
true, then the action can be performed and a new state is generated by
adding the fact that the hand holds block X, and by deleting the facts
that @var{X} is clear, and that @var{X} is on the table.  Similarly,
the other three actions have their own preconditions, facts to add,
and facts to delete to generate the state after application of the
action.  Again, these actions are specified as database transformers
and so we use the DCG notation with its implicit database arguments.

The predicate @pred{action_sequence/2} is defined to be the transitive
closure of the action DB transformer, so it will search paths through
the graph of database states whose edges are actions that transform
the preceding states into the succeeding ones.  We need to table this
predicate since this graph clearly contains cycles.  (Ref. Reza's
thesis).

The predicate @pred{test/2} sets up a desired initial state (including
the rule to define @pred{handEmpty}) and final state and calls
@pred{action_sequence/2} to carry out a sequence of actions that gets
the system from the initial state to the target state.  This program
does not save that path, and does not search for a minimal path.
These functionalities would not be difficult to add (e.g. see
C.R. Ramakrishnan's paper on search.)

We give partial output for a query to find a sequence of actions to
turn an initial state into a final state.  We elide many intermediate
states.  (A total of 500 were generated.)

@begin{verbatim}
| ?- test([onTable(a),on(b,c),onTable(c),onTable(d),clear(a),clear(b),clear(d)],
    [onTable(b),on(c,b),on(a,c),on(d,a),clear(d)]).
clear(a).
clear(b).
holds(d).
on(b,c).
onTable(a).
onTable(c).
:-(handEmpty,\+(holds(A))).

clear(d).
clear(b).
holds(a).
on(b,c).
onTable(d).
onTable(c).
:-(handEmpty,\+(holds(A))).

.......

clear(a).
holds(d).
on(a,c).
on(c,b).
onTable(b).
:-(handEmpty,\+(holds(A))).

clear(d).
on(d,a).
on(a,c).
on(c,b).
onTable(b).
:-(handEmpty,\+(holds(A))).

yes
| ?- 
@end{verbatim}



@section{Representation Details}

We turn to how a database is represented as a term in the
@tt{prolog_db} data type.  A @tt{prolog_db} database is represented as
a ground Prolog term (or the name of such an interned term).  The term
represents a trie that is indexed at each potential branch point.

The first problem is how to store a set of key-value pairs so that
given a key and value, we can efficiently add the pair to the table,
and given a key, efficiently access its paired value in the table.
There are many data structures that support these operations.  We'll
describe the one we use by starting by assuming that the keys are
n-bit integers.  We use a 4-way tree to store the index.  The last 2
bits of the key determine which of the four subtrees of the root
contain the value for the key.  The next 2 bits of the key determine
which subtree of that subtree contains the value, etc.

So as a first approximation, if we have the following 3 6-bit integers

@begin{verbatim}
15: 00 11 11
24: 01 10 00
34: 10 00 10
@end{verbatim}

@noindent they would be stored as in the following index tree:

@begin{verbatim}
h( h([],[],h([],V24,[],[]),[]),
   []
   h(h([],[],V34,[]),[],[],[]),
   h([],[],[],h(V15,[],[],[]))
 )
@end{verbatim}

@begin{enumerate}
@item The 15 (00 11 11) position is found by taking the fourth (11)
subtree of the main tree, then the fourth (11) subtree of that, and
then the first (00) subtree of that.

@item The 24 (or 01 10 00) position is found by taking the first (00)
subtree of the main tree, and then the third subtree (10) of that, and
then the second (01) subtree of that.

@item The 34 (10 00 10) position is found by taking the third (10)
subtree of the main tree, then the first (00) subtree of that, and
then the third (10) subtree of that.

@end{enumerate}

Any subtree that contains no values is represented by [ ].  

The position accessed by a key integer is used to store the list of
values associated with that key.

We will call these trees, index trees.

When we fix the number of bits to @tt{n}, then every value is stored
at the same distance from the root of the index tree; the size of the
data structure is @tt{O(k*n)}, where @tt{k} is the number of values in
the tree; all operations are of complexity @tt{n}; the representation
is canonical (assuming care is taken on deletion and we handle
multiple values for a single key consistently).  (There are some
obvious caveats if we have large numbers of distinct values for a
single key.)

For our trie terms, we want to index on the symbols in a Prolog term,
and not n-bit integers.  But we can turn symbols into n-bit integers
by choosing a hash function whose output value is an appropriately
sized integer.  The more difficult problem is that we will need
indexes of radically different sizes: many indexes in our trie will
have only one or two entries, but some may have hundreds of thousands
or more.  We don't want to fix the bit-size once and for all, but when
we have few entries in an index, we want a small number of bits (and
shallower index tree), and when we have a large number of entries to
index, we want a larger bit-size.

So we choose the bit-size based on the number of keys in the index.
The structure we use is of the form: @tt{ht(NBits,NEntries,IndTree)},
where @tt{NBits} is the number of bits for the index tree,
@tt{NEntries} is the number of key entries in the index tree, and
@tt{IndTree} is the index tree (for @tt{NBits} bits).  So the code
keeps track of how many entries there are in such an index tree and if
it overflows, doubles the bit-size and creates a new index for the
given entries using that new bit-size.  An index tree of bit-size
@tt{n} is allowed to contain @tt{2^(n/2)} through @tt{2^n-1} entries;
overflow or underflow triggers resizing.  The minimum size nonempty
tree has a bit size of 2.  The allowable bit-sizes are 2, 4, 8, 16,
32, and 64.

Now a trie-term is just a set of nested index trees, where the value
associated with a symbol key is an index tree for the remainder of the
trie.  (The last value is the body of rule whose preordered head
generated the symbol sequence through the trie.)

Because of resizing, the complexity arguments above don't completely
apply to trie-terms.  The expectation is that resizing is rare enough
that the effective complexity remains log.

@subsection{Prefix Terms}

In the above section describing the @tt{prolog_db} predicates that
treat a single trie-term as representing a set of extensional
relational tables, we identified the table operands by using a simple
predicate/arity notation.  But in fact there is a more general way to
specify a set of tuples for those table operations to manipulate.

We need to look a little more closely at the trie
data structure that represents sets of terms.  Consider the set of
terms:
@begin{verbatim}
   r(a,b,c).
   r(a,b,d).
   r(a,e,f).
   r(a,f(a,b),c).
   r(b,g,c).
@end{verbatim}
@noindent and their pre-order traversals in a trie:
@begin{verbatim}
   r/3 - a - b - c
               - d	   
             e - f
             f/2 - a - b - c
       - b - g - c
@end{verbatim}

@noindent The position of each symbol in the trie can be thought of as
the root of a sub-trie.  We can indicate such a sub-trie by using a
``prefix term,'' which is a Prolog term whose pre-ordering consists of
an initial sequence of ground symbols followed a sequence of distinct
variables.  For example, @tt{r(_,_,_)} and @tt{r(a,f(a,_),_)} are
prefix terms, but @tt{r(_,b,_)} is not.  Associated with a prefix term
(and with its identified sub-trie) is an integer (called the arity)
that indicates the number of variables in the prefix term and thus the
number of terms that would be retrieved by a pass through the
determined sub-trie.  The full trie always has arity 1, i.e. a full
traversal through the entire trie will produce a single term.  In our
example, the sub-trie determined by the prefix term r(_,_,_), which is
rooted after the @tt{r/3} has arity 3, since starting at that node
will retrieve 3-tuples from the sub-trie; in our example, [a,b,c],
[a,b,d], [a,e,f], [a,f(a,b),c], and [b,g,c].  So the prefix term
r(_,_,_) is natural way to indicate the sub-trie that determines
tuples in the @tt{r/3} table.  As another example, the prefix term
@tt{r(a,f(_,_),_)} determines a sub-trie that contains triples, here
just the single triple, [a,b,c].

The arguments in the call to, say, @pred{union_in_db(P1,P2,P3,DB0,DB)}
that indicate tables, i.e., @var{P1}, @var{P2}, and @var{P3}, can
actually be prefix terms, and they refer to a set of tuples in a
trie-term as described in the previous paragraph.  So, in fact, a
table specification of @pred{r/3}, say, is simply an accepted
short-hand for the actual prefix term @pred{r(_,_,_)}.

").    


/* The following are commented out, and made explicit in the code, so
that this code can itself be loaded by load_in_db and processed. */

%:- import append/3 from basics.
%:- import member/2 from basics.
%:- import log_ith/3 from basics.
%:- import term_hash/3 from machine.
%:- import standard_symbol/3 from std_xsb.
%:- import call_c/1 from standard.
%:- import intern_termhash/2 from machine.

:- comment(assert_in_db/3, "@pred{assert_in_db(+Clause,+DB0,?DB)} adds
a clause, @var{Clause}, (or a sequence of clauses, if @var{Clause} is
a list of clauses) to @var{DB0} to generate @var{DB}. @var{DB} will be
of the same form as @var{DB0}, regular or interned. ").

:- index assert_in_db/3-2.
assert_in_db(Clauses,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    assert_in_db_b(Clauses,DB0,DB).
assert_in_db(Clauses,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    assert_in_db_b(Clauses,DB0,DB),
    machine:intern_termhash(DB,H).
assert_in_db(Clauses,module,module) :-
    (Clauses = [Clause|MClauses]
     ->	assert(Clause),
	assert_list(MClauses)
     ;	assert(Clauses)
    ).

assert_list([]).
assert_list([Clause|Clauses]) :-
    assert(Clause),
    assert_list(Clauses).

assert_in_db_b(Clauses,DB0,DB) :-
    (Clauses == []
     ->	DB0 = DB
     ; Clauses = [Clause|MClauses]
     ->	assert1_in_db_b(Clause,DB0,DB1),
	assert_in_db_b(MClauses,DB1,DB)
     ;	assert1_in_db_b(Clauses,DB0,DB)
    ).

:- comment(assert1_in_db/3, "@pred{assert1_in_db(+Clause,+DB0,?DB)}
asserts a single clause, @var{Clause}, into @var{DB0}, generating
@var{DB}.  Normally @pred{assert_in_db/3} should be used instead of
this predicate.  This predicate in needed @em{only} in cases in which
the ``clause'' to be asserted is itself a list, and so interpreting it
as a list of clauses would be incorrect. The form of @var{DB} will be
the same as the form of @var{DB0}, regular or interned. ").

:- index assert1_in_db/3-2.
assert1_in_db(Clause,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    assert1_in_db_b(Clause,DB0,DB).
assert1_in_db(Clause,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    assert1_in_db_b(Clause,DB0,DB),
    machine:intern_termhash(DB,H).

assert1_in_db_b(Clause,DB0,DB) :-
    (Clause = (Head :- Body)
     ->	true
     ;	Head = Clause,
	Body = true
    ),
    add_to_trie(Head,Body,DB0,DB).

:- comment(assert_in_db/2, "@pred{assert_in_db(+Clause,?DB)} adds a
clause, @var{Clause}, (or a sequence of clauses, if @var{Clause} is a
list) to the empty DB to generate @var{DB}. @var{DB} will be a regular
DB trie term.").

assert_in_db(Clauses,DB) :-
    new_db(DB0),
    assert_in_db(Clauses,DB0,DB).

:- comment(assert_in_dbi/2, "@pred{assert_in_dbi(+Clause,?DB)} adds a
clause, @var{Clause}, (or a sequence of clauses, if @var{Clause} is a
list) to the empty DB to generate @var{DB}.  @var{DB} will be an
interned DB trie term.").

assert_in_dbi(Clauses,DB) :-
    new_dbi(DB0),
    assert_in_db(Clauses,DB0,DB).

:- comment(retractall_in_db/3,"@pred{retractall_in_db(+Goal,+DB0,?DB)}
retracts all clauses whose heads unify with @var{Goal} (or a term in
@var{Goal} if @var{Goal} is a list) from @var{DB0} generating
@var{DB1}.  The form of @var{DB} will be the same as the form
of @var{DB0}. ").

:- index retractall_in_db/3-2.
retractall_in_db(Clauses,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    retractall_in_db_b(Clauses,DB0,DB).
retractall_in_db(Clauses,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    retractall_in_db_b(Clauses,DB0,DB),
    machine:intern_termhash(DB,H).
retractall_in_db(Clauses,module,module) :-
    (Clauses = [Clause|MClauses]
     ->	retractall(Clause),
	retractall_list(MClauses)
     ;	retractall(Clauses)
    ).

retractall_list([]).
retractall_list([Clause|Clauses]) :-
    retractall(Clause),
    retractall_list(Clauses).

retractall_in_db_b(Clauses0,DB0,DB) :-
    (Clauses0 == []
     ->	DB = DB0
     ; Clauses0 = [Clause|Clauses]
     ->	retractall1_in_db(Clause,DB0,DB1),
	retractall_in_db_b(Clauses,DB1,DB)
     ;	retractall1_in_db(Clauses0,DB0,DB)
    ).

retractall1_in_db(Clause,DB0,DB) :-
    (Clause = (Head:-Body)
     ->	true
     ;	Head = Clause
    ),
    remove_from_trie(Head,Body,DB0,_Vars,DB).

:- comment(clause_in_db/3,"@pred{clause_in_db(?Head,?Body,+DB)}
unifies, in turn, @var{Head} and @var{Body} with the head and body of
rules in @var{DB}.  @var{DB} may be either regular or interned.").

:- index clause_in_db/3-3.
clause_in_db(Head,Body,'$TRIETERM'(DB)) :-
    clause_in_db_b(Head,Body,DB).
clause_in_db(Head,Body,'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H),
    clause_in_db_b(Head,Body,DB).
clause_in_db(Head,Body,module) :-
    clause(Head,Body).

clause_in_db_b(Head,Body,DB) :-
    find_in_trie(Head,Body,DB).

:- comment(fact_in_db/2, "@pred{fact_in_db(?Fact,+DB)} unifies, in
turn, @var{Fact} with each fact in DB, @var{DB}.  The order of
returned facts is indeterminate.  @var{DB} may be either regular or
interned.").

fact_in_db(Head,DB) :-
    clause_in_db(Head,true,DB).

:- comment(call_in_db/2, "@pred{call_in_db(+Goal,+DB)} calls the goal
@var{Goal} in the database @var{DB}, binding variables in @var{Goal}
for each instance of @var{Goal} that is proved.  @var{DB} may be
either regular or interned.").

:- index call_in_db/2-2.
call_in_db(Goal,'$TRIETERM'(DB)) :-
    call_in_db_b(Goal,DB).
call_in_db(Goal,'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H),
    call_in_db_b(Goal,DB).
call_in_db(Goal,module) :- call(Goal).

call_in_db_b(true,_DB) :- !.
call_in_db_b((A,B),DB) :- !, call_in_db_b(A,DB), call_in_db_b(B,DB).
call_in_db_b(\+(G),DB) :- !, \+ call_in_db_b(G,DB).
call_in_db_b(tnot(G),DB) :- !, tnot db_table_call(DB,G).
call_in_db_b(findall(T,G,L),DB) :- !, findall(T,call_in_db_b(G,DB),L).
call_in_db_b((do_all G),DB) :- !, (do_all call_in_db_b(G,DB)).
call_in_db_b((C->A;B),DB) :- !,
    (call_in_db_b(C,DB) -> call_in_db_b(A,DB) ; call_in_db_b(B,DB)).
call_in_db_b((A;B),DB) :- !, (call_in_db_b(A,DB) ; call_in_db_b(B,DB)).
call_in_db_b(table(Goal),DB) :- !, db_table_call(DB,Goal).
call_in_db_b(xact(G),DB) :- !, phrase(G,DB,DB).  % for transaction logic, if useful.
call_in_db_b(G,_DB) :- functor(G,F,A), std_xsb:standard_symbol(F,A,_), !,
    standard:call_c(G).
call_in_db_b(G,DB) :-
    clause_in_db_b(G,B,DB),
    call_in_db_b(B,DB).

:- table db_table_call/2.
db_table_call(DB,Goal) :-
    call_in_db_b(Goal,DB).

:- comment(dump_db/2,"@var{dump_db(+DB,+Filename)} writes the clauses
in DB, @var{DB}, to the file @var{Filename}.  If @var{Filename} is
'userout', the clauses will be written to userout.  @var{DB} may be
either regular or interned.").

dump_db('$TRIETERM'(DB),FileName) :-
    dump_db_b(DB,FileName).
dump_db('$TRIETERMI'(H),FileName) :-
    machine:intern_termhash(DB,H),
    dump_db_b(DB,FileName).

dump_db_b(DB,Filename) :-
    (Filename == userout
     ->	OStr = userout
     ;	open(Filename,write,OStr)
    ),
    (do_all
     clause_in_db_b(Head,Body,DB),
     (Body == true
      -> write_canonical_lettervar(OStr,Head)
      ;	 write_canonical_lettervar(OStr,(Head:-Body))
     ),
     writeln(OStr,'.')
    ),
    (Filename == userout
     ->	true
     ;	close(OStr)
    ).

:- comment(pure_call_in_db/2,"@pred{pure_call_in_db(+G,+DB)} has the
same functionality as @pred{call_in_db(G,DB)}, but it is implemented
without cuts, so it can be used when this file is loaded into a DB by
@pred{load_in_db/2}.  @var{DB} may be either regular or interned.").

:- index pure_call_in_db/2-2.
pure_call_in_db(G,'$TRIETERM'(DB)) :-
    pure_call_in_db_b(G,DB).
pure_call_in_db(G,'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H),
    pure_call_in_db_b(G,DB).

pure_call_in_db_b(G,DB) :-
    (ctrl_str(G)
     ->	pure_ctrl_call_in_db_b(G,DB)
     ;	G \== true,functor(G,F,A),std_xsb:standard_symbol(F,A,_)
     ->	standard:call_c(G)
     ;	clause_in_db_b(G,B,DB),
	pure_call_in_db_b(B,DB)
    ).

ctrl_str(true).
ctrl_str((_,_)).
ctrl_str(\+(_)).
ctrl_str(xact(_)).
ctrl_str((_;_)).

pure_ctrl_call_in_db_b(true,_).
pure_ctrl_call_in_db_b((A,B),DB) :- pure_call_in_db_b(A,DB), pure_call_in_db_b(B,DB).
pure_ctrl_call_in_db_b(\+(G),DB) :- \+ pure_call_in_db_b(G,DB).
pure_ctrl_call_in_db_b(xact(G),DB) :- !, phrase(G,DB,DB).  % for transaction logic, if useful.
pure_ctrl_call_in_db_b((A;B),DB) :-
    (A = (C->A1)
     ->	(pure_call_in_db_b(C,DB) -> pure_call_in_db_b(A1,DB) ; pure_call_in_db_b(B,DB))
     ;	(pure_call_in_db_b(A,DB) ; pure_call_in_db_b(B,DB))
    ).
pure_ctrl_call_in_db_b(M:G,_DB) :- call(M:G).

:- comment(new_db/1, "@pred{new_db(?DB)} generates (or tests for) and
empty trie-term database, @var{DB}, represented as a Prolog trie
term.").

new_db('$TRIETERM'([])).

:- comment(new_dbi/1, "@pred{new_db(?DB)} generates (or tests for) and
empty interned trie-term database, @var{DB}, represented in interned
form.").

new_dbi('$TRIETERMI'([])).

:- comment(new_db_like/2, "@pred{new_db_like(+DB0,-DB)} generates an
empty database in @var{DB} in the same form as the input database
@var{DB0}. ").

new_db_like('$TRIETERM'(_),'$TRIETERM'([])).
new_db_like('$TRIETERMI'(_),'$TRIETERMI'([])).

:- comment(is_empty_db/1, "@pred{is_empty_db(+DB)} is true if @var{DB}
represents an empty database (of either regular or interned form.) ").

is_empty_db('$TRIETERM'([])).
is_empty_db('$TRIETERMI'([])).

is_empty_db_b([]).

:- comment(cvt_db_to_i/2, "@pred{cvt_db_to_i(+DB,-H)} converts a
regular database @var{DB} to an interned database @var{H}. ").

cvt_db_to_i('$TRIETERM'(DB),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H).

:- comment(cvt_db_from_i/2, "@pred{cvt_db_to_i(+H,-DB)} converts an
interned database @var{H} to a regular term database @var{H}. ").

cvt_db_from_i('$TRIETERMI'(H),'$TRIETERM'(DB)) :-
    machine:intern_termhash(DB,H).

:- comment(is_db/1, "@pred{is_db(+DB)} succeeds if @var{DB} has the
form of a valid database term, of either regular or interned form."  ).

is_db('$TRIETERM'(_)).
is_db('$TRIETERMI'(_)).

:- comment(find_in_trie/3, "find_in_trie(?Head,?Body,+Trie) succeeds
for terms @var{Head} in ht-trie @var{Trie} with body @var{Body}.
@var{Trie} is an unwrapped trie term.").

find_in_trie(Head,Body,Trie) :-
    find_in_trie_list([Head],Body0,Trie,Vars),
    my_unnumbervars(Body0,Vars,Body).

:- comment(find_in_trie_list/4,
"find_in_trie_list(?HeadTail,?Body,+Trie,?Vars) succeeds for the
trie-path selected by the list of terms @var{HeadTail} (which must be
a proper list) in ht-trie @var{Trie} with clause body @var{Body},
where @vars{Vars} is an open loglist of variables.").

find_in_trie_list([],Body,Bodies,_Vars) :-
    basics:member(Body,Bodies).
find_in_trie_list([HeadTerm|HeadTail],Body,Trie,Vars) :-
    find_in_first_hashtree(HeadTerm,SubTrie,Trie),
    \+ (HeadTerm = '$VAR'(VN),integer(VN)),	%next clause handles this case
    (atomic(HeadTerm)
     ->	find_in_trie_list(HeadTail,Body,SubTrie,Vars)
     ;	HeadTerm =.. [_|SubHeads],
	basics:append(SubHeads,HeadTail,NewHeadTail),
	find_in_trie_list(NewHeadTail,Body,SubTrie,Vars)
    ).
find_in_trie_list([HeadTerm|HeadTail],Body,Trie,Vars) :-
    find_in_first_hashtree('$VAR'(X),SubTrie,Trie),
    basics:log_ith(X,Vars,HeadTerm),
    find_in_trie_list(HeadTail,Body,SubTrie,Vars).

:- comment(add_to_trie/4, "add_to_trie(+Head,+Body,+Trie0,-Trie) adds
the clause with head @var{Head} and body @var{Body} to ht-trie @var{Trie0},
returning the ht-trie @var{Trie}.").

add_to_trie(Head,Body,Trie0,Trie) :-
    (ground(Head-Body)
     ->	add_to_trie_ground_list([Head],Body,Trie0,Trie)
     ;	copy_term([Head|Body],[Head1|Body1]),
	numbervars([Head1|Body1],0,_),
	add_to_trie_ground_list([Head1],Body1,Trie0,Trie)
    ).

:- comment(add_to_trie_ground_list/4,
"add_to_trie_ground_list(+HeadTail,+Body,+Trie0,-Trie) adds the
trie-path for the list of terms, @var{HeadTail}, with body,
@var{Body}, to ht-trie, @var{Trie0}, returning ht-trie, @var{Trie}.").

add_to_trie_ground_list([],Body,List,MList) :-
    add_to_body_list(List,Body,MList).
add_to_trie_ground_list([HeadTerm|HeadTail],Body,Trie0,Trie) :-
    (atomic(HeadTerm)
     ->	NHeadTail = HeadTail
     ;	HeadTerm = '$VAR'(VN),integer(VN)
     ->	NHeadTail = HeadTail
     ;	HeadTerm =.. [_|SubTerms],
	basics:append(SubTerms,HeadTail,NHeadTail)
    ),
    update_hashtree(HeadTerm,NewSubTrie,OldSubTrie,Trie0,Trie1),
    add_to_trie_ground_list(NHeadTail,Body,OldSubTrie,NewSubTrie),
    db_comps_d(Trie1,HashBits,NumInTab,HashTree),
    (NumInTab < floor(2**HashBits)
     ->	Trie = Trie1
     ;	NHashBits is HashBits*2,
	db_comps_c(Trie,NHashBits,NumInTab,NHashTree),
	rebuild_hashtree(HashTree,HashBits,NHashBits,NHashTree)
    ).

:- comment(add_to_body_list/3, "add_to_body_list(+List,+Body,-NewList)
adds @var{Body} to the ordered list @var{List}, returning a new
ordered list containing @var{Body} in @var{NewList}.  If @var{Body} is
already in List, List is returned.").

add_to_body_list([],Body,[Body]).
add_to_body_list([Body0|List],Body,MList) :-
    (Body @< Body0
     ->	MList = [Body,Body0|List]
     ;	Body == Body0
     ->	MList = [Body0|List]
     ;	MList = [Body0|MList1],
	add_to_body_list(List,Body,MList1)
    ).

:- comment(find_in_first_hashtree/3,
"@pred{find_in_first_hashtree(?Term,-Val,+Trie)} returns in @var{Val}
the value associated with key, @var{Term}, found in the first
hash-tree in the ht-trie, @var{Trie}.  It traverses through the first
hash-tree, to return either another ht-trie, if it is internal in the
full trie, or the list of bodies, if it is the last hash-tree of a
full ht-trie.").

find_in_first_hashtree(Term,Val,Trie) :-
    db_comps_d(Trie,HashBits,_NumInTab,HashTree),
    (var(Term)
     ->	bind_from_hash(Term,Val,HashBits,HashTree)
     ;	Size is floor(2**HashBits),
	machine:term_hash(Term,Size,HashCode),
	(atomic(Term)
	 -> Key = Term
	 ; Term = '$VAR'(_)
	 -> Key = Term
	 ;  standard:functor(Term,Mod,Fun,Ari),
	    Key = functor(Mod,Fun,Ari)
	),
	find_in_subtree(Key,Val,HashCode,HashBits,HashTree)
    ).

:- comment(find_in_subtree/5,
"find_in_subtree(?Key,-Rec,+HashCode,+HashBits,+HashTree) returns in
@var{Rec} the value associates with key, @var{Key}, which has
hashcode, @var{HashCode}, in hash-tree, @var{HashTree}, whose keys have
@var{HashBits} bits.  It recurses through the hash-tree index.").

find_in_subtree(Key,Rec,HashCode,HashBits,HashTree) :-
    (HashBits =< 0
     ->	my_member((Key-Rec),HashTree)
     ;	HashTree = h(HashTree0,HashTree1,HashTree2,HashTree3),
	NHashBits is HashBits - 2,
	HashBit is HashCode /\ 3,
	NHashCode is HashCode >> 2,
	(HashBit < 2
	 -> (HashBit < 1
	     ->	find_in_subtree(Key,Rec,NHashCode,NHashBits,HashTree0)
	     ;	find_in_subtree(Key,Rec,NHashCode,NHashBits,HashTree1)
	    )
	 ;  HashBit < 3
	 -> find_in_subtree(Key,Rec,NHashCode,NHashBits,HashTree2)
	 ;  find_in_subtree(Key,Rec,NHashCode,NHashBits,HashTree3)
	)
    ).

:- comment(bind_from_hash/4,
"bind_from_hash(-Term,-Rec,+HashBits,+HashTree) nondeterministically
binds the variables, @var{Term} and @var{Rec}, to the key-value pairs in
hash-tree, @var{HashTree}, whose keys have @var{HashBits} bits.").

bind_from_hash(Term,Val,HashBits,HashTree) :-
    HashTree \== [],
    (HashBits =< 0
     ->	my_member((Key-Val),HashTree),
	(Key = functor(Mod,Fun,Arity)
	 -> standard:functor(Term,Mod,Fun,Arity)
	 ;  Term = Key
	)
	;  HashTree = h(HashTree0,HashTree1,HashTree2,HashTree3),
	NHashBits is HashBits - 2,
	(bind_from_hash(Term,Val,NHashBits,HashTree0)
	 ;  
	 bind_from_hash(Term,Val,NHashBits,HashTree1)
	 ;  
	 bind_from_hash(Term,Val,NHashBits,HashTree2)
	 ;  
	 bind_from_hash(Term,Val,NHashBits,HashTree3)
	)
    ).

%% deterministic for common case of length 1 list (list must be +)
my_member(X,[Y|L]) :-
    (L == []
     ->	X = Y
     ;	(X = Y
	 ;  
	 my_member(X,L)
	)
    ).

:- comment(update_hashtree/5,
"update_hashtree(+Term,?NewVal,-OldVal,+HashTree0,-HashTree) adds a new
key-value pair (or finds an existing pair), @var{Term} and
@var{NewVal}, to the hash-tree, @var{HashTree0}, producing the new
hash-tree, @var{HashTree}.  @var{OldVal} is bound to the previous
value for this key in the hash table (or to [] if this is a new
key.)").

update_hashtree(Term,NewVal,OldVal,HT0,HT) :-
    db_comps_d(HT0,HashBits,NumInTab,HashTree),
    Size is floor(2**HashBits),
    machine:term_hash(Term,Size,HashCode),
    (atomic(Term)
     ->	Key = Term
     ;	Term = '$VAR'(VN),integer(VN)
     ->	Key = Term
     ;	standard:functor(Term,Mod,Fun,Ari),
	Key = functor(Mod,Fun,Ari)
    ),
    update_subtree(Key,NewVal,OldVal,HashCode,HashBits,Added,HashTree,NHashTree),
    NNumInTab is NumInTab+Added,
    db_comps_c(HT,HashBits,NNumInTab,NHashTree).

:- comment(update_subtree/8,
"update_subtree(+Key,+NewVal,-OldVal,+HashCode,+HashBits,-Added,+HashTree,-NHashTree)
recurses through @var{HashTree} to add a key-value pair, in @var{Key}
and @var{NewVal}, to the hash-tree, @var{HashTree}, generating a new
hash-tree, @var{NHashTree}.  @var{OldVal} is returned and is the value
for this key in @var{HashTree} (or [] if the key is new.)
@var{HashCode} is the hash code for @var{Key}; @var{HashBits} is the
number of bits in the @var{HashCode} (and in keys in @var{HashTree}),
and @var{Added} is returned as 1 if a new key is added, and 0 if the
key already existed.").

update_subtree(Key,NewVal,OldVal,HashCode,HashBits,Added,HashTree,NHashTree) :-
    (HashBits =< 0
     ->	(NewVal == []
	 -> remove_from_ordered_list(HashTree,Key,OldVal,NHashTree),
	    Added = -1
	 ;  add_to_ordered_list(HashTree,Key-NewVal,OldVal,Added,NHashTree)
	)
     ;	h_comps_d(HashTree,HashTree0,HashTree1,HashTree2,HashTree3),
	NHashBits is HashBits - 2,
	HashBit is HashCode /\ 3,
	NHashCode is HashCode >> 2,
	(HashBit < 2
	 -> (HashBit < 1
	     ->	NHashTree = h(NHashTree0,HashTree1,HashTree2,HashTree3),
		update_subtree(Key,NewVal,OldVal,NHashCode,NHashBits,Added,HashTree0,NHashTree0)
	     ;	NHashTree = h(HashTree0,NHashTree1,HashTree2,HashTree3),
		update_subtree(Key,NewVal,OldVal,NHashCode,NHashBits,Added,HashTree1,NHashTree1)
	    )
	 ;  HashBit < 3
	 -> NHashTree = h(HashTree0,HashTree1,NHashTree2,HashTree3),
	    update_subtree(Key,NewVal,OldVal,NHashCode,NHashBits,Added,HashTree2,NHashTree2)
	 ;  NHashTree = h(HashTree0,HashTree1,HashTree2,NHashTree3),
	    update_subtree(Key,NewVal,OldVal,NHashCode,NHashBits,Added,HashTree3,NHashTree3)
	)
    ).

:- comment(add_to_ordered_list/5,
"add_to_ordered_list(+OValues,+Key-NewVal,-OldVal,-Added,-NValues)
adds (or replaces) a key-value pair in an ordered list of key-value
pairs (where the key will be an atom, $VAR, or functor(Mod,Fun,Ari)
and the value will be a ht-trie.)  @var{OValues} is the old list of
key-value pairs (represented as (@var{Key} - @var{Value}).) The new
key-value pair is (@var{Key} - @var{NewVal}). @var{NValues} is the
resulting list of key-value pairs.  @var{OldVal} is the old value for
key @var{Key} in @var{OValues} (or [] if none), and is returned.
@var{Added} is 1 if the key @var{Key} is new, and 0 if it already
exists in a pair in @var{OValues}.").

add_to_ordered_list([],KeyNVal,[],1,[KeyNVal]).
add_to_ordered_list([OValue|Values],KeyNVal,OldVal,Added,NValues) :-
    OValue = (TKey-TVal),
    KeyNVal = (Key-_NewVal),
    (TKey @< Key
     ->	NValues = [OValue|NValues1],
	add_to_ordered_list(Values,KeyNVal,OldVal,Added,NValues1)
     ;	TKey == Key
     ->	NValues = [KeyNVal|Values],
	Added = 0,
	OldVal = TVal
     ;	NValues = [KeyNVal,OValue|Values],
	Added = 1,
	OldVal = []
    ).

:- comment(remove_from_trie/5,
"remove_from_trie(+Term,+Value,+Trie0,-Vars,-Trie) removes the term,
@var{Term}, with value, @var{Value}, from the trie @var{Trie0}
yielding @var{Trie}, where @var{Vars} is a log-list of variable
bindings.").

remove_from_trie(Term,Value,Trie0,Vars,Trie) :-
    remove_from_trie_list([Term],Value,Trie0,Vars,Trie).

:- comment(remove_from_trie_list/5,
"remove_from_trie_list(+TermList,?Body,+Trie0,-Vars,-Trie) recurses
through the trie to remove the clause, the remainder of its head is
@var{TermList}, whose body is @var{Body}, from the trie, @var{Trie0},
yielding trie, @var{Trie}. @var{Vars} is a log-list of variable
bindings.").

remove_from_trie_list([],Body,OBodies,_Vars,NBodies) :-
    (nonvar(Body)
     ->	remove_from_ordered_list(OBodies,Body,NBodies)
     ;	NBodies = []
    ).
remove_from_trie_list([Term|TermList],Body,Trie0,Vars,Trie) :-
    (is_most_general_term([Body,Term|TermList])
     ->	Trie = []		% delete everything below!
     ;	(nonvar(Term), Term = '$VAR'(VN), integer(VN)
	 -> basics:log_ith(VN,Vars,NTerm)
	 ;  NTerm = Term
	),
	findall([NTerm|TermList],
		(find_in_first_hashtree(NTerm,_,Trie0),
		 \+ (NTerm = '$VAR'(VN0), integer(VN0))
		),
		TermLists),
	remove_from_trie_lists(TermLists,NTerm,Body,Trie0,Vars,Trie1),
	findall(['$VAR'(VN1)|TermList],
		find_in_first_hashtree('$VAR'(VN1),_,Trie0),
		VTermLists),
	remove_from_trie_lists(VTermLists,NTerm,Body,Trie1,Vars,Trie)
    ).

:- comment(remove_from_trie_lists/6,
"@pred{remove_from_trie_list(+TermLists,+Term,?Body,+Trie0,-Vars,-Trie)}
removes each trie-path for each list in the list, @var{TermLists},
each with clause body, @var{Body}, from the trie, @var{Trie0},
yielding trie, @var{Trie}. @var{Term} is the term at this point in the
input term, used to bind a potential variable in the trie.  @var{Vars}
is a log-list of variable bindings.").

remove_from_trie_lists([],_,_,Trie,_Vars,Trie).
remove_from_trie_lists([TL|TLs],Term,Body,Trie0,Vars,Trie) :-
    (TL = ['$VAR'(VN)|_]
     ->	(copy_term(TL-Vars,NTL-NVars),
	 basics:log_ith(VN,NVars,Term) % new vars
	 -> remove_from_trie_list_nv(NTL,Body,Trie0,NVars,Trie1)
	 ;  Trie1 = Trie0
	)
     ;	remove_from_trie_list_nv(TL,Body,Trie0,Vars,Trie1)
    ),
    remove_from_trie_lists(TLs,Term,Body,Trie1,Vars,Trie).

:- comment(remove_from_trie_list_nv/5,
"@pred{remove_from_trie_list_nv(+TermList,+Body,+Trie0,+Vars,-Trie)}
removes an trie-path, corresponding to @var{TermList}, with body
@var{Body}, from ht-trie, @var{Trie0}, yielding ht-trie, @var{Trie}.
@var{Vars} is a log-list of variable bindings to handle ($VAR)
variables in the input ht-trie, @var{Trie0}.").

remove_from_trie_list_nv([Term|TermList],Body,Trie0,Vars,Trie) :-
    (atomic(Term)
     ->	NTermList = TermList
     ; Term = '$VAR'(_)
     ->	NTermList = TermList
     ;	Term =.. [_|SubTerms],
	basics:append(SubTerms,TermList,NTermList)
    ),
    update_hashtree(Term,NewSubTrie,OldSubTrie,Trie0,Trie1),
    remove_from_trie_list(NTermList,Body,OldSubTrie,Vars,NewSubTrie),
    ((NewSubTrie == [] ; NewSubTrie = ht(_,_,[]))
     ->	remove_from_hash_trie(Term,_OldVal,Trie0,Trie)
     ;	Trie = Trie1
    ).
	
:- comment(remove_from_hash_trie/4,
"@pred{remove_from_hash_trie(+Term,?OldVal,+Trie0,-Trie)} removes the
entry for the pair Term and OldVal from the hash table, HashTree0,
yielding the hash table, HashTree.").

remove_from_hash_trie(Term,OldVal,Trie0,Trie) :-
    db_comps_d(Trie0,HashBits,NumInTab,HashTree),
    Size is floor(2**HashBits),
    machine:term_hash(Term,Size,HashCode),
    (atomic(Term)
     ->	Key = Term
     ; Term = '$VAR'(I), integer(I)
     ->	Key = Term
     ;	standard:functor(Term,Mod,Fun,Ari),
	Key = functor(Mod,Fun,Ari)
    ),
    NNumInTab is NumInTab - 1,
    remove_from_subtree(Key,OldVal,HashCode,HashBits,HashTree,NHashTree0),
    MinSize is floor(2** (HashBits//2)),
    ((NNumInTab >= MinSize ; Size =< 4)
     ->	NHashTree = NHashTree0,
	NHashBits = HashBits
     ;	NHashBits is HashBits // 2,
	rebuild_hashtree(NHashTree0,HashBits,NHashBits,NHashTree)
    ),
    db_comps_c(Trie,NHashBits,NNumInTab,NHashTree).

:- comment(remove_from_subtree/6,
"@pred{remove_from_subtree(+Key,?OldVal,+HashCode,+HashBits,+HashTree,-NHashTree)}
removes the entry for the pair @var{Term} and @var{OldVal} from the
hash tree, @var{HashTree}, yielding the hash tree,
@var{NHashTree}. @var{HashCode} is the hash code for @var{Key} and
@var{HashBits} is the number of bits in the hash code (and in all hash
codes for entries in @var{HashTree}).").

remove_from_subtree(Key,OldVal,HashCode,HashBits,HashTree,NHashTree) :-
    (HashBits =< 0
     ->	remove_from_ordered_list(HashTree,Key,OldVal,NHashTree)
     ;	h_comps_d(HashTree,HashTree0,HashTree1,HashTree2,HashTree3),
	NHashBits is HashBits - 2,
	HashBit is HashCode /\ 3,
	NHashCode is HashCode >> 2,
	(HashBit < 2
	 -> (HashBit < 1
	     ->	remove_from_subtree(Key,OldVal,NHashCode,NHashBits,HashTree0,NHashTree0),
		h_comps_c(NHashTree,NHashTree0,HashTree1,HashTree2,HashTree3)
	     ;	remove_from_subtree(Key,OldVal,NHashCode,NHashBits,HashTree1,NHashTree1),
		h_comps_c(NHashTree,HashTree0,NHashTree1,HashTree2,HashTree3)
	    )
	 ;  HashBit < 3
	 -> remove_from_subtree(Key,OldVal,NHashCode,NHashBits,HashTree2,NHashTree2),
	    h_comps_c(NHashTree,HashTree0,HashTree1,NHashTree2,HashTree3)
	 ;  remove_from_subtree(Key,OldVal,NHashCode,NHashBits,HashTree3,NHashTree3),
	    h_comps_c(NHashTree,HashTree0,HashTree1,HashTree2,NHashTree3)
	)
    ).
:- comment(remove_from_ordered_list/4,
"@pred{remove_from_ordered_list(+OValues,+Key,?OldVal,-NValues)}
removes the pair, (@var{Key} - @var{OldVal}), from the ordered list of
pairs in @var{OValues}, returning the resulting ordered list in
@var{NValues}.  It fails if the pair is not in the original list.").

remove_from_ordered_list([OValue|OValues],Key,OldVal,NValues) :-
    (OValue = (Key-OldVal)
     ->	NValues = OValues
     ;	NValues = [OValue|NValues1],
	remove_from_ordered_list(OValues,Key,OldVal,NValues1)
    ).

:- comment(remove_from_ordered_list/3,
"@pred{remove_from_ordered_list(+OValues,+Value,-NValues)} removes
@var{Value} from the list of values in @var{OValues} returning the
list @var{NValues}, failing if it is not there.  This is used at the
leaves of a trie, to remove clause bodies.").

remove_from_ordered_list([OVal|Values],Value,NValues) :-
    (OVal == Value
     ->	NValues = Values
     ;	NValues = [OVal|NValues1],
	remove_from_ordered_list(Values,Value,NValues1)
    ).

:- comment(rebuild_hashtree/4,
"@pred{rebuild_hashtree(+HashTree0,+HashBits,+NewBitSize,-HashTree)}
rebuilds an hash-tree with a different hash-bit size (or width).  This
allows either fixing a hash-tree to be of the right 'width', or to
change the width to agree with another hash-tree one wants to compare
with.  @var{HashTree0} is the old hash-tree with hash-bit size of
@var{HashBits}, @var{NewBitSize} is the new hash-bit size, and
@var{HashTree} is the new re-sized hash-tree. ").

rebuild_hashtree(HashTree0,HashBits,NewBitSize,HashTree) :-
    findall((Term-Val),bind_from_hash(Term,Val,HashBits,HashTree0),TermVals),
    add_all_terms(TermVals,NewBitSize,0,_,[],HashTree).

add_all_terms([],_,N,N,HT,HT).
add_all_terms([(Term-Val)|Terms],HashBits,N0,N,HT0,HT) :-
    Size is floor(2**HashBits),
    machine:term_hash(Term,Size,HashCode),
    (atomic(Term)
     ->	Key = Term
     ;	standard:functor(Term,Mod,Fun,Arity),
	Key = functor(Mod,Fun,Arity)
    ),	 
    update_subtree(Key,Val,_,HashCode,HashBits,Added,HT0,HT1),
    N1 is N0+Added,
    add_all_terms(Terms,HashBits,N1,N,HT1,HT).

:- comment(load_in_db/2, "@pred{load_in_db(+FileName,-DB)} reads the
clauses in @file{FileName} and creates the database @var{DB} that
contains them.  @var{DB} will be of regular term form.").

load_in_db(FileName,DB) :-
    new_db(DB0),
    load_in_db(FileName,DB0,DB).

:- comment(load_in_dbi/2, "@pred{load_in_dbi(+FileName,-DB)} reads the
clauses in @file{FileName} and creates the database @var{DB} that
contains them.  @var{DB} will be of interned form.").

load_in_dbi(FileName,H) :-
    new_dbi(H0),
    load_in_db(FileName,H0,H).

:- comment(load_in_db/3, "@pred{load_in_db(+FileName,+DB0,-DB)} reads
the clauses in @file{FileName} and adds them to the database
@var{DB0}, creating a new database @var{DB}.  @var{DB} will be of the
same form as @var{DB0}.  ").

load_in_db(FileName,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    load_in_db_b(FileName,DB0,DB).
load_in_db(FileName,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    load_in_db_b(FileName,DB0,DB),
    machine:intern_termhash(DB,H).

:- comment(load_in_db/4, "
@pred{load_in_db(+FileName,+ExclSet,+DB0,?DB)} reads the clauses in
@var{FileName} and, except for those that unify with clauses in the DB
@var{ExclSet}, asserts them into @var{DB0} generating @var{DB}.  The
most common use of the @var{ExclSet} is to avoid adding unwanted
directives.  ").

load_in_db(FileName,'$TRIETERM'(Excl),'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    load_in_db_b(FileName,Excl,DB0,DB).
load_in_db(FileName,'$TRIETERMI'(HExcl),'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    machine:intern_termhash(EDB,HExcl),
    load_in_db_b(FileName,EDB,DB0,DB),
    machine:intern_termhash(DB,H).

load_in_db_b(FileName,DB0,DB) :-
    is_empty_db_b(EDB),
    load_in_db_b(FileName,EDB,DB0,DB).

load_in_db_b(FileName,Excludes,DB0,DB) :-
    open(FileName,read,IStr),
    read_terms_to_trie(IStr,Excludes,DB0,DB),
    close(IStr).

read_terms_to_trie(IStr,Excludes,DB0,DB) :-
    read(IStr,Term),
    (Term == end_of_file
     ->	DB = DB0
     ;	(clause_in_db_b(Term,true,Excludes)
	 -> DB1 = DB0
	 ;  assert_in_db_b(Term,DB0,DB1)
	),
	read_terms_to_trie(IStr,Excludes,DB1,DB)
    ).

my_unnumbervars(Term,Vars,NTerm) :-
    (var(Vars)			% no vars, so term must be ground
     ->	NTerm = Term
     ; var(Term)
     ->	NTerm = Term
     ;	atomic(Term)
     ->	NTerm = Term
     ;	Term = '$VAR'(I),integer(I)
     ->	basics:log_ith(I,Vars,NTerm)
     ;	standard:functor(Term,Mod,Fun,Ari),
	standard:functor(NTerm,Mod,Fun,Ari),
	Term =.. [Fun|Args],
	NTerm =.. [Fun|NArgs],
	my_unnumbervars_list(Args,Vars,NArgs)
    ).

my_unnumbervars_list([],_Vars,[]).
my_unnumbervars_list([Term|Terms],Vars,[NTerm|NTerms]) :-
    my_unnumbervars(Term,Vars,NTerm),
    my_unnumbervars_list(Terms,Vars,NTerms).

/**********************************************************/

:- comment(find_hashtable_for_prefix/4,
"@pred{find_hashtable_for_prefix(+HeadTail,+HT,-TailLen,-SHT)}
searches for the ht-trie associated in the ht-trie @var{HT} with the
first subterm of @var{HeadTail} that is a variable.  @var{HeadTail}
must consist (when sequentialized) of a sequence of bound terms
followed by a sequence of distinct variables.  If this condition does
not hold, the predicate fails.  The ht-trie that is found is returned
in @var{SHT}, and the number of variables remaining in the tail is
returned in @var{TailLen}.").

find_hashtable_for_prefix([HeadTerm|HeadTail],HT,TailLen,SHT) :-
    nonvar(HeadTerm),
    find_in_first_hashtree(HeadTerm,SHT0,HT),
    (atomic(HeadTerm)
     ->	NHeadTail = HeadTail
     ;	HeadTerm =.. [_|SubTerms],
	basics:append(SubTerms,HeadTail,NHeadTail)
    ),
%    NHeadTail \== [],
    (is_most_general_term(NHeadTail)
     ->	basics:length(NHeadTail,TailLen),
	SHT = SHT0
     ;	find_hashtable_for_prefix(NHeadTail,SHT0,TailLen,SHT)
    ).

:- comment(update_hashtable/4,
"@pred{update_hashtable_for_prefix(+HeadTail,+NSHT,+OSHT,-TailLen,+HT0,-HT)}
replaces the ht-trie that is associated in the ht-trie @var{HT0} with
the first variable subterm of @var{HeadTail} with the ht-trie
@var{NSHT}.  The ht-trie that is replaced is returned in @var{OSHT}.
The new ht-trie with the replaced subterm is returned in @var{HT}.
@var{HeadTail} must consist (when sequentialized) of a sequence of
bound terms followed by a sequence of distinct variables.  If this
condition does not hold, the predicate fails.  ").

%% DSW: can we combine this with assert_ref_with_trie, and have both efficient?
update_hashtree_for_prefix(HeadTail,NSHT,OSHT,TailLen,HT0,HT) :-
    (NSHT == []
     ->	check_prefix_term(HeadTail,TailLen,'*_in_db/5'),
	remove_from_trie_list(HeadTail,_,HT0,_,HT)
     ;	update_hashtree_for_prefix_1(HeadTail,NSHT,OSHT,TailLen,HT0,HT)
    ).

update_hashtree_for_prefix_1([HeadTerm|HeadTail],NSHT,OSHT,TailLen,HT0,HT) :-
    nonvar(HeadTerm),
    NSHT \== [],
    (atomic(HeadTerm)
     ->	NHeadTail = HeadTail
     ;	HeadTerm =.. [_|SubTerms],
	basics:append(SubTerms,HeadTail,NHeadTail)
    ),
    NHeadTail \== [],
    (is_most_general_term(NHeadTail)
     ->	basics:length(NHeadTail,TailLen),
	update_hashtree(HeadTerm,NSHT,OSHT,HT0,HT)
     ;	update_hashtree(HeadTerm,NSHT0,OSHT0,HT0,HT),
	update_hashtree_for_prefix_1(NHeadTail,NSHT,OSHT,TailLen,OSHT0,NSHT0)
    ).


check_taillen(T1,T2,HeadTail,MsgPred) :-
    (T1 = T2
     ->	true
     ;	misc_error(('[',MsgPred,'] Predicate selection pattern: ',HeadTail,
		    ' is inconsistent with other patterns.'))
    ).

check_prefix_term(HeadTail,TailLen,MsgPred) :-
    (is_prefix_term(HeadTail,TailLen0)
     ->	check_taillen(TailLen,TailLen0,HeadTail,MsgPred)
     ;	misc_error(('[',MsgPred,'] Predicate selection pattern: ',HeadTail,' is illegal.'))
    ).

:- comment(is_prefix_term/2,"
@pred{is_prefix_term(+HeadTail,?TailLen)} checks that @var{HeadTail}
is a @tt{prefix-term}.  A @tt{prefix term} is a prolog term that, when
viewed in prefix order, consists of a sequence of constants followed
by a sequence of distinct variables.  Such a term uniquely determines
a position in a term trie (i.e., prolog DB). ").

is_prefix_term(HeadTail,TailLen) :-
    prefix_term(HeadTail,_Pre,Tail),
    basics:length(Tail,TailLen).

prefix_term(PT,Prefix,TailVars) :-
    PT = [Head|Tail],
    (var(Head)
     ->	is_most_general_term(PT),
	TailVars = PT,
	Prefix = []
     ; atomic(Head)
     ->	Prefix = [Head|PrefixT],
	prefix_term(Tail,PrefixT,TailVars)
     ;	Head =.. [_Functor|Args],
	basics:append(Args,Tail,ArgsTail),
	standard:functor(Head,Mod,Functor,Arity),
	Prefix = [functor(Mod,Functor,Arity)|PrefixT],
	prefix_term(ArgsTail,PrefixT,TailVars)
    ).


:- comment(apply_op_in_db/6,
"@pred{apply_op_in_db(+OpLambda,+P1,+P2,+P3,+DB0,?DB)} applies a
binary database operation (union, intersect, subtract, etc.) to two
predicates in a database and puts the result into another predicate in
the database.  This is an internal predicate.  For example, to union
the tuples of p/3 and q/3 and put the result into r/3, one could do:

@begin{verbatim}
| ?- assert_in_db([p(a,c,d),p(a,d,e),q(a,c,d),q(b,c,d)],DB1),
	   apply_op_in_db(lambda(A,B,C,union_db_b(A,B,C)),p/3,q/3,r/3,DB1,DB2),
	   dump_db(DB2,userout).
@end{verbatim}

Here we have put tuples for @tt{p/3} and @tt{q/3} into @var{DB1}.
Then we union those two predicates, putting the result into a
predicate @tt{r/3} in the new database @var{DB2}.  And then we print
out the resulting database.  We must use the *_b form for union, since
it takes an unwrapped trie-term as required.

@var{Pref1}, @var{Pref2}, and @var{Pref3} must be prefix terms.  (For
convenience, the form @tt{Pred/Arity} is converted to a prefix term
using @pred{functor/3}.  Clearly, this is the most commonly used form
of prefix terms.)  The operation of @var{OpLambda} is of the form
@tt{lambda(Var1,...,Varn,Goal)}, which is called by binding the
arguments to @var{Var2} through @var{Varn} and then @tt{call}-ing
@var{Goal}.  The operation must take 2 input trie (databases) and
produce an output trie (database.)  The operation will be applied to
the two tries determined in the input database, @var{DB0}, by the two
prefix terms, and the resulting trie will replace the trie determined
by the target prefix term, creating a new database @var{DB}.  (The
target prefix term will be added if it is not in the input database,
@var{DB0}.)  ").

:- index apply_op_in_db/6-5.
apply_op_in_db(Lambda,Pref1,Pref2,Pref3,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    apply_op_in_db_b(Lambda,Pref1,Pref2,Pref3,DB0,DB).
apply_op_in_db(Lambda,Pref1,Pref2,Pref3,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    apply_op_in_db_b(Lambda,Pref1,Pref2,Pref3,DB0,DB),
    machine:intern_termhash(DB,H).

apply_op_in_db_b(lambda(HT1,HT2,HT3,Op),Pref1,Pref2,Pref3,DB0,DB) :-
    cvt_pa(Pref1,Pref1a),
    cvt_pa(Pref2,Pref2a),
    cvt_pa(Pref3,Pref3a),
    (find_hashtable_for_prefix([Pref1a],DB0,TL,HT1)
     ->	true
     ;	HT1 = [],
	check_prefix_term([Pref1a],TL,'*_in_db/5')
    ),
    (find_hashtable_for_prefix([Pref2a],DB0,TL2,HT2)
     ->	check_taillen(TL,TL2,Pref2a,'*_in_db/5')
     ;	HT2 = [],
	check_prefix_term([Pref2a],TL,'*_in_db/5')
    ),
    call(Op),
    (update_hashtree_for_prefix([Pref3a],HT3,_,TL,DB0,DB)
     ->	true
     ;	misc_error(('[*_in_db/5] Predicate selection pattern: ',Pref3a,
		    ' is inconsistent with other patterns.'))
    ).

cvt_pa(PA,PSkel) :-
    (PA = P/A
    -> functor(PSkel,P,A)
    ;  PSkel = PA
    ).

:- comment(apply_rel_op_in_db/4, "
@pred{apply_rel_op_in_db(+RelOpLambdaTerm,+Pref1,+Pref2,+DB)} applies
the relational operator specified in @var{RelOpLambdaTerm} to the
subtries determined by @var{Pref1} and @var{Pref2} in @var{DB} and
succeeds or fails accordingly.  This is an internal predicate.
@var{RelOpLambdaTerm} is of the form @tt{lambda(HT1,HT2,Goal)}, where
@var{HT1} and @var{HT2} are variables and @var{Goal} is a binary goal
whose arguments are those varaibles.  The goal must define the desired
relational operator on subtries.  ").

apply_rel_op_in_db(Lambda,Pref1,Pref2,'$TRIETERM'(DB)) :-
    apply_rel_op_in_db_b(Lambda,Pref1,Pref2,DB).
apply_rel_op_in_db(Lambda,Pref1,Pref2,'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H),
    apply_rel_op_in_db_b(Lambda,Pref1,Pref2,DB).

apply_rel_op_in_db_b(lambda(HT1,HT2,Op),Pref1,Pref2,DB) :-
    cvt_pa(Pref1,Pref1a),
    cvt_pa(Pref2,Pref2a),
    (find_hashtable_for_prefix([Pref1a],DB,TL,HT1)
     -> true
     ;	HT1 = [],
	check_prefix_term([Pref1a],TL,'*_in_db/3')
    ),
    (find_hashtable_for_prefix([Pref2a],DB,TL,HT2)
     ->	true
     ;	HT2 = [],
	check_prefix_term([Pref2a],TL,'*_in_db/3')
    ),
    call(Op).

/**********************************************************/

/* These operations are just to support testing. */


naive_union_db(DB1,DB2,DB3) :-
    findall((H:-B),find_in_trie(H,B,DB1),Clauses),
    assert_in_db(Clauses,DB2,DB3).

naive_intersect_db(DB1,DB2,DB3) :-
    findall((H:-B),(find_in_trie(H,B,DB1),find_in_trie(H,B,DB2)),Rules),
    (DB4 = '$TRIETERM'(_) -> DB4 = '$TRIETERM'([]) ; DB4 = '$TRIETERMI'([])),
    assert_in_db(Rules,DB4,DB3).

naive_difference_db(DB1,DB2,DB3) :-
    findall((H:-B),(clause_in_db(H,B,DB1),
		    \+clause_in_db(H,B,DB2)
		   ),
		   Clauses),
    (DB1 = '$TRIETERM'(_) -> DB0 = '$TRIETERM'([]) ; DB0 = '$TRIETERMI'([])),
    assert_in_db(Clauses,DB0,DB3).

naive_sym_diff_db(DB1,DB2,DB3) :-
    findall((H:-B),
	    (clause_in_db(H,B,DB1),
	     \+ clause_in_db(H,B,DB2)
	     ;	
	     clause_in_db(H,B,DB2),
	     \+ clause_in_db(H,B,DB1)
	    ),
	    Clauses),
    (DB1 = '$TRIETERM'(_) -> DB0 = '$TRIETERM'([]) ; DB0 = '$TRIETERMI'([])),
    assert_in_db(Clauses,DB0,DB3).

naive_subset_db(DB1,DB2) :-
    \+ (clause_in_db(H,B,DB1),
	\+ clause_in_db(H,B,DB2)).

naive_disjoint_db(DB1,DB2) :-
    \+ (clause_in_db(H,B,DB1),
	clause_in_db(H,B,DB2)).

/*** end of testing operations ***/
/**********************************************************/

:- comment(union_db/3, " @pred{union_db(+DB1,+DB2,-DB)} takes two
prolog databases, in @var{DB1} and @var{DB2} and produces a prolog DB,
in @var{DB}, that is the union of the two input databases.  This is
done by traversing the two input tries in concert and in the process
generating the output trie.  This allows the output trie to share
subtries with the input tries, and to perhaps avoid traversing large
portions of the input tries.  For example, if the two input databases
contain facts for disjoint predicates, the union just creates a new
trie that points to entire definitions of the predicates in the input
tries, so such a union is done in time proportional to the number of
predicates, and completely independent of the size (i.e., the number
of tuples) of the predicates.  The form of all the DBs must be the
same. ").

union_db('$TRIETERM'(DB1),'$TRIETERM'(DB2),'$TRIETERM'(DB)) :-
    union_db_b(DB1,DB2,DB).
union_db('$TRIETERMI'(H1),'$TRIETERMI'(H2),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    union_db_b(DB1,DB2,DB),
    machine:intern_termhash(DB,H).

union_db_b(DB1,DB2,DB) :-
    (DB1 == DB2
     ->	DB = DB1
     ;	db_comps_d(DB1,HBits1,Num1,HT1),
     	db_comps_d(DB2,HBits2,Num2,HT2),
	(HBits1 < HBits2
	 -> HBits = HBits2,
	    rebuild_hashtree(HT1,HBits1,HBits,HT1b),
	    union_subtrees(HBits,HT1b,HT2,DNum,NHT)
	 ; HBits2 < HBits1
	 -> HBits = HBits1,
	    rebuild_hashtree(HT2,HBits2,HBits,HT2b),
	    union_subtrees(HBits,HT1,HT2b,DNum,NHT)
	 ;  HBits = HBits1,
	    union_subtrees(HBits,HT1,HT2,DNum,NHT)
	),
	NNum is Num1 + Num2 - DNum,
	fix_ht_size(HBits,NNum,NHT,DB)
    ).

:- comment(union_in_db/5, "@pred{union_in_db(+P1,+P2,+P3,+DB0,?DB)}
unions two subtries in @var{DB0} producing a new (or replaced) subtrie
in @var{DB}.  The input tries are determined by the prefix terms of
@var{P1} and @var{P2}, and the place of the resulting trie is
determined by prefix term @var{P3}.  (See @pred{is_prefix_term/2} and
@pred{apply_op_in_db/6} for discussions of prefix terms.)  The form of
@var{DB} will be the same as that of @var{DB0}. ").

union_in_db(P1,P2,P3,H0,H) :-
    apply_op_in_db(lambda(A,B,C,union_db_b(A,B,C)),P1,P2,P3,H0,H).

union_subtrees(HashBits,HT1,HT2,DNum,HT) :-
    (HashBits =< 0
     ->	union_kvs(HT1,HT2,0,DNum,HT)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	union_subtree(HashBits,HT10,HT20,DNum0,HT30),
	union_subtree(HashBits,HT11,HT21,DNum1,HT31),
	union_subtree(HashBits,HT12,HT22,DNum2,HT32),
	union_subtree(HashBits,HT13,HT23,DNum3,HT33),
	DNum is DNum0+DNum1+DNum2+DNum3,
	h_comps_c(HT,HT30,HT31,HT32,HT33)
    ).

%% DNum is diff, negative for loss (dupls)
union_subtree(HashBits,SHT1,SHT2,DNum,SHT) :-
    (SHT1 == []
     ->	SHT = SHT2, DNum = 0
     ; SHT2 == []
     ->	SHT = SHT1, DNum = 0
     ; SHT1 == SHT2
     ->	SHT = SHT1, count_keys(SHT,DNum)
     ;	NHashBits is HashBits - 2,
	union_subtrees(NHashBits,SHT1,SHT2,DNum,SHT)
    ).

%% DNum is number in both sets (so will be subtracted from sum to get count.)
union_kvs(HT1,HT2,DNum0,DNum,HT) :- 
    (HT1 == []
     ->	DNum = DNum0,
	HT = HT2
     ; HT2 == []
     ->	DNum = DNum0,
	HT = HT1
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> HT = [K1-V1|HTt],
	    union_kvs(HT1t,HT2,DNum0,DNum,HTt)
	 ;  K1 @> K2
	 -> HT = [K2-V2|HTt],
	    union_kvs(HT1,HT2t,DNum0,DNum,HTt)
	 ;  (V1 @= ht(_,_,_)
	     ->	union_db_b(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	     ;	union_values(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	    ),
	    DNum1 is DNum0 + Added,
	    union_kvs(HT1t,HT2t,DNum1,DNum,HTt)	
	)
    ).
    
union_values(V1s,V2s,V3s) :-
    (V1s == []
     ->	V3s = V2s
     ; V2s == []
     ->	V3s = V1s
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> V3s = [V1|V3st],
	    union_values(V1st,V2s,V3st)
	 ;  V1 @> V2
	 -> V3s = [V2|V3st],
	    union_values(V1s,V2st,V3st)
	 ;  V3s = [V1|V3st],
	    union_values(V1st,V2st,V3st)
	)
    ).
	
/********************************************************/

:- comment(intersect_db/3, " @pred{intersect_db(+DB1,+DB2,?DB)} takes
two prolog databases (tries), in @var{DB1} and @var{DB2} and produces
a prolog DB, in @var{DB}, that is the intersection of the two input
databases.  This is done by traversing the two input tries in concert
and in the process generating the output trie.  This allows the output
trie to share subterms with the input tries, and to perhaps avoid
traversing large portions of the input tries.  For example, if the two
input databases contain facts for disjoint predicates, the intersect
just creates a new trie that contains none of the predicates, so such
an Intersection is done in time proportional to the number of
predicates, and completely independent of the size (i.e., the number
of tuples) of the predicates.  The forms of all the DBs must be the
same. ").

intersect_db('$TRIETERM'(DB1),'$TRIETERM'(DB2),'$TRIETERM'(DB)) :-
    intersect_db_b(DB1,DB2,DB).
intersect_db('$TRIETERMI'(H1),'$TRIETERMI'(H2),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    intersect_db_b(DB1,DB2,DB),
    machine:intern_termhash(DB,H).

intersect_db_b(DB1,DB2,DB) :-
    (DB1 == DB2
     ->	DB = DB1
     ;	db_comps_d(DB1,HBits1,_Num1,HT1),
     	db_comps_d(DB2,HBits2,_Num2,HT2),
	(HBits1 < HBits2
	 -> HBits = HBits2,
	    rebuild_hashtree(HT1,HBits1,HBits,HT1b),
	    intersect_subtrees(HBits,HT1b,HT2,INum,NHT)
	 ; HBits2 < HBits1
	 -> HBits = HBits1,
	    rebuild_hashtree(HT2,HBits2,HBits,HT2b),
	    intersect_subtrees(HBits,HT1,HT2b,INum,NHT)
	 ;  HBits = HBits1,
	    intersect_subtrees(HBits,HT1,HT2,INum,NHT)
	),
	fix_ht_size(HBits,INum,NHT,DB)
    ).

:- comment(intersect_in_db/5,
"@pred{intersect_in_db(+P1,+P2,+P3,+DB0,?DB)} intersects two subtries
in @var{DB0} producing a new (or replaced) subtrie in @var{DB}.  The
input tries are determined by the prefix terms of @var{P1} and
@var{P2}, and the place of the resulting trie is determined by prefix
term @var{P3}.  (See @pred{is_prefix_term/2} and
@pred{apply_op_in_db/6} for discussions of prefix terms.)  The form of
@var{DB} will be the same as that of @var{DB0}. ").

intersect_in_db(P1,P2,P3,DB0,DB) :-
    apply_op_in_db(lambda(A,B,C,intersect_db_b(A,B,C)),P1,P2,P3,DB0,DB).

intersect_subtrees(HashBits,HT1,HT2,INum,HT) :-
    (HashBits =< 0
    ->	intersect_kvs(HT1,HT2,0,INum,HT)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	intersect_subtree(HashBits,HT10,HT20,INum0,HT30),
	intersect_subtree(HashBits,HT11,HT21,INum1,HT31),
	intersect_subtree(HashBits,HT12,HT22,INum2,HT32),
	intersect_subtree(HashBits,HT13,HT23,INum3,HT33),
	INum is INum0+INum1+INum2+INum3,
	h_comps_c(HT,HT30,HT31,HT32,HT33)
    ).

intersect_subtree(HashBits,SHT1,SHT2,INum,SHT) :-
    (SHT1 == []
     ->	SHT = [], INum = 0
     ; SHT2 == []
     ->	SHT = [], INum = 0
     ; SHT1 == SHT2
     ->	SHT = SHT1, count_keys(SHT,INum)
     ;	NHashBits is HashBits - 2,
	intersect_subtrees(NHashBits,SHT1,SHT2,INum,SHT)
    ).

intersect_kvs(HT1,HT2,INum0,INum,HT) :-
    (HT1 == []
     ->	INum = INum0, HT = []
     ; HT2 == []
     ->	INum = INum0, HT = []
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> intersect_kvs(HT1t,HT2,INum0,INum,HT)
	 ;  K1 @> K2
	 -> intersect_kvs(HT1,HT2t,INum0,INum,HT)
	 ;  (V1 @= ht(_,_,_)
	     ->	intersect_db_b(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	     ;	intersect_values(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	    ),
	    INum1 is INum0 + Added,
	    intersect_kvs(HT1t,HT2t,INum1,INum,HTt)
	)
    ).
    
intersect_values(V1s,V2s,V3s) :-
    (V1s == []
     ->	V3s = []
     ; V2s == []
     ->	V3s = []
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> intersect_values(V1st,V2s,V3s)
	 ;  V1 @> V2
	 -> intersect_values(V1s,V2st,V3s)
	 ;  V3s = [V1|V3st],
	    intersect_values(V1st,V2st,V3st)
	)
    ).

/********************************************************/

:- comment(difference_db/3, " @var{difference_db(+DB1,+DB2,?DB)} takes
two @tt{prolog_db} trie term databases, in @var{DB1} and @var{DB2} and
produces a prolog DB, in @var{DB}, that is the set difference of the
two input databases, i.e., all clauses in @var{DB1} that are not in
@var{DB2}.  This is done by traversing the two input tries (and their
index trees) in concert and in the process generating the output trie.
This allows the output trie (and index trees) to share subterms (and
sub index trees) with the input tries, and to perhaps avoid traversing
large portions of the input tries.  For example, if the two input
databases contain the same sets of tuples, the difference immediately
detects that and returns the empty database.  The forms of all the DBs
must be the same. ").

difference_db('$TRIETERM'(DB1),'$TRIETERM'(DB2),'$TRIETERM'(DB)) :-
    difference_db_b(DB1,DB2,DB).
difference_db('$TRIETERMI'(H1),'$TRIETERMI'(H2),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    difference_db_b(DB1,DB2,DB),
    machine:intern_termhash(DB,H).

difference_db_b(DB1,DB2,DB) :-
    (DB1 == DB2
     ->	DB = []
     ;	db_comps_d(DB1,HBits1,Num1,HT1),
     	db_comps_d(DB2,HBits2,_Num2,HT2),
	(HBits1 < HBits2
	 -> HBits = HBits2,
	    rebuild_hashtree(HT1,HBits1,HBits,HT1b),
	    difference_subtrees(HBits,HT1b,HT2,RNum,NHT)
	 ; HBits2 < HBits1
	 -> HBits = HBits1,
	    rebuild_hashtree(HT2,HBits2,HBits,HT2b),
	    difference_subtrees(HBits,HT1,HT2b,RNum,NHT)
	 ;  HBits = HBits1,
	    difference_subtrees(HBits,HT1,HT2,RNum,NHT)
	),
	NNum is Num1-RNum,
	fix_ht_size(HBits,NNum,NHT,DB)
    ).

:- comment(difference_in_db/5,
"@pred{difference_in_db(+P1,+P2,+P3,+DB0,?DB)} computes the set
difference of two subtries in @var{DB0} producing a new (or replaced)
subtrie in @var{DB}.  The input tries are determined by the prefix
terms of @var{P1} and @var{P2}, and the place of the resulting trie is
determined by prefix term @var{P3}.  (See @pred{is_prefix_term/2} and
@pred{apply_op_in_db/6} for discussions of prefix terms.)  The form of
@var{DB} will be the same as that of @var{DB0}. ").

difference_in_db(P1,P2,P3,DB0,DB) :-
    apply_op_in_db(lambda(A,B,C,difference_db_b(A,B,C)),P1,P2,P3,DB0,DB).

difference_subtrees(HashBits,HT1,HT2,RNum,HT) :-
    (HashBits =< 0
    ->	difference_kvs(HT1,HT2,0,RNum,HT)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	difference_subtree(HashBits,HT10,HT20,RNum0,HT30),
	difference_subtree(HashBits,HT11,HT21,RNum1,HT31),
	difference_subtree(HashBits,HT12,HT22,RNum2,HT32),
	difference_subtree(HashBits,HT13,HT23,RNum3,HT33),
	RNum is RNum0+RNum1+RNum2+RNum3,
	h_comps_c(HT,HT30,HT31,HT32,HT33)
    ).

difference_subtree(HashBits,SHT1,SHT2,RNum,SHT) :-
    (SHT1 == []
     ->	SHT = [], RNum = 0
     ; SHT2 == []
     ->	SHT = SHT1, RNum = 0
     ; SHT1 == SHT2
     ->	SHT = [], count_keys(SHT1,RNum)
     ;	NHashBits is HashBits - 2,
	difference_subtrees(NHashBits,SHT1,SHT2,RNum,SHT)
    ).

%% RNum is number removed from list, *not* in list.
difference_kvs(HT1,HT2,RNum0,RNum,HT) :-
    (HT1 == []
     ->	RNum = RNum0, HT = []
     ; HT2 = []
     ->	RNum = RNum0, 
	HT = HT1
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> HT = [K1-V1|HTt],
	    difference_kvs(HT1t,HT2,RNum0,RNum,HTt)
	 ;  K1 @> K2
	 -> difference_kvs(HT1,HT2t,RNum0,RNum,HT)
	 ;  (V1 @= ht(_,_,_)
	     ->	difference_db_b(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	     ;	difference_values(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	    ),
	    RNum1 is RNum0 + 1 - Added,
	    difference_kvs(HT1t,HT2t,RNum1,RNum,HTt)
	)
    ).
    
%%difference_values(_,_,_) :- writeln(userout,difference_values),fail.
difference_values(V1s,V2s,V3s) :-
    (V1s == []
     ->	V3s = []
     ; V2s == []
     ->	V3s = V1s
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> V3s = [V1|V3st],
	    difference_values(V1st,V2s,V3st)
	 ;  V1 @> V2
	 -> difference_values(V1s,V2st,V3s)
	 ;  difference_values(V1st,V2st,V3s)
	)
    ).

/********************************************************/

:- comment(sym_diff_db/3, " @var{sym_diff_db(+DB1,+DB2,?DB)} takes two
prolog databases (tries), in @var{DB1} and @var{DB2} and produces a
prolog DB, in @var{DB}, that is symmetric difference of the two input
databases, i.e., all tuples in one but not both of @var{DB1} and
@var{DB2}.  This is done by traversing the two input tries in concert
and in the process generating the output trie.  This allows the output
trie to share subterms with the input tries, and to perhaps avoid
traversing large portions of the input tries.  The form of all the DBs
must be the same. ").

sym_diff_db('$TRIETERM'(DB1),'$TRIETERM'(DB2),'$TRIETERM'(DB)) :-
    sym_diff_db_b(DB1,DB2,DB).
sym_diff_db('$TRIETERMI'(H1),'$TRIETERMI'(H2),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    sym_diff_db_b(DB1,DB2,DB),
    machine:intern_termhash(DB,H).

sym_diff_db_b(DB1,DB2,DB) :-
    (DB1 == DB2
     ->	DB = []
     ;	db_comps_d(DB1,HBits1,_Num1,HT1),
     	db_comps_d(DB2,HBits2,_Num2,HT2),
	(HBits1 < HBits2
	 -> HBits = HBits2,
	    rebuild_hashtree(HT1,HBits1,HBits,HT1b),
	    sym_diff_subtrees(HBits,HT1b,HT2,RNum,NHT)
	 ; HBits2 < HBits1
	 -> HBits = HBits1,
	    rebuild_hashtree(HT2,HBits2,HBits,HT2b),
	    sym_diff_subtrees(HBits,HT1,HT2b,RNum,NHT)
	 ;  HBits = HBits1,
	    sym_diff_subtrees(HBits,HT1,HT2,RNum,NHT)
	),
	fix_ht_size(HBits,RNum,NHT,DB)
    ).

:- comment(sym_diff_in_db/5,
"@pred{sym_diff_in_db(+P1,+P2,+P3,+DB0,?DB)} computes the symmetric
difference of two subtries in @var{DB0} producing a new (or replaced)
subtrie in @var{DB}.  The input tries are determined by the prefix
terms of @var{P1} and @var{P2}, and the place of the resulting trie is
determined by prefix term @var{P3}.  (See @pred{is_prefix_term/2} and
@pred{apply_op_in_db/6} for discussions of prefix terms.)  The form of
@var{DB} will be the same as that of @var{DB0}. ").

sym_diff_in_db(P1,P2,P3,DB0,DB) :-
    apply_op_in_db(lambda(A,B,C,sym_diff_db_b(A,B,C)),P1,P2,P3,DB0,DB).

% RNum is number in result.
sym_diff_subtrees(HashBits,HT1,HT2,RNum,HT) :-
    (HashBits =< 0
    ->	sym_diff_kvs(HT1,HT2,0,RNum,HT)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	sym_diff_subtree(HashBits,HT10,HT20,RNum0,HT30),
	sym_diff_subtree(HashBits,HT11,HT21,RNum1,HT31),
	sym_diff_subtree(HashBits,HT12,HT22,RNum2,HT32),
	sym_diff_subtree(HashBits,HT13,HT23,RNum3,HT33),
	RNum is RNum0+RNum1+RNum2+RNum3,
	h_comps_c(HT,HT30,HT31,HT32,HT33)
    ).

sym_diff_subtree(HashBits,SHT1,SHT2,RNum,SHT) :-
    (SHT1 == []
     ->	SHT = SHT2, count_keys(SHT,RNum)
     ; SHT2 == []
     ->	SHT = SHT1, count_keys(SHT,RNum)
     ; SHT1 == SHT2
     ->	SHT = [], RNum = 0
     ;	NHashBits is HashBits - 2,
	sym_diff_subtrees(NHashBits,SHT1,SHT2,RNum,SHT)
    ).

sym_diff_kvs(HT1,HT2,RNum0,RNum,HT) :-
    (HT1 == []
     ->	basics:length(HT2,Len),
	RNum is RNum0 + Len,
	HT = HT2
     ; HT2 = []
     ->	basics:length(HT1,Len),
	RNum is RNum0 + Len,
	HT = HT1
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> HT = [K1-V1|HTt],
	    RNum1 is RNum0 + 1,
	    sym_diff_kvs(HT1t,HT2,RNum1,RNum,HTt)
	 ;  K1 @> K2
	 -> HT = [K2-V2|HTt],
	    RNum1 is RNum0 + 1,
	    sym_diff_kvs(HT1,HT2t,RNum1,RNum,HTt)
	 ;  (V1 @= ht(_,_,_)
	     ->	sym_diff_db_b(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	     ;	sym_diff_values(V1,V2,V3),
		add_kv(V3,K1,Added,HT,HTt)
	    ),
	    RNum1 is RNum0 + Added,
	    sym_diff_kvs(HT1t,HT2t,RNum1,RNum,HTt)
	)
    ).
    
sym_diff_values(V1s,V2s,V3s) :-
    (V1s == []
     ->	V3s = V2s
     ; V2s == []
     ->	V3s = V1s
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> V3s = [V1|V3st],
	    sym_diff_values(V1st,V2s,V3st)
	 ;  V1 @> V2
	 -> V3s = [V2|V3st],
	    sym_diff_values(V1s,V2st,V3st)
	 ;  sym_diff_values(V1st,V2st,V3s)
	)
    ).

/********************************************************/

:- comment(xprod_in_db/5,"
@pred{xprod_in_db(+Pref1,+Pref2,+Pref3,+DB0,?DB)} computes the cross
product of the tries determined by @var{Pref1} and @var{Pref2} and
puts the result at the position determined by @var{Pref3}.  The number
of variables in @var{Pref3} must be the sum of the numbers of
variables in @var{Pref1} and @var{Pref2}.  For example, assuming the
call to build_db constructs a prolog database, @var{DB0}, the query:
@begin{verbatim}	   
| ?- build_db(DB),xprod_in_db(p(_,_),q(_,_,_),r(_,_,_,_,_),DB0,DB).
@end{verbatim}	   
would compute the cross product of relations p/2 and q/3 in @var{DB0}
putting the result in r/5 (with r tuples having the p values preceding
the q values). The predicate r/5 is added or replaced and the new DB
@var{DB} is generated. (Note that, since the form p/2 is converted to
prefix term p(_,_), p/2, q/3 and r/5 could be used in place of the
explicit prefix terms in this query.)

The tuples of r/5 would be as though it were defined by the following
Prolog rule:
@begin{verbatim}
r(P1,P2,Q1,Q2,Q3) :- r(P1,P2), q(Q1,Q2,Q3).
@end{verbatim}

The @var{Pref1} values must be facts (i.e., clauses without (non-true)
bodies.)  The complexity of this operation is the size of the
@var{Pref1} trie, since every leaf of the @var{Pref1} trie is
essentially extended with a pointer to the @var{Pref2} trie.  The form of
@var{DB} will be the same as that of @var{DB0}. ").

xprod_in_db(Pref1,Pref2,Pref3,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    xprod_in_db_b(Pref1,Pref2,Pref3,DB0,DB).
xprod_in_db(Pref1,Pref2,Pref3,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    xprod_in_db_b(Pref1,Pref2,Pref3,DB0,DB),
    machine:intern_termhash(DB,H).

xprod_in_db_b(Pref1,Pref2,Pref3,DB0,DB) :-
    cvt_pa(Pref1,Pref1a),
    cvt_pa(Pref2,Pref2a),
    cvt_pa(Pref3,Pref3a),
    (find_hashtable_for_prefix([Pref2a],DB0,TLen2,HT2),
     find_hashtable_for_prefix([Pref1a],DB0,TLen1,HT1)
     ->	check_prefix_term([Pref3a],TLen3,'xprod_in_db/5'),
	(TLen3 =\= TLen1+TLen2
	 -> misc_error(('[prolog_db:xprod_in_db] ',Pref1,', ',Pref2,', and ',
			Pref3,' are inconsistent for cross product.'))
	 ;  update_hashtree_for_prefix([Pref3a],NSHT,_OSHT,TLen3,DB0,DB),
	    xprod_ht(HT1,HT2,NSHT)
	)
     ;  retractall_in_db_b(Pref3a,DB0,DB) % one or other component is empty
    ).
     
xprod_ht(HT1,HT2,NSHT) :-
    (HT1 == [true]
     ->	NSHT = HT2
     ;	db_comps_d(HT1,HashBits,Num,HTree),
	db_comps_d(NSHT,HashBits,Num,NHTree),
	xprod_htrees(HashBits,HTree,HT2,NHTree)
    ).

xprod_htrees(HashBits,HTree,HT2,NHTree) :-
    (HashBits =< 0
     ->	xprod_kvs(HTree,HT2,NHTree)
     ;	h_comps_d(HTree,H1,H2,H3,H4),
	xprod_htree(HashBits,H1,HT2,NH1),
	xprod_htree(HashBits,H2,HT2,NH2),
	xprod_htree(HashBits,H3,HT2,NH3),
	xprod_htree(HashBits,H4,HT2,NH4),
	h_comps_c(NHTree,NH1,NH2,NH3,NH4)
    ).

xprod_htree(HashBits,H,HT2,NH) :-
    (H == []
     ->	NH = []
     ;	NHashBits is HashBits - 2,
	xprod_htrees(NHashBits,H,HT2,NH)
    ).

xprod_kvs([],_HT2,[]).
xprod_kvs([K-V|KVs],HT2,[K-NV|NKVs]) :-
    (V @= ht(_,_,_)
     ->	xprod_ht(V,HT2,NV)
     ;	(V == [true]
	 -> NV = HT2
	 ;  misc_error(('[prolog_db:xprod_in_db]: left argument not facts'))
	)
    ),
    xprod_kvs(KVs,HT2,NKVs).

/********************************************************/

:- comment(join_in_db/6,
"@pred{join_in_db(+Pref1,+Pref2,+NJoin,+Pref3,+DB0,?DB)} performs an
equi-join of tuples of terms specified by prefix terms @var{Pref1} and
@var{Pref2} producing a new set of terms as specified in prefix term
@var{Pref3}.  @var{NJoin} is the number of join variables.  The join
arguments are the first @var{NJoin} fields of the tuples specified by
@var{Pref1} and @var{Pref2}.  The number of variables in @var{Pref3}
must be the sum of the numbers of variables in @var{Pref1} and
@var{Pref2} minus @var{NJoin}.  Assuming the call to @pred{build_db/1}
builds a prolog DB in @var{DB0}, an example query might be:
@begin{verbatim} | ?- build_db(DB0),join_in_db(p/3,q/4,1,r/6,DB0,DB).
@end{verbatim}

This query would materialize the predicate r/6 in to @var{DB}, where r/6
definition in Prolog would be:
@begin{verbatim}
r(J,P2,P3,Q2,Q3,Q4) :- p(J,P2,P3), q(J,Q2,Q3,Q4).
@end{verbatim}

The p/3 clauses must be ground facts. (q/4 could have rules with
non-empty bodies, but I don't see any application for this.)  The form
of @var{DB} will be the same as that of @var{DB0}. ").

:- index join_in_db/6-5.
join_in_db(Pref1,Pref2,NJoin,Pref3,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    join_in_db_b(Pref1,Pref2,NJoin,Pref3,DB0,DB).
join_in_db(Pref1,Pref2,NJoin,Pref3,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    join_in_db_b(Pref1,Pref2,NJoin,Pref3,DB0,DB),
    machine:intern_termhash(DB,H).

join_in_db_b(Pref1,Pref2,NJoin,Pref3,DB0,DB) :-
    cvt_pa(Pref1,Pref1a),
    cvt_pa(Pref2,Pref2a),
    cvt_pa(Pref3,Pref3a),
    find_hashtable_for_prefix([Pref1a],DB0,TLen1,HT1),
    find_hashtable_for_prefix([Pref2a],DB0,TLen2,HT2),
    OutLen is TLen1+TLen2-NJoin,
    check_prefix_term([Pref3a],OutLen,'join_in_db/6'),
    join_ht(HT1,HT2,NJoin,HT3),
    (HT3 == []
     ->	retractall_in_db_b(Pref3a,DB0,DB)
     ;	update_hashtree_for_prefix([Pref3a],HT3,_,_,DB0,DB)
    ).

join_ht([],_DB2,_N,[]) :- !.
join_ht(_DB1,[],_N,[]) :- !.
join_ht(DB1,DB2,NJoin,DB) :-
    db_comps_d(DB1,HBits1,_Num1,HT1),
    db_comps_d(DB2,HBits2,_Num2,HT2),
    (HBits1 < HBits2
     ->	HBits = HBits2,
	rebuild_hashtree(HT1,HBits1,HBits,HT1b),
	join_subtrees(HBits,HT1b,HT2,NJoin,INum,NHT)
     ;	HBits2 < HBits1
     ->	HBits = HBits1,
	rebuild_hashtree(HT2,HBits2,HBits,HT2b),
	join_subtrees(HBits,HT1,HT2b,NJoin,INum,NHT)
     ;	HBits = HBits1,
	join_subtrees(HBits,HT1,HT2,NJoin,INum,NHT)
    ),
    fix_ht_size(HBits,INum,NHT,DB).

join_subtrees(HashBits,HT1,HT2,NJoin,INum,HT) :-
    (HashBits =< 0
     ->	join_kvs(HT1,HT2,NJoin,0,INum,HT)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	join_subtree(HashBits,HT10,HT20,NJoin,INum0,HT30),
	join_subtree(HashBits,HT11,HT21,NJoin,INum1,HT31),
	join_subtree(HashBits,HT12,HT22,NJoin,INum2,HT32),
	join_subtree(HashBits,HT13,HT23,NJoin,INum3,HT33),
	INum is INum0+INum1+INum2+INum3,
	h_comps_c(HT,HT30,HT31,HT32,HT33)
    ).

join_subtree(HashBits,HT1,HT2,NJoin,INum,HT) :-
    (HT1 == []
     ->	HT = [], INum = 0
     ; HT2 == []
     ->	HT = [], INum = 0
     ;	NHashBits is HashBits - 2,
	join_subtrees(NHashBits,HT1,HT2,NJoin,INum,HT)
    ).

join_kvs(KVs1,KVs2,NJoin,INum0,INum,KVs) :-
    (KVs1 == []
     ->	INum = INum0,
	KVs = []
     ; KVs2 == []
     ->	INum = INum0,
	KVs = []
     ;	KVs1 = [K1-V1|KVs1r],
     	KVs2 = [K2-V2|KVs2r],
	(K1 @< K2
	 -> join_kvs(KVs1r,KVs2,NJoin,INum0,INum,KVs)
	 ;  K1 @> K2
	 -> join_kvs(KVs1,KVs2r,NJoin,INum0,INum,KVs)
	 ;  (K1 = functor(_M,_P,Arity)
	     ->	NJoin1 is NJoin - 1 + Arity
	     ;	NJoin1 is NJoin - 1
	    ),
	    (NJoin1 =:= 0
	     ->	xprod_ht(V1,V2,NV)
	     ;	join_ht(V1,V2,NJoin1,NV)
	    ),
	    add_kv(NV,K1,Added,KVs,KVsR),
	    INum1 is INum0 + Added,
	    join_kvs(KVs1r,KVs2r,NJoin,INum1,INum,KVsR)
	)
    ).

/********************************************************/

:- comment(reorder_in_db/4,
"@pred{reorder_in_db(+PrefI,+PrefO,+DB0,DB)} generates a new version
of the set of tuples determined by the @var{PrefI} prefix term by
re-ordering its arguments and puts it in the predicate determined by
@var{PrefO}.  Note that one can use this (carefully) to change the
index on @var{Pref1} since tuples are indexed in left-to-right order.
The variables in @var{PrefO} must be a permutation of the variables in
@var{PrefI}, which defines the reordering of the arguments.  Note that
the tries corresponding to an unchanged final sequence of variables in
the two prefix terms will not be traversed in the reordering.

The following example interchanges the first two arguments of @tt{p/4}
in @var{DB0} to create @tt{q/4} in @var{DB}:

@begin{verbatim}	   
| ?- build_db(DB0),reorder_in_db(p(A,B,C,D),q(B,A,C,D),DB0,DB).
@end{verbatim}	   

Note that reordering can bring a later argument to the beginning so it
would be available for use in a call to @pred{join_in_db/6}.  The form
of @var{DB} will be the same as that of @var{DB0}. ").

reorder_in_db(PrefI,PrefO,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    reorder_in_db_b(PrefI,PrefO,DB0,DB).
reorder_in_db(PrefI,PrefO,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    reorder_in_db_b(PrefI,PrefO,DB0,DB),
    machine:intern_termhash(DB,H).

reorder_in_db_b(PrefI,PrefO,DB0,DB) :-
    cvt_pa(PrefI,PrefIa),
    cvt_pa(PrefO,PrefOa),
    prefix_term([PrefIa],IPr,IVars),
    is_most_general_term(IVars),
    prefix_term([PrefOa],OPr,OVars),
    (OPr == IPr
     ->	misc_error(('[prolog_in_db:reorder_in_db] Output must be different from input: ',OPr))     ;	true
    ),
    is_most_general_term(OVars),
    num_common_tail_vars(IVars,OVars,NCom),
    (NCom =:= 0
     ->	findall(PrefO,clause_in_db_b(PrefI,true,DB0),Clauses),
	retractall_in_db_b(PrefO,DB0,DB1),
	assert_in_db_b(Clauses,DB1,DB)
     ;	find_hashtable_for_prefix([PrefIa],DB0,TLen,IHT),
	(NCom =:= TLen
	 -> copy_in_db_b(PrefIa,PrefOa,DB0,DB)
	 ;  NArgs is TLen - NCom,
	    basics:length(IArgs,NArgs),
	    basics:append(IArgs,_,IVars),
	    copy_term(PrefO,OPrefO),
	    check_prefix_term([PrefOa],TLen,'reorder_in_db/4'),
	    reorder_ht(IHT,IArgs,NArgs,OPrefO,PrefOa,DB0,DB)
	)
    ).

num_common_tail_vars(IV,OV,NCom) :-
    (IV == OV
     ->	basics:length(IV,NCom)
     ;	IV = [_|IVt],
	OV = [_|OVt],
	num_common_tail_vars(IVt,OVt,NCom)
    ).

reorder_ht([],_CArgs,_NArgs,_OPrefO,_PrefO,DB,DB) :- !.
reorder_ht(IHT,CArgs,NArgs,OPrefO,PrefO,DB0,DB) :-
    db_comps_d(IHT,HBits,_Num,HTree),
    reorder_subtrees(HBits,HTree,CArgs,NArgs,OPrefO,PrefO,DB0,DB).

reorder_subtrees(HashBits,HTree,CArgs,NArgs,OPrefO,PrefO,DB0,DB) :-
    (HashBits =< 0
     ->	reorder_kvs(HTree,CArgs,NArgs,OPrefO,PrefO,DB0,DB)
     ;	h_comps_d(HTree,HTree1,HTree2,HTree3,HTree4),
	reorder_subtree(HashBits,HTree1,CArgs,NArgs,OPrefO,PrefO,DB0,DB1),
	reorder_subtree(HashBits,HTree2,CArgs,NArgs,OPrefO,PrefO,DB1,DB2),
	reorder_subtree(HashBits,HTree3,CArgs,NArgs,OPrefO,PrefO,DB2,DB3),
	reorder_subtree(HashBits,HTree4,CArgs,NArgs,OPrefO,PrefO,DB3,DB)
    ).

reorder_subtree(HashBits,HTree,CArgs,NArgs,OPrefO,PrefO,DB0,DB) :-
    (HTree == []
     ->	DB = DB0
     ;	NHashBits is HashBits - 2,
	reorder_subtrees(NHashBits,HTree,CArgs,NArgs,OPrefO,PrefO,DB0,DB)
    ).

reorder_kvs([],_CArgs,_NArgs,_OPrefO,_PrefO,DB,DB).
reorder_kvs(KVs,CArgs,NArgs,OPrefO,PrefO,DB0,DB) :-
    KVs = [K-V|KVsr],
    copy_term(CArgs-PrefO,CArgsT-PrefOT),
    CArgsT = [CArgT|RemArgs],
    (K = functor(Mod,Fun,Ari)
     ->	standard:functor(CArgT,Mod,Fun,Ari),
	CArgT =.. [_Pred|Args],
	basics:append(Args,RemArgs,NCArgs)
     ;	CArgT = K,
	NCArgs = RemArgs
    ),
    (NCArgs == []
     ->	assert_pref_with_trie([PrefOT],[OPrefO],NArgs,V,DB0,DB1)
     ;	reorder_ht(V,NCArgs,NArgs,OPrefO,PrefOT,DB0,DB1)
    ),
    reorder_kvs(KVsr,CArgs,NArgs,OPrefO,PrefO,DB1,DB).

assert_pref_with_trie([HeadTerm|HeadTail],OPref,NArgs,HT,DB0,DB) :-
    (atomic(HeadTerm)
     ->	NHeadTail = HeadTail,
	(is_most_general_term(OPref)
	 -> NArgs1 is NArgs - 1,
	    OPrefTail = constant % is simpler most general term
	 ;  OPref = [HeadTerm|OPrefTail],
	    NArgs1 = NArgs
	)
     ; HeadTerm = '$VAR'(VN),integer(VN)
     ->	NHeadTail = HeadTail,
	(is_most_general_term(OPref)
	 -> NArgs1 is NArgs - 1,
	    OPrefTail = constant
	 ;  OPref = [_|OPrefTail],
	    NArgs1 = NArgs
	)
     ;  HeadTerm =.. [_|SubTerms],
	basics:length(SubTerms,K),
	basics:append(SubTerms,HeadTail,NHeadTail),
	(is_most_general_term(OPref)
	 -> NArgs1 is NArgs - 1 + K,
	    OPrefT = constant
	 ;  OPref = [OPrefH|OPrefT],
	    OPrefH =.. [_|PrefSubTerms],
	    basics:append(PrefSubTerms,OPrefT,OPrefTail),
	    NArgs1 = NArgs
	)
    ),
    (NArgs1 =:= 0
     ->	update_hashtree(HeadTerm,HT,_,DB0,DB1) % add SubTrie HT
     ;	update_hashtree(HeadTerm,NewSubTrie,OldSubTrie,DB0,DB1),
	assert_pref_with_trie(NHeadTail,OPrefTail,NArgs1,HT,OldSubTrie,NewSubTrie)
    ),
    db_comps_d(DB1,HashBits,NumInTab,HashTree),
    (NumInTab < floor(2**HashBits)
     ->	DB = DB1
     ;	NHashBits is HashBits*2,
	db_comps_c(DB,NHashBits,NumInTab,NHashTree),
	rebuild_hashtree(HashTree,HashBits,NHashBits,NHashTree)
    ).


/********************************************************/

:- comment(project_in_db/5,
"@pred{project_in_db(+PrefI,+NArgs,+PrefO,+DB0,?DB)} creates a new
relation that projects away the first @var{NArgs} of the tuples of
@var{PrefI} in @var{DB0}, adding the result according to @var{PrefO}
in @var{DB}.  The number of variables in @var{PrefI} minus @var{NArgs}
must equal the number of variables in @var{PrefO}.  An example of its
use is:
	   
@begin{verbatim}	   
| ?- build_db(DB0),project_in_db(p(_,_,_,_),2,q(_,_),DB0,DB).
@end{verbatim}

which projects away the first two arguments of the @tt{p/4} relation
in @var{DB0} to generate the @tt{q/2} relation in @var{DB}.  The form
of @var{DB} will be the same as that of @var{DB0}.  ").

project_in_db(PrefI,NArgs,PrefO,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    project_in_db_b(PrefI,NArgs,PrefO,DB0,DB).
project_in_db(PrefI,NArgs,PrefO,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    project_in_db_b(PrefI,NArgs,PrefO,DB0,DB),
    machine:intern_termhash(DB,H).

project_in_db_b(PrefI,NArgs,PrefO,DB0,DB) :-
    cvt_pa(PrefI,PrefIa),
    cvt_pa(PrefO,PrefOa),
    (find_hashtable_for_prefix([PrefIa],DB0,TLen,IHT)
     ->	TLenO is TLen - NArgs,
	check_prefix_term([PrefOa],TLenO,'project_in_db/5'),
	project_ht(IHT,NArgs,OHT),
	(OHT == []
	 -> remove_from_trie(PrefOa,_,DB0,_,DB)
	 ;  update_hashtree_for_prefix([PrefOa],OHT,_,_,DB0,DB)
	)
     ;  DB = DB0
    ).

project_ht(IHT,NArgs,OHT) :-
    (IHT == []
     ->	OHT = []
     ;	db_comps_d(IHT,HBits,_Num,HTree),
	project_subtrees(HBits,HTree,NArgs,[],OHT)
    ).

project_subtrees(HashBits,HTree,NArgs,DB0,DB) :-
    (HashBits =< 0
     ->	project_kvs(HTree,NArgs,DB0,DB)
     ;	h_comps_d(HTree,HTree1,HTree2,HTree3,HTree4),
	project_subtree(HashBits,HTree1,NArgs,DB0,DB1),
	project_subtree(HashBits,HTree2,NArgs,DB1,DB2),
	project_subtree(HashBits,HTree3,NArgs,DB2,DB3),
	project_subtree(HashBits,HTree4,NArgs,DB3,DB)
    ).

project_subtree(HashBits,HTree,NArgs,DB0,DB) :-
    (HTree == []
     ->	DB = DB0
     ;	NHashBits is HashBits - 2,
	project_subtrees(NHashBits,HTree,NArgs,DB0,DB)
    ).

project_kvs([],_NArgs,DB,DB).
project_kvs([K-V|KVsr],NArgs,DB0,DB) :-
    (K = functor(_Mod,_Pred,Arity)
     ->	NArgs1 is NArgs - 1 + Arity
     ;	NArgs1 is NArgs - 1
    ),
    (NArgs1 =:= 0
     ->	union_db_b(V,DB0,DB1)
     ;	project_ht(V,NArgs1,DB2),
	union_db_b(DB0,DB2,DB1)
    ),
    project_kvs(KVsr,NArgs,DB1,DB).

/********************************************************/

:- comment(copy_in_db/4, "@pred{copy_in_db(+PrefI,+PrefO,+DB0,?DB)}
makes a copy of @var{PrefI} into @var{Pref0} generating @var{DB} from
@var{DB0}.  @var{PrefI} and @var{PrefO} must be prefix terms and have
the same number of variables.  This is a very fast operation since it
simple copies the pointer to the appropriate subtrie.  The form of
@var{DB} will be the same as that of @var{DB0}. ").

copy_in_db(PrefI,PrefO,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    copy_in_db_b(PrefI,PrefO,DB0,DB).
copy_in_db(PrefI,PrefO,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    copy_in_db_b(PrefI,PrefO,DB0,DB),
    machine:intern_termhash(DB,H).

copy_in_db_b(PrefI,PrefO,DB0,DB) :-
    cvt_pa(PrefI,PrefIa),
    cvt_pa(PrefO,PrefOa),
    (find_hashtable_for_prefix([PrefIa],DB0,TL,HT1)
     ->	true
     ;	HT1 = [],
	check_prefix_term([PrefIa],TL,'copy_in_db/4')
    ),
    (update_hashtree_for_prefix([PrefOa],HT1,_,TL,DB0,DB)
     ->	true
     ;	misc_error(('[prolog_in_db:copy_in_db/4] Predicate selection pattern: ',PrefOa,
		    ' is illegal or inconsistent with other patterns.'))
    ).

:- comment(materialize_in_db/4,
"@pred{materialize_in_db(+PrefI,+PrefO,+DB0,?DB)} calls the query
@var{PrefI} in @var{DB0} and asserts the term @var{Pref0} into
@var{DB} for every answer.  Normally every variable in @var{Pref0}
will be in the goal @var{PrefI}.  The form of @var{DB} will be the
same as that of @var{DB0}. ").

:- index materialize_in_db/4-3.
materialize_in_db(PrefI,PrefO,'$TRIETERM'(DB0),'$TRIETERM'(DB)) :-
    materialize_in_db_b(PrefI,PrefO,DB0,DB).
materialize_in_db(PrefI,PrefO,'$TRIETERMI'(H0),'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB0,H0),
    materialize_in_db_b(PrefI,PrefO,DB0,DB),
    machine:intern_termhash(DB,H).

materialize_in_db_b(PrefI,PrefO,DB0,DB) :-
    cvt_pa(PrefI,PrefIa),
    cvt_pa(PrefO,PrefOa),
    prefix_term([PrefIa],_,Tail),
    prefix_term([PrefOa],_,Tail),
    findall(PrefOa,call_in_db(PrefIa,DB0),Facts),
    retractall_in_db_b(PrefOa,DB0,DB1),
    assert_in_db_b(Facts,DB1,DB).

/******************************************************/

:- comment(subset_db/2, "@pred{subset_db(+DB1,+DB2)} succeeds if the
terms in prolog DB @var{DB1} are a subset of the terms in prolog DB
@pred{DB2}, and fails otherwise. The form of both DBs must be the
same. ").

subset_db('$TRIETERM'(DB1),'$TRIETERM'(DB2)) :-
    subset_db_b(DB1,DB2).
subset_db('$TRIETERMI'(H1),'$TRIETERMI'(H2)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    subset_db_b(DB1,DB2).

subset_db_b(DB1,DB2) :-
    (DB1 == DB2
     ->	true
     ;	db_comps_d(DB1,HBits1,_Num1,HT1),
     	db_comps_d(DB2,HBits2,_Num2,HT2),
	(HBits1 < HBits2
	 -> rebuild_hashtree(HT1,HBits1,HBits2,HT1b),
	    subset_subtrees(HBits2,HT1b,HT2)
	 ; HBits2 < HBits1
	 -> rebuild_hashtree(HT2,HBits2,HBits1,HT2b),
	    subset_subtrees(HBits1,HT1,HT2b)
	 ;  subset_subtrees(HBits1,HT1,HT2)
	)
    ).

:- comment(subset_in_db/3, "@pred{subset_in_db(+Pref1,+Pref2,+DB)}
succeeds if the tuples specified by prefix term @var{Pref1} are a
subset of those specified by @var{Pref2} in @var{DB}.  The number of
variables in @var{Pref1} and @var{Pref2} must be the same. @var{DB}
may be of either form. ").

subset_in_db(P1,P2,DB) :-
    apply_rel_op_in_db(lambda(A,B,subset_db_b(A,B)),P1,P2,DB).

subset_subtrees(HashBits,HT1,HT2) :-
    (HashBits =< 0
    ->	subset_kvs(HT1,HT2)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	subset_subtree(HashBits,HT10,HT20),
	subset_subtree(HashBits,HT11,HT21),
	subset_subtree(HashBits,HT12,HT22),
	subset_subtree(HashBits,HT13,HT23)
    ).

subset_subtree(HashBits,SHT1,SHT2) :-
    (SHT1 == []
     ->	true
     ; SHT2 == []
     ->	fail
     ; SHT1 == SHT2
     ->	true
     ;	NHashBits is HashBits - 2,
	subset_subtrees(NHashBits,SHT1,SHT2)
    ).

subset_kvs(HT1,HT2) :-
    (HT1 == []
     ->	true
     ; HT2 == []
     ->	fail
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> fail
	 ;  K1 @> K2
	 -> subset_kvs(HT1,HT2t)
	 ;  (V1 @= ht(_,_,_)
	     ->	subset_db_b(V1,V2)
	     ;	subset_values(V1,V2)
	    ),
	    subset_kvs(HT1t,HT2t)
	)
    ).

subset_values(V1s,V2s) :-
    (V1s == []
     ->	true
     ; V2s == []
     ->	fail
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> fail
	 ;  V1 @> V2
	 -> subset_values(V1s,V2st)
	 ;  subset_values(V1st,V2st)
	)
    ).


:- comment(is_empty_in_db/2, " @pred{is_empty_in_db(+Pref,+DB)}
succeeds if the relation determined by prefix term @var{Pref} in
database @var{DB} is empty.  It fails otherwise.  @var{DB} may be of
either regular or interned form. ").

is_empty_in_db(Pref,'$TRIETERM'(DB)) :-
    is_empty_in_db_b(Pref,DB).
is_empty_in_db(Pref,'$TRIETERMI'(H)) :-
    machine:intern_termhash(DB,H),
    is_empty_in_db_b(Pref,DB).

is_empty_in_db_b(Pref,DB) :-
    cvt_pa(Pref,Prefa),
    \+ find_hashtable_for_prefix([Prefa],DB,_TL,_HT).


:- comment(move_in_db/4, " @pred{move_in_db(+Pref1,+Pref2,+DB0,-DB)}
generates @var{DB} that is the same as @var{DB0} except that the
tuples of @var{Pref1} are copied to @var{Pref2} and the tuples of
@var{Pref1} are deleted.  ").

move_in_db(Pref1,Pref2,DB0,DB) :-
    copy_in_db(Pref1,Pref2,DB0,DB1),
    retractall_in_db(Pref1,DB1,DB).

:- comment(size_db/2," @pred{size_db(+DB,-Count)} returns the number
of clauses in @var{DB} in @var{Count}.  ").

size_db('$TRIETERM'(DB),Cnt) :-
    size_db_b(DB,Cnt).
size_db('$TRIETERMI'(H),Cnt) :-
    machine:intern_termhash(DB,H),
    size_db_b(DB,Cnt).

size_db_b(DB,Cnt) :-
    gensym(ctr,Ctr),
    conset(Ctr,0),
    (do_all
     clause_in_db_b(_,_,DB),
     coninc(Ctr)
    ),
    conget(Ctr,Cnt).

:- comment(count_in_db/3, " @pred{count_in_db(+Pref,+DB,-Count)}
returns in @var{Count} the number of tuples specified by prefix term
@var{Pref} in database @var{DB}.  ").
%% should it use clause_in_db instead of call_in_db?
%% this is simple way...
%% might be faster if done by traversing the DB trie.

count_in_db(Pref,DB,Count) :-
    cvt_pa(Pref,Prefa),
    gensym(ctr,Ctr),
    conset(Ctr,0),
    (do_all
     call_in_db(Prefa,DB),
     coninc(Ctr)
    ),
    conget(Ctr,Count).


/******************************************************/

:- comment(subsumed_db/2," @pred{subsumed_db(+DB1,+DB2)} succeeds if
database @var{DB1} is subsumed by database @var{DB2}.  If the
databases contain only facts, then this succeeds if the set of ground
instances of @var{DB1} is a subset of the set of ground instances of
@var{DB2}. If the databases contain rules, then an approximation is
attempted by comparing individual rules.  The databases must be of the
same form.  ").

subsumed_db('$TRIETERM'(DB1),'$TRIETERM'(DB2)) :-
    subsumed_db_b(DB1,DB2).
subsumed_db('$TRIETERMI'(H1),'$TRIETERMI'(H2)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    subsumed_db_b(DB1,DB2).

:- comment(subsumed_in_db/3," @pred{subsumed_in_db(+Pref1,+Pref2,+DB)}
succeeds if the set of tuples specified by prefix term @var{Pref1} is
subsumed by the set of tuples specified by prefix term @var{Pref2} in
database @var{DB}.  If the tuple sets contain only facts, then this
succeeds if the set of ground instances of tuples in @var{DB}
determined by @var{Pref1} is a subset of the set of ground instances
of those determined by @var{Pref2}. If tuple sets contain rules (?),
then an approximation is attempted by comparing individual rules.  ").

    subsumed_in_db(P1,P2,DB) :-
    apply_rel_op_in_db(lambda(A,B,subsumed_db_b(A,B)),P1,P2,DB).

subsumed_db_b(DB1,DB2) :-
    difference_db_b(DB1,DB2,DDB),
    findall((H:-B),clause_in_db_b(H,B,DDB),Clauses),
    all_subsumed(Clauses,DB2).

all_subsumed([],_).
all_subsumed([(Head:-Body)|Clauses],DB) :-
    (Body = true
     ->	subsumed_fact_in_db(Head,DB)
     ;	subsumed_rule_in_db(Head,Body,DB)
    ),
    all_subsumed(Clauses,DB).

subsumed_fact_in_db(G,DB) :-
    term_variables(G,Vars),
    call_in_db_b(G,DB),
    is_most_general_term(Vars).

%% Hack for rules; must exist another single rule with eq-shorter body
%% with all subgoals subsuming this body
subsumed_rule_in_db(Head,Body,DB) :-
    term_variables(Head,Vars),
    clause_in_db_b(Head,GBody,DB),
    is_most_general_term(Vars),
    subsumes_goal_lists(GBody,Body).

subsumes_goal_lists(GBody,Body) :-
    (GBody = (G1,Gs)
     ->	subsumes_some(G1,Body),
	subsumes_goal_lists(Gs,Body)
     ;	subsumes_some(GBody,Body)
    ).

subsumes_some(G,Body) :-
    (Body = (B1,Bs)
     ->	(subsumes_chk(G,B1)
	 -> true
	 ;  subsumes_some(G,Bs)
	)
     ;	subsumes_chk(G,Body)
    ).


/******************************************************/

:- comment(disjoint_db/2, "@pred{disjoint_db(+DB1,+DB2)} succeeds if
the prolog databases @var{DB1} and @var{DB2} are disjoint.  @var{DB1}
and @var{DB2} must be of the same form.  ").

disjoint_db('$TRIETERM'(DB1),'$TRIETERM'(DB2)) :-
    disjoint_db_b(DB1,DB2).
disjoint_db('$TRIETERMI'(H1),'$TRIETERMI'(H2)) :-
    machine:intern_termhash(DB1,H1),
    machine:intern_termhash(DB2,H2),
    disjoint_db_b(DB1,DB2).

:- comment(disjoint_in_db/3, "@pred{disjoint_in_db(+Pref1,+Pref2,+DB)}
succeeds if the tuples specified by prefix term @var{Pref1} are
disjoint from those specified by @var{Pref2} in @var{DB}.  The number
of variables in @var{Pref1} and @var{Pref2} must be the same. @var{DB}
may be of either form.  ").

disjoint_in_db(P1,P2,DB) :-
    apply_rel_op_in_db(lambda(A,B,disjoint_db_b(A,B)),P1,P2,DB).

disjoint_db_b(DB1,DB2) :-
    (DB1 == DB2
     ->	is_empty_db_b(DB1)
     ;	db_comps_d(DB1,HBits1,_Num1,HT1),
     	db_comps_d(DB2,HBits2,_Num2,HT2),
	(HBits1 < HBits2
	 -> rebuild_hashtree(HT1,HBits1,HBits2,HT1b),
	    disjoint_subtrees(HBits2,HT1b,HT2)
	 ; HBits2 < HBits1
	 -> rebuild_hashtree(HT2,HBits2,HBits1,HT2b),
	    disjoint_subtrees(HBits1,HT1,HT2b)
	 ;  disjoint_subtrees(HBits1,HT1,HT2)
	)
    ).

disjoint_subtrees(HashBits,HT1,HT2) :-
    (HashBits =< 0
    ->	disjoint_kvs(HT1,HT2)
     ;	h_comps_d(HT1,HT10,HT11,HT12,HT13),
	h_comps_d(HT2,HT20,HT21,HT22,HT23),
	disjoint_subtree(HashBits,HT10,HT20),
	disjoint_subtree(HashBits,HT11,HT21),
	disjoint_subtree(HashBits,HT12,HT22),
	disjoint_subtree(HashBits,HT13,HT23)
    ).

disjoint_subtree(HashBits,SHT1,SHT2) :-
    (SHT1 == []
     ->	true
     ; SHT2 == []
     ->	true
     ; SHT1 == SHT2
     ->	fail
     ;	NHashBits is HashBits - 2,
	disjoint_subtrees(NHashBits,SHT1,SHT2)
    ).

disjoint_kvs(HT1,HT2) :-
    (HT1 == []
     ->	true
     ; HT2 == []
     ->	true
     ;	HT1 = [K1-V1|HT1t],
	HT2 = [K2-V2|HT2t],
	(K1 @< K2
	 -> disjoint_kvs(HT1t,HT2)
	 ;  K1 @> K2
	 -> disjoint_kvs(HT1,HT2t)
	 ;  (V1 @= ht(_,_,_)
	     ->	disjoint_db_b(V1,V2)
	     ;	disjoint_values(V1,V2)
	    ),
	    disjoint_kvs(HT1t,HT2t)
	)
    ).
    
disjoint_values(V1s,V2s) :-
    (V1s == []
     ->	true
     ; V2s == []
     ->	true
     ;	V1s = [V1|V1st],
	V2s = [V2|V2st],
	(V1 @< V2
	 -> disjoint_values(V1st,V2s)
	 ;  V1 @> V2
	 -> disjoint_values(V1s,V2st)
	 ;  fail
	)
    ).

/**********************************************************/

:- comment(equal_in_db/3, "@pred{equal_in_db(P1,P2,DB)} succeeds if
the set of tuples specified by prefix term @var{P1} is the same as the
set of tuples specified by prefix term @var{P2}.  @var{DB} may be of
either regular or intern form.  ").

equal_in_db(P1,P2,DB) :-
    apply_rel_op_in_db(lambda(A,B,(A==B)),P1,P2,DB).


/**********************************************************/

db_comps_d(DB,B,N,H) :-
    (DB == []
     ->	B = 2, N = 0, H = []
     ;	DB = ht(B,N,H)
    ).

db_comps_c(DB,B,N,H) :- 
    (H == []
     ->	DB = H
     ;	DB = ht(B,N,H)
    ).

%% -> deconstruct
h_comps_d(H,H1,H2,H3,H4) :-
    (H == []
     ->	H1 = [], H2 = [], H3 = [], H4 = []
     ;	H = h(H1,H2,H3,H4)
    ).

%% <- construct
h_comps_c(H,H1,H2,H3,H4) :-
    (H1 == [], H2 == [], H3 == [], H4 == []
     ->	H = []
     ;	H = h(H1,H2,H3,H4)
    ).

add_kv(V,K,A,HT0,HT) :-
    (V = []
     ->	HT0 = HT,
	A = 0
     ;	HT0 = [K-V|HT],
	A = 1
    ).

count_keys([],0).
count_keys([_|R],N) :-
    (R == []
     ->	N = 1
     ;	length(R,N1),
	N is N1+1
    ).
count_keys(h(HT1,HT2,HT3,HT4),N) :-
    count_keys(HT1,N1),
    count_keys(HT2,N2),
    count_keys(HT3,N3),
    count_keys(HT4,N4),
    N is N1+N2+N3+N4.

fix_ht_size(_HBits,0,HT,[]) :- !,
    (HT == []
     ->	true
     ;	standard:writeln(userout,'ERROR in ht num, internal bug!! ')
    ).
fix_ht_size(HBits,NNum,HT,DB) :-
    (NNum =< 1
     ->	GHBits = 2
     ;	GHBits is 2**ceiling(log(log(NNum+1)/log(2))/log(2))
    ),
    (GHBits =:= HBits
     ->	db_comps_c(DB,HBits,NNum,HT)
     ;	rebuild_hashtree(HT,HBits,GHBits,NHT),
	db_comps_c(DB,GHBits,NNum,NHT)
    ).

