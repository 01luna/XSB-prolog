; Script generated by the Inno Setup Script Wizard.

#define MyAppName "XSB"
#define MyAppVerName "XSB 3.1"
#define MyAppPublisher "XSB"
#define MyAppURL "http://xsb.sourceforge.net/"
#define MyAppUrlName "XSB Web Site.url"

#define XSB_DIR "{reg:HKLM\SYSTEM\CurrentControlSet\Control\Session Manager\Environment,XSB_DIR|{pf}\XSB}"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
ChangesEnvironment=yes
DefaultDirName={#XSB_DIR}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=C:\xsb\LICENSE
InfoBeforeFile=C:\xsb\README
OutputBaseFilename=xsb-3.1
Compression=lzma
SolidCompression=yes
PrivilegesRequired=none

VersionInfoVersion=3.1
VersionInfoCopyright=© Paulo Moura, 2007

AllowRootDirectory=yes
UninstallFilesDir="{userdocs}\XSB uninstaller"

MinVersion=0,5.0

[Types]
Name: "base"; Description: "XSB installation"

[Components]
Name: "base"; Description: "Base system"; Types: base; Flags: disablenouninstallwarning

[Tasks]
Name: website; Description: "&Visit {#MyAppName} web site"; Components: base
Name: shortcut; Description: "&Create a desktop shortcut to the XSB folder"; Components: base

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
BeveledLabel=XSB 3.1 © The Research Foundation of SUNY, 1986, 1993-2002

[Dirs]
Name: "{userdocs}\XSB uninstaller"

[Files]
Source: "C:\XSB\*"; Excludes: ".*,CVS"; DestDir: "{app}"; Components: base; Flags: ignoreversion recursesubdirs createallsubdirs

[INI]
Filename: "{app}\{#MyAppUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#MyAppURL}"; Components: base

[Icons]
Name: "{group}\License"; Filename: "{app}\LICENSE"; Components: base
Name: "{group}\Read Me"; Filename: "{app}\README"; Components: base

Name: "{group}\Web Site"; Filename: "{#MyAppUrl}"; Components: base

Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; Components: base

[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "XSB_DIR"; ValueData: "{app}"; Components: base; Flags: deletevalue uninsdeletevalue

[Run]
Filename: "{app}\{#MyAppUrlName}"; Flags: shellexec nowait; Tasks: website

[UninstallDelete]
Type: filesandordirs; Name: "{app}"; Components: base
Type: filesandordirs; Name: "{group}"; Components: base
