:- compiler_options([ciao_directives]).

:- document_export
	delete_class_cascade/1,
	obliterate_class/1,obliterate_classes/1,
	obliterate_objects/1,
	retractall_itsobjects/1,
	retractall_subclasses/1.

:- import member/2 from basics.

/* This import statement is not needed for execution -- just for
checking dead code, and for understanding the interface between the
files.

:- import
	attribute_object/3, class/4, 
	immediate_attribute/3, immediate_relationship/3, 
	immediate_schrel/3, strel/3,
	object/4,
	retractall_attribute/3, retractall_attribute_object/3,
	retractall_class/4, retractall_memberof/2,
	retractall_object/4,  retractall_relationship/3,
	retractall_schrel/3, retractall_strel/3,  retractall_subclass/2 
	from oms_init_oms.
*/

:- comment(module," Please document!").

%% handle caches

 /* obliterate_concept takes a CID and removes it from the taxonomy and
then remeoves everything else that becomes disconnected. */

obliterate_class(Cid) :-
	(findall(PCid,immediate_subclass(Cid,PCid),PCids),
	 member(PCid,PCids),
	 retractall_subclass(Cid,PCid),
	 fail
	 ;
	 gc_class(Cid)
	).

retractall_itsobjects(Cid):-
	findall(Oid,immediate_memberof(Oid,Cid),Oids),
	(
	 member(Oid,Oids),
	 retractall_memberof(Oid,Cid),
	 \+ immediate_memberof(Oid,_), 
	 obliterate_object(Oid),
	 fail
	 ;
	 true
	).

obliterate_objects(ObjList) :-
	findall(p(Oid,Cid),(member(Oid,ObjList),
		     retractall_object(Oid,_,_,_),
		     immediate_memberof(Oid,Cid)),
		Cids),
	member(p(Oid,Cid),Cids),
	retractall_memberof(Oid,Cid),
	fail.
obliterate_objects(_) :- gc_attribute_fail.
obliterate_objects(_) :- gc_attribute_object_fail.
obliterate_objects(_).

obliterate_object(Oid) :-
	retractall_attribute(Oid,_,_),
	retractall_attribute_object(Oid,_,_),
	retractall_attribute_object(_,_,Oid),
	retractall_memberof(Oid,_),
	retractall_object(Oid,_,_,_).

obliterate_classes(Cids) :-
	(findall(p(Cid,PCid),
		 (member(Cid,Cids),immediate_subclass(Cid,PCid)),
		 PCids),
	 member(p(Cid,PCid),PCids),
	 retractall_subclass(Cid,PCid),
	 fail
	 ;
	 gc_classes(Cids)
	).


/* gc_class takes a class and if it is not connected in the
taxonomy, deletes it and all other implied unconnected OMS components.
It is primarily intended to be called after retractall_subclass, to
transitively remove the newly detached subclass (if it is indeed
detached) and all newly detached OMS components.  */

gc_class(Cid) :-
	\+ immediate_subclass(Cid,_),
	gc_class_fail(Cid).
gc_class(_) :- gc_allrels_fail.
gc_class(_).

gc_classes(Cids) :-
	member(Cid,Cids),
	\+ immediate_subclass(Cid,_),
	gc_class_fail(Cid).
gc_classes(_) :- gc_allrels_fail.
gc_classes(_).

gc_class_fail(Cid) :-
	retractall_class(Cid,_,_,_),
	(findall(Oid,immediate_memberof(Oid,Cid),Oids),
	 member(Oid,Oids),
	 retractall_memberof(Oid,Cid),
	 \+ immediate_memberof(Oid,_),
	 retractall_object(Oid,_,_,_),
	 fail
	 ;
	 findall(CCid,immediate_subclass(CCid,Cid),CCids),
	 member(CCid,CCids),
	 retractall_subclass(CCid,Cid),
	 \+ immediate_subclass(CCid,_),
	 gc_class_fail(CCid),
	 fail
	).


gc_allrels_fail :- gc_relationship_fail.
gc_allrels_fail :- gc_schrel_fail.
gc_allrels_fail :- gc_strel_fail.
gc_allrels_fail :- gc_attribute_fail.
gc_allrels_fail :- gc_attribute_object_fail.

gc_relationship_fail :-
	findall(r(A,B,C),orphan_relationship(A,B,C),Rlns),
	member(r(A,B,C),Rlns),
	retractall_relationship(A,B,C),
	fail.

gc_schrel_fail :-
	findall(r(A,B,C),orphan_schrel(A,B,C),Rlns),
	member(r(A,B,C),Rlns),
	retractall_schrel(A,B,C),
	fail.

gc_strel_fail :-
	findall(r(A,B,C),orphan_strel(A,B,C),Rlns),
	member(r(A,B,C),Rlns),
	retractall_strel(A,B,C),
	fail.

gc_attribute_fail :-
	findall(r(A,B,C),orphan_attribute(A,B,C),Rlns),
	member(r(A,B,C),Rlns),
	retractall_attribute(A,B,C),
	fail.

gc_attribute_object_fail :-
	findall(r(A,B,C),orphan_attribute_object(A,B,C),Rlns),
	member(r(A,B,C),Rlns),
	retractall_attribute_object(A,B,C),
	fail.

orphan_relationship(A,B,C) :-
	immediate_relationship(A,B,C),
	\+ (class(A,_,_,_), class(B,_,_,_), class(C,_,_,_)).
	 
orphan_schrel(A,B,C) :-
	immediate_schrel(A,B,C),
	\+ (class(A,_,_,_), class(B,_,_,_), class(C,_,_,_)).
	 
orphan_strel(A,B,C) :-
	strel(A,B,C),
	\+ (class(A,_,_,_), class(B,_,_,_), class(C,_,_,_)).
	 
orphan_attribute(A,B,C) :-
	immediate_attribute(A,B,C),
	\+ (object(A,_,_,_), class(B,_,_,_), class(C,_,_,_)).
	 
orphan_attribute_object(A,B,C) :-
	attribute_object(A,B,C),
	\+ (object(A,_,_,_), class(B,_,_,_), object(C,_,_,_)).

/* Delete a class, all its subclasses, and all their relations
and attributes */
delete_class_cascade(ACid) :-
	obliterate_class(ACid).

retractall_subclasses(Cid):-
	findall(Sid,immediate_subclass(Sid,Cid),Sids),
	(
	 member(Sid,Sids),
	 \+ thereis_another_parent(Sid,Cid),
	 obliterate_class(Sid),
	 fail
	 ;
	 true
	).

thereis_another_parent(Sid,Cid):-
	immediate_subclass(Sid,X),\+(X=Cid).



