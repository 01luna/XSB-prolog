/* File:      flrutils.P
**
** Author(s): Bertram Ludaescher (V 1.2, 11/13/1998)
**
**            Guizhen Yang       (V 2.0, 04/12/1999)
**            Guizhen Yang	 (V 2.5, 05/06/1999)
**              supervised by Michael Kifer
**
** Contact:   xsb-contact@cs.sunysb.edu
** 
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
*/


:- import
	put/1, write/1, tab/1, writeln/1, write_canonical/1, display/1,
	atom_chars/2, see/1, seen/0, tell/1, told/0, seeing/1, telling/1,
	file_exists/1, cputime/1, var/1
   from standard.

:- import append/3, member/2 from basics.

:- import
	consult/1, consult/2, compile/2, load_dyn/1
   from consult.

:- import sort/2, findall/3 from setof.

:- import bootstrap_package/2, package_configuration/2 from packaging.

:- import library_directory/1 from usermod.

:- import trie_assert/1, trie_retract_all/1 from tables.

:- import slash/1, is_absolute_filename/1, parse_filename/4 from machine.

:- import fmt_write_string/3, file_time/2 from file_io.

:- import unix/1 from unix.

:- import read_tokens/2 from flrtokens.

:- import rule/3 from flrparser.

:- import set_load_mode/1, compile_flogic/3 from flrcompiler.

:- dynamic flora_maxerr(_).

:- export 
	'_$_$_flora_flcompile'/5,
	'_$_$_flora_read_parse_compile'/6,
	'_$_$_flora_loadlist'/1,
	'_$_$_flora_dynloadlist'/1,
	'_$_$_flora_check_filename'/1,
	flcompile/1, flcompile/2, flconsult/1, flconsult/2, dyncompile/1,
	dyncompile/2, dynconsult/1, dynconsult/2, load/1, dynload/1,
	maxerr/1, flora_prefix/1, flora_word/2, terminate_list/1,
	flora_member/2, displayln_errmsg/1, displayln/0, displayln/1.


/*********************************************************************
  03/1999, modified by Guizhen Yang (guizyang@CS.SunySB.EDU)
*********************************************************************/


/********************************************************************/
flora_prefix("_$_$_flora_").

flora_word(S,A) :-
	atom_chars(S,Str),
	flora_prefix(P),
	append(P,Str,L),
	atom_chars(A,L).


/********************************************************************/
terminate_list([]) :- !.
terminate_list([_|T]) :- terminate_list(T).


/********************************************************************/
flora_member(NV,NVs) :-
	var(NVs),
	!,
	NVs=[NV|_].

flora_member(var(N,V),[var(N,V)|_]) :-
	N \= '_',
	!.

flora_member(NV,[_|NVs]) :-
	flora_member(NV,NVs).


/********************************************************************/
displayln_errmsg(A) :-
	display('+++ Error: '),
	displayln(A).

displayln :-
	telling(F),
	tell(userout),
	put(10),
	tell(F).

displayln(A) :-
	telling(F),
	tell(userout),
	write(A),
	put(10),
	tell(F).


/*********************************************************************/
flcompile(InFile,Dtvs) :-
	'_$_$_flora_flcompile'(static_mode,Dtvs,InFile,force,Module),
	displayln,
	displayln('*** FLORA: calling XSB ...'),
	compile(Module,[optimize,spec_repr]).

flcompile(InFile) :-
	flora_default_directives(Dtvs),
	flcompile(InFile,Dtvs).


/*********************************************************************/
dyncompile(InFile,Dtvs) :-
	'_$_$_flora_flcompile'(dynamic_mode,Dtvs,InFile,force,Module),
	displayln,
	displayln('*** FLORA: calling XSB ...'),
	compile(Module,[optimize,spec_repr]).

dyncompile(InFile) :-
	flora_default_directives(Dtvs),
	dyncompile(InFile,Dtvs).


/*********************************************************************/
flconsult(File,Dtvs) :- 
	'_$_$_flora_flcompile'(static_mode,Dtvs,File,noforce,Module),
	displayln, display('*** FLORA: consulting '),
	display(Module), displayln(' ...'),
	consult(Module,[optimize,spec_repr]).

flconsult(File) :- 
	flora_default_directives(Dtvs),
	flconsult(File,Dtvs).


/*********************************************************************/
dynconsult(File,Dtvs) :- 
	'_$_$_flora_flcompile'(dynamic_mode,Dtvs,File,noforce,Module),
	displayln, display('*** FLORA: dynamically loading '),
	display(Module), displayln(' ...'),
	load_dyn(Module).

dynconsult(File) :- 
	flora_default_directives(Dtvs),
	dynconsult(File,Dtvs).


/*********************************************************************/
'_$_$_flora_loadlist'([X]) :- load(X).

'_$_$_flora_loadlist'([X1,X2|Xs]) :-
	load(X1),
	'_$_$_flora_loadlist'([X2|Xs]).


/*********************************************************************/
'_$_$_flora_dynloadlist'([X]) :- dynload(X).

'_$_$_flora_dynloadlist'([X1,X2|Xs]) :-
	dynload(X1),
	'_$_$_flora_dynloadlist'([X2|Xs]).


/*********************************************************************/
load(File) :-
	'_$_$_flora_check_filename'(File),
	parse_filename(File,D,B,E),
	( E = 'flr' ->
	    flconsult(File)

	  ; (E = 'P'; E = 'O') ->
	    flora_locate_file(File,TFile),
	    ( file_exists(TFile) ->
		displayln, display('*** FLORA: consulting '),
		display(TFile), displayln(' ...'),
		consult(File)

	      ;
		displayln, display('+++ Error: '),
		display(File), displayln(' does NOT exist !'),
		fail
	    )

	  ; E \= '' ->
	    displayln,
	    displayln('+++ Error: file extension must be flr, P or O !'),
	    fail

	  ;
	    fmt_write_string(FlrName,'%s%s.flr',f(D,B)),
	    flora_locate_file(FlrName,FlrFile),
	    ( file_exists(FlrFile) ->
		flconsult(FlrFile)

	      ;
	        fmt_write_string(PName,'%s%s.P',f(D,B)),
	        flora_locate_file(PName,PFile),
		( file_exists(PFile) ->
		    displayln, display('*** FLORA: consulting '),
		    display(PFile), displayln(' ...'),
		    consult(PFile,[optimize,spec_repr])

		  ;
		    fmt_write_string(OName,'%s%s.O',f(D,B)),
	            flora_locate_file(OName,OFile),
		    ( file_exists(OFile) ->
			displayln, display('*** FLORA: consulting '),
			display(OFile), displayln(' ...'),
			consult(OFile,[optimize,spec_repr])

		      ;
		        displayln, display('+++ Error: '),
			display(File), displayln(' does NOT exist !'),
			fail
		    )
		)
	    )
	).


/*********************************************************************/
dynload(File) :-
	'_$_$_flora_check_filename'(File),
	parse_filename(File,D,B,E),
	( E = 'flr' ->
	    dynconsult(File)

	  ; E = 'P' ->
	    flora_locate_file(File,TFile),
	    ( file_exists(TFile) ->
		displayln, display('*** FLORA: dynamically loading '),
		display(TFile), displayln(' ...'),
		load_dyn(File)

	      ;
		displayln, display('+++ Error: '),
		display(File), displayln(' does NOT exist !'),
		fail
	    )

	  ; E \= '' ->
	    displayln,
	    displayln('+++ Error: file extension must be flr or P!'),
	    fail

	  ;
	    fmt_write_string(FlrName,'%s%s.flr',f(D,B)),
	    flora_locate_file(FlrName,FlrFile),
	    ( file_exists(FlrFile) ->
		dynconsult(FlrFile)

	      ;
	        fmt_write_string(PName,'%s%s.P',f(D,B)),
	        flora_locate_file(PName,PFile),
		( file_exists(PFile) ->
		    displayln, display('*** FLORA: dynamically loading '),
		    display(PFile), displayln(' ...'),
		    load_dyn(PFile)

		  ;
		    displayln, display('+++ Error: '),
		    display(File), displayln(' does NOT exist !'),
		    fail
		)
	    )
	).


/**********************************************************************
  '_$_$_flora_flcompile'(+LoadMode,+Directives,+InFile,+Force,-OutFile)
  InFile must have '.flr' as extension or no extension (in this case,
  '.flr' is appended).
**********************************************************************/
'_$_$_flora_flcompile'(LoadMode,Directives,InFile,Force,OutFile) :-
	flora_maxerr(MaxErr),
	flora_check_directives(Directives,Dtvs),
	'_$_$_flora_check_filename'(InFile),
	parse_filename(InFile,_,_,E),
	( E = 'flr' ->
	    FullFileName=InFile
	  ;
	    fmt_write_string(FullFileName,'%s.flr',f(InFile))
	),
	flora_locate_file(FullFileName,FlrFile),
	( not file_exists(FlrFile) ->
	    displayln, display('+++ Error: '),
	    display(FlrFile),
	    displayln(' does NOT exist !'),
	    fail

	  ;
	    parse_filename(FlrFile,D,B,_),
	    ( LoadMode == static_mode ->
	        fmt_write_string(PFile,'%s%s.P',f(D,B))
	      ;
	        fmt_write_string(PFile,'%s%s_dyn.P',f(D,B))
	    ),
	    ( Force = force ->
		generate_code(LoadMode,Dtvs,FlrFile,PFile,MaxErr,Status)
	      ;
	        ( LoadMode == static_mode ->
		    fmt_write_string(OFile,'%s%s.O',f(D,B))
		  ;
		    OFile=PFile
		),
		( file_older_than(OFile,FlrFile) ->
		    generate_code(LoadMode,Dtvs,FlrFile,PFile,MaxErr,Status)
		  ;
		    Status=noneed
		)
	    ),
	    ( Status = failure ->	fail

	      ; Status = noneed -> OutFile=OFile

	      ;
		OutFile=PFile
	    )
	),
	!.


/********************************************************************/
write_tokens(S,Ts) :-
	telling(F),
	tell(S),
	write_tokens(Ts),
	tell(F).


write_tokens([]).

write_tokens([X|Xs]) :-
	write_tokens(X),
	write_tokens(Xs).

write_tokens(atom(white_space)) :- !, write(' ').
write_tokens(atom(rule_delimeter)) :- !, write('.').

write_tokens(atom(A)) :-
	!,
	atom_chars(A,[_|L]),
	atom_chars(B,L),
	write(B).

write_tokens(identifier(X)) :- !, write(X).
write_tokens(num(N)) :- !, write(N).
write_tokens(var(V)) :- !, write(V).
write_tokens(string_atom(X)) :- !, write_canonical(X).

write_tokens(string_list(X)) :-
	!,
	atom_chars(A,X),
	write('"'),
	write(A),
	write('"').


/********************************************************************/
write_rules([]).

write_rules([null|Rs]) :- !, write_rules(Rs).

write_rules([R|Rs]) :-
	write_rules(R),
	nl,
	write_rules(Rs).

write_rules(fact(H)) :-
	!,
	write_canonical(H),
	writeln('.').

write_rules(rule(H,Gs)) :-
	!,
	write_canonical(H),
	writeln(' :- '),
	write_body(Gs).

write_rules(query(Body,NVs)) :-
	!,
	write('?- '),
	flora_word(print_all,A),
	write_canonical(A),
	write('('),
	write_canonical(Body),
	write(','),
	write_canonical(NVs),
	writeln(').'),
	nl,
	flora_word(tag,T),
	write_canonical(T), writeln('(query).').

write_rules(unbound_warning(F,L)) :-
	!,
	write('%%% '),
	telling(S),
	unbound_warning(S,F,L).

write_rules(import_directive(PAs,M)) :-
	!,
	writeln(':- import'),
	write_PAs(PAs),
	nl, write('   from '), write_canonical(M), writeln('.').

write_rules(export_directive(PAs)) :-
	!,
	writeln(':- export'),
	write_PAs(PAs),
	writeln('.').

write_rules(table_directive(auto_table)) :-
	!,
	writeln(':- auto_table.').

write_rules(table_directive(PAs)) :-
	!,
	writeln(':- table'),
	write_PAs(PAs),
	writeln('.').


/********************************************************************/
write_body([X]) :-
	!,
	tab(8),
	write_canonical(X),
	writeln('.').

write_body([X1,X2|Xs]) :-
	!,
	tab(8),
	write_canonical(X1),
	writeln(','),
	write_body([X2|Xs]).

/********************************************************************/
write_PAs([pa(P,A)]) :-
	!,
	tab(8),
	write_canonical(P),
	write('/'),
	write(A).

write_PAs([pa(P,A)|PAs]) :-
	tab(8),
	write_canonical(P),
	write('/'),
	write(A),
	writeln(','),
	write_PAs(PAs).


/********************************************************************/
unbound_warning(S,Form,L) :-
	telling(F),
	tell(S),
	write('*** Warning: '),
	write_list(L),
	write(' unbound in '),
	writeln(Form),
	tell(F).


/********************************************************************/
write_list([]).

write_list([X]) :-
	!,
	write(X).

write_list([X1,X2|Xs]) :-
	!,
	write(X1),
	write(', '),
	write_list([X2|Xs]).


/********************************************************************/
generate_code(LoadMode,Directives,FlrFile,PFile,MaxErr,Status) :-
	displayln, display('*** FLORA: compiling '), display(FlrFile),
	(LoadMode == static_mode -> Str="static" ; Str="dynamic"),
	atom_chars(A,Str),
	display(' to '), display(A), displayln(' code,'),
	display('           with option(s) '),
	telling(F), tell(userout), write_list(Directives), tell(F),
	displayln(' ...'),
	cputime(T0),
	seeing(PreInFile),
	telling(PreOutFile),
	tell(PFile),
	( LoadMode == static_mode ->
	    copy_flrfile(closure,'flrheader.P',S1)
	  ;
	    copy_flrfile(closure,'flrheader_dyn.P',S1)
	),
	( S1 = failure ->
	    Status=failure
	  ;
	    see(FlrFile),
	    set_load_mode(LoadMode),
	    compile_flrfile(MaxErr,0,S2,[],Options),
	    seen,
	    ( S2 = failure ->
		Status=failure
	      ;
	        directives_options(LoadMode,Directives,Options,Status)
	    )
	),
	told,
	see(PreInFile),
	tell(PreOutFile),
	cputime(T1),
	( Status = failure ->
	    true
	  ;
	    displayln, display('*** Done! CPU time used: '),
	    T is T1-T0,
	    display(T), display(' seconds on '), unix(hostname)
	).


/********************************************************************/
directives_options(LoadMode,Directives,Options,Status) :-
	( member(flraggregate,Options) ->
	    ( LoadMode == static_mode ->
		FlrAgg='flraggregate.P'
	      ;
	        FlrAgg='flraggregate_dyn.P'
	    ),
	    copy_flrfile(lib,FlrAgg,S1)
	  ;
	    S1=success
	),
	( member(flrassert,Options) ->
	    ( LoadMode == static_mode ->
		FlrAss='flrassert.P'
	      ;
	        FlrAss='flrassert_dyn.P'
	    ),
	    copy_flrfile(lib,FlrAss,S2)
	  ;
	    S2=success
	),
	(member(eqlevel(N1),Directives) -> true ; N1=0),
	(member(skolem,Options) -> N2=1 ; N2=0),
	( N2 == 1, N1 == 0 ->
	    displayln,
	    displayln('*** Warning: skolemization in rule head performed')
	  ;
	    true
	),
	external_options(Options,TempOpts),
	flora_check_directives(TempOpts,Opts),
	( member(eqlevel(N3),Opts) ->
	    displayln, display('*** FLORA: compiler option '),
	    display(eqlevel(N3)), display(' detected in source code '),
	    ( N3 > N1 ->
		displayln('(overriding)')

	      ; N1 > N3 ->
	        displayln('(overridden)')
	      ;
	        displayln
	    )
	  ;
	    N3=0
	),
	(N1 < N2 -> N4=N2 ; N4=N1),
	(N3 < N4 -> Level=N4 ; Level=N3),
	( LoadMode == static_mode ->
	    fmt_write_string(Trailer,'flrtrailer%d.P',f(Level))
	  ;
	    fmt_write_string(Trailer,'flrtrailer%d_dyn.P',f(Level))
	),
	copy_flrfile(closure,Trailer,S3),
	( S1=success, S2=success, S3=success ->
	    Status=success
	  ;
	    Status=failure
	).


/********************************************************************/
external_options([],[]).

external_options([X|Ints],Exts) :-
	internal_option(X),
	!,
	external_options(Ints,Exts). 

external_options([X|Ints],[X|Exts]) :- external_options(Ints,Exts). 

internal_option(flraggregate) :- !.
internal_option(flrassert)    :- !.
internal_option(skolem)       :- !.


/********************************************************************/
copy_flrfile(Dir,File,Status) :-
	( package_configuration(dir(flora),FloraDir) ->
	    true
	  ;
	    bootstrap_package(flora,flora),
	    package_configuration(dir(flora),FloraDir)
	),
	slash(S),
	fmt_write_string(FullName,'%s%s%s%s%s',f(FloraDir,S,Dir,S,File)),
	( flora_file_exists(FullName) ->
	    flora_output_file(FullName),
	    Status=success
	  ;
	    Status=failure
	).


/********************************************************************/
flora_file_exists(File) :-
	( file_exists(File) ->
	    true
	  ;
	    displayln, display('+++ Error: '), display(File),
	    displayln(' does NOT exist !'), fail
	).


/********************************************************************/
flora_output_file(File) :-
	seeing(In),
	see(File),
	repeat,
	  get0(C),
	  (C = -1 -> true ; put(C)),
	  C = -1,
	!,
	seen,
	see(In).


/********************************************************************/
compile_flrfile(MaxErr,ErrorCount,Status,OldOpts,NewOpts) :-
	read_parse_compile([],Tokens,FlattenedRules,S1,Opts),
	( member(error(_),S1) ->
	    NewCount is ErrorCount+1,
	    (MaxErr=all -> M is NewCount+1 ; M=MaxErr),
	    ( M =< NewCount ->
		Status=failure,
		displayln, display('+++ aborted due to '),
		display(NewCount),
		displayln(' error(s) found'), displayln

	      ; member(not_eof,S1),
		member(atom(rule_delimeter),Tokens) ->
		  compile_flrfile(MaxErr,NewCount,Status,_,_)

	      ; member(not_eof,S1), discard_tokens(S2),
		member(not_eof,S2) ->
		  compile_flrfile(MaxErr,NewCount,Status,_,_)
	      ;
		Status=failure,
		displayln, display('+++ '), display(NewCount),
		displayln(' error(s) found'),
		display('+++ aborted due to end of file'), displayln
	    )

	  ;
	    ( member(unbound_warning(Form,L),FlattenedRules) ->
		displayln, unbound_warning(userout,Form,L)
	      ;
		true
	    ),
	    write('%%% '),
	    telling(File), write_tokens(File,Tokens), nl,
	    write_rules(FlattenedRules), nl,
	    ( ErrorCount > 0 ->
		true
	      ;
		append(OldOpts,Opts,Temp1),
		sort(Temp1,Temp2)
	    ),
	    ( member(eof,S1) ->
		( ErrorCount = 0 ->
		    Status=success,
		    NewOpts=Temp2
		  ;
		    Status=failure,
		    displayln, display('+++ '), display(ErrorCount),
		    displayln(' error(s) found'),
		    display('+++ aborted due to end of file'),
		    displayln 
		)
	      ;
		compile_flrfile(MaxErr,ErrorCount,Status,Temp2,NewOpts)
	    )
	),
	!.


/********************************************************************/
'_$_$_flora_read_parse_compile'(LoadMode,L,Tokens,FlattenedRules,Status,Opts) :-
	set_load_mode(LoadMode),
	read_parse_compile(L,Tokens,FlattenedRules,Status,Opts).


/********************************************************************/
read_parse_compile(L,Tokens,FlattenedRules,Status,Opts) :-
	read_tokens(Tokens,ReadStatus),
	( member(error(Msg),ReadStatus) ->
	    Status=ReadStatus,
	    FlattenedRules=[null],
	    displayln, display('>>>   '),
	    write_tokens(userout,Tokens),
	    displayln(' <--- here?'),
	    displayln_errmsg(Msg)

	  ; L = [atom('$?'),atom('$-')], rule(null,Tokens,[]) ->
	    Status=[error(noop)|ReadStatus]

	  ;
	    append(L,Tokens,Ts),
	    ( rule(FlogicRule,Ts,[]) ->
		( compile_flogic(FlogicRule,FlattenedRules,Opts) ->
		    Status=ReadStatus
		  ;
		    FlattenedRules=[null],
		    displayln, display('>>>   '),
		    write_tokens(userout,Tokens), displayln,
		    displayln_errmsg('compiling error'),
		    Status=[error('compiling error')|ReadStatus]
		)

	      ;
		displayln, display('>>>   '),
		write_tokens(userout,Tokens), displayln,
		displayln_errmsg('parsing error'),
		FlogicRule=[null],
		Status=[error('parsing error')|ReadStatus]
	    )
	).


/********************************************************************/
discard_tokens(Status) :-
	displayln,
	repeat,
	displayln('... discarding tokens ...'),
	read_tokens(Tokens,Status),
	( member(atom(rule_delimeter),Tokens), !
	 ;
	  member(eof,Status), !
	).


/*********************************************************************/
flora_check_directives(Original,Final) :-
	flora_directives(Original,L1),
	findall(X1,(member(X1,L1),X1\=eqlevel(_)),L2),
	findall(eqlevel(X2),member(eqlevel(X2),L1),L3),
	( L3 = [] ->
	    L4=[]
	  ;
	    member(eqlevel(N1),L3),
	    not (member(eqlevel(N2),L3),N2>N1),
	    L4=[eqlevel(N1)]
	),
	!,
	append(L4,L2,Final).

flora_directives([],[]) :- !.

flora_directives([H|T],Opts) :-
	( flora_directive(H) ->
	    Opts=[H|R],
	    flora_directives(T,R)

	  ;
	    displayln, display('*** Warning: unknown compiler option '),
	    display(H), displayln(' (discarded)'),
	    flora_directives(T,Opts)
	),
	!.

flora_directives(_,_) :-
	displayln,
	displayln('+++ Error: compiler directive must be a LIST of options !'),
	fail.

flora_directive(eqlevel(N)) :- 0 =< N, N =< 1, !.

flora_default_directives([eqlevel(0)]).


/*********************************************************************/
'_$_$_flora_check_filename'(Name) :-
	( not atom(Name) ->
	    displayln, displayln('+++ Error: invalid file name'),
	    fail

	  ;
	    true
	).


/*********************************************************************/
flora_locate_file(In,In) :-
	is_absolute_filename(In),
	!.

flora_locate_file(In,Out) :-
	flora_library_directory(LibDir),
	slash(S),
	fmt_write_string(Out,'%s%s%s',f(LibDir,S,In)),
	file_exists(Out),
	!.

flora_locate_file(In,In).


/*********************************************************************/
file_older_than(F1,F2) :-
	( file_exists(F1) ->
	    file_time(F1,time(F1Time1,F1Time2)),
	    file_time(F2,time(F2Time1,F2Time2)),
	    time(F1Time1,F1Time2) @< time(F2Time1,F2Time2)

	  ;
	    true
	).


/*********************************************************************/
flora_library_directory('.').
flora_library_directory(X) :- library_directory(X).


/*********************************************************************/
:- maxerr(5).

maxerr(X) :-
	( var(X) ->
	    flora_maxerr(X)

	  ; X \== all, (not integer(X); X < 1) ->
	    displayln,
	    displayln('+++ Error: invalid argument to maxerr'),
	    fail

	  ;
	    trie_retract_all(flora_maxerr(_)),
	    trie_assert(flora_maxerr(X))
	).


/*********************************************************************/
