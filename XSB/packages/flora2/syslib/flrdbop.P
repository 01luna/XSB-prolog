/* File:      flrdbop.P
**
** Author(s): Guizhen Yang 
**
** Contact:   flora-users@lists.sourceforge.net
**
** Copyright (C) The Research Foundation of SUNY, 1999-2001
**
** FLORA-2 is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** FLORA-2 is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with FLORA-2; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
** 
** 
*/


:- compiler_options([xpp_on]).

#include "flora_terms.flh"
#include "flora_porting.flh"
#include "flora_exceptions.flh"


:- import append/3 from basics.

:- import
	flora_fdb_storage_name/2,
	flora_check_module_name/1,
	flora_decode_module_name/2,
	flora_is_flogic_wrapper/3,
	flora_setup_flogic_fact_wrapper/1,
	flora_flogic_fact_wrapper/2
   from flrwrapper.

:- import flora_list2conjunct/2 from flrutils.

:- import
	flora_storage_check_module_name/1,
	flora_storage_check_deletepredicate/2,
	flora_storage_convertlist/3,
	flora_storage_is_negation_symbol/1,
        flora_storage_check_existence/1
   from flrstorageutils.

:- import
	flora_db_find_base/2,
	flora_db_insert_base/2,
	flora_db_delete_base/2
   from flrstoragebase.

:- import 
	get_canonical_form/2,
	convert_to_head_literal/2
   from flrcanon.

:- import flora_refresh_tables/1 from flrtables.

:- import flora_abort/0, flora_abort/1 from flrutils.
:- import flora_decode_goal_as_atom/2 from flrdecode.

:- import flora_check_export_registry/4 from flrexport.

:- import P2H_PREDICATE/4 from prolog2hilog.

/*********************************************************************
  This file is for the following non-backtrackable FLORA database
  operations: insert, insertall, delete, deleteall, erase, eraseall.
*********************************************************************/


/*****************************************************************************
  fllibinsert(+List)
*****************************************************************************/
FLLIBINSERT(ThisModuleName,List) :- FLLIBINSERT(ThisModuleName,List,true).


/*****************************************************************************
  fllibinsert(+List,+Condition)
*****************************************************************************/
FLLIBINSERT(ThisModuleName,List,Condition) :-
	call(Condition),
	flora_storage_insertfacts(ThisModuleName, List).


/*****************************************************************************
  flora_storage_insertfacts(+List)
*****************************************************************************/
flora_storage_insertfacts(_ThisModuleName,[]) :- !.

flora_storage_insertfacts(_ThisModuleName,[P|_T]) :-
	var(P),
	!,
	flora_abort('Uninstantiated argument in insert{...}').

flora_storage_insertfacts(ThisModuleName,[FLSYSDBUPDATE(P,StorageName,Module)|T]) :-
	!,
        flora_storage_check_existence(Module),
        flora_check_export_registry(ThisModuleName,Module,P,FLEXPORT_UPDATABLE),
	flora_internal_db_insert(StorageName,P),
	flora_storage_insertfacts(ThisModuleName, T).

flora_storage_insertfacts(_ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Var,_ModuleName1,_Caller)|_T]) :-
        var(Var),
        !,
	flora_abort('Uninstantiated argument in insert{...}').

flora_storage_insertfacts(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,(First,Rest),ModuleName1,Caller)|T]) :-
	!,
        (First = FLLIBMODOBJ(_F,_A,_MN,_C,_O) ->
            %% For cases such as
            %% Y=a(), Z=main, X = ${pp(${Y@Z})}, insert{X}
            First
        ;
            %% For cases such as
            %% X = ','(a,b), insert{X}
            flora_storage_insertfacts(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,First,ModuleName1,Caller)])
        ),
        flora_storage_insertfacts(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Rest,ModuleName1,Caller)]),
	flora_storage_insertfacts(ThisModuleName,T).

flora_storage_insertfacts(ThisModuleName,[FLLIBMODLIT(F,Args,ModuleName1,Caller)|T]) :-
	!,
	P2H_CONVERSION(ModuleName,ModuleName1),
	flora_storage_check_module_name(ModuleName),
	(get_canonical_form(FLLIBMODLIT(F,Args,ModuleName,Caller), FLCANON(_,_,_,P)) -> true
	;
	    flora_decode_goal_as_atom(Args,AtomArgs),
	    flora_abort(['A non-fact found in insert{...}, ', AtomArgs])
	),
        ( F == FL_LIBMOD -> 
	    convert_to_head_literal(P,HeadP)
        ;
            HeadP = P
        ),
	flora_fdb_storage_name(ModuleName,StorageName),
	flora_internal_db_insert(StorageName,HeadP),
	flora_storage_insertfacts(ThisModuleName,T).

flora_storage_insertfacts(ThisModuleName,[FLLIBMODOBJ(F,Args,ModuleName1,Caller,O)|T]) :-
	!,
	P2H_CONVERSION(ModuleName,ModuleName1),
	flora_check_module_name(ModuleName),
	FLLIBMODOBJ(F,Args,ModuleName,Caller,O),
	flora_storage_insertfacts(ThisModuleName,T).

flora_storage_insertfacts(ThisModuleName,[','(C1,C2)|T]) :-
	!,
	%% Break up conjunction, since insertion of conjunction is not
	%% allowed in XSB.
	flora_storage_insertfacts(ThisModuleName,[C1,C2|T]).

flora_storage_insertfacts(ThisModuleName,[P|T]) :-
	%% This is a meta programming feature, since a variable can appear
	%% in the list of literals to be updated.
	functor(P,F,N),
	( flora_decode_module_name(P,ModuleName1) ->
	    P2H_CONVERSION(ModuleName,ModuleName1),
	    flora_storage_check_module_name(ModuleName),
	    flora_fdb_storage_name(ModuleName,StorageName),
	    convert_to_head_literal(P,HeadP),
	    flora_internal_db_insert(StorageName,HeadP),
	    flora_storage_insertfacts(ThisModuleName,T)

	; N == 2, F == ';' ->
	    flora_decode_goal_as_atom(P,PA),
	    flora_abort(['Insertion of disjunctive information is not allowed, ',
			 PA])

	; N == 1, flora_storage_is_negation_symbol(F) ->
	    flora_decode_goal_as_atom(P,PA),
	    flora_abort(['Insertion of negative information is not allowed, ',
			 PA])
	;
	    flora_decode_goal_as_atom(P,PA),
	    ( F == WRAP_HILOG ->
		flora_abort(['Attempt to insert a HiLog term (non-fact), ',
			     PA])
	    ;
		flora_abort(['Attempt to insert a Prolog term (non-fact), ',
			     PA])
	    )
	).


/*****************************************************************************
  fllibinsertall(+List)
*****************************************************************************/
FLLIBINSERTALL(ThisModuleName,List) :- FLLIBINSERT(ThisModuleName,List). % same as FLLIBINSERTALL(List,true).


/*****************************************************************************
  fllibinsertall(+List,+Condition)
*****************************************************************************/
FLLIBINSERTALL(ThisModuleName,List,Condition) :-
	findall(List,Condition,FsList),
	!,
	flora_insertall_facts(ThisModuleName,FsList).


/*****************************************************************************
  flora_insertall_facts(+FactsList)

  Note: This predicate always succeeds.
*****************************************************************************/
flora_insertall_facts(_ThisModuleName,[]) :- !.

flora_insertall_facts(ThisModuleName,[Fs|FL]) :-
	flora_storage_insertfacts(ThisModuleName,Fs),
	flora_insertall_facts(ThisModuleName,FL).


/*****************************************************************************
  fllibdelete(+List)
*****************************************************************************/
FLLIBDELETE(ThisModuleName,List) :- FLLIBDELETE(ThisModuleName,List,true).


/*****************************************************************************
  fllibdelete(+List,+Condition)

  The semantics is such that all the literals in "List" must be base facts
  in the storage to be deleted.
*****************************************************************************/
FLLIBDELETE(_ThisModuleName,[P],true) :-
	var(P),
	!,
	flora_abort('Uninstantiated argument in delete{...}').

FLLIBDELETE(ThisModuleName,[FLSYSDBUPDATE(P,StorageName,Module)],true) :-
	!,
        flora_storage_check_existence(Module),
        flora_check_export_registry(ThisModuleName,Module,P,FLEXPORT_UPDATABLE),
	flora_db_find_base(StorageName,P),
	flora_internal_db_delete(StorageName,P).

FLLIBDELETE(_ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Var,_ModuleName1,_Caller)|_T]) :-
        var(Var),
        !,
	flora_abort('Uninstantiated argument in delete{...}').

FLLIBDELETE(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,(First,Rest),ModuleName1,Caller)]) :-
	!,
        (First = FLLIBMODOBJ(_F,_A,_MN,_C,_O) ->
            %% For cases such as
            %% Y=a(), Z=main, X = ${pp(${Y@Z})}, delete{X}
            First
        ;
            %% For cases such as
            %% X = ','(a,b), delete{X}
            FLLIBDELETE(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,First,ModuleName1,Caller)])
        ),
        FLLIBDELETE(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Rest,ModuleName1,Caller)]).

FLLIBDELETE(_ThisModuleName,[FLLIBMODLIT(F,Args,ModuleName1,Caller)],true) :-
	!,
	P2H_CONVERSION(ModuleName,ModuleName1),
	flora_storage_check_module_name(ModuleName),
	get_canonical_form(FLLIBMODLIT(F,Args,ModuleName,Caller), FLCANON(_,_,_,P)),
        ( F == FL_LIBMOD -> 
	    convert_to_head_literal(P,HeadP)
        ;
            HeadP = P
        ),
	flora_fdb_storage_name(ModuleName,StorageName),
	!,
	flora_db_find_base(StorageName,HeadP),
	flora_internal_db_delete(StorageName,HeadP).

FLLIBDELETE(ThisModuleName,List,Condition) :-
	flora_storage_convertlist(List,CallList,FactList),
	flora_list2conjunct([Condition|CallList],Goal),
	!,
	call(Goal),
	flora_storage_deletelist(ThisModuleName,FactList).


/*****************************************************************************
  flora_storage_deletelist(+List)
*****************************************************************************/
flora_storage_deletelist(_ThisModuleName,[]) :- !.

flora_storage_deletelist(ThisModuleName,[FLSYSDBUPDATE(P,StorageName)|L]) :-
	!,
	flora_decode_module_name(P,Module),
        flora_check_export_registry(ThisModuleName,Module,P,FLEXPORT_UPDATABLE),
	convert_to_head_literal(P,HeadP),
	flora_internal_db_delete(StorageName,HeadP),
	flora_storage_deletelist(ThisModuleName,L).

flora_storage_deletelist(ThisModuleName,[H|L]) :-
	!,
	flora_storage_deletelist(ThisModuleName,H),
	flora_storage_deletelist(ThisModuleName,L).


/*****************************************************************************
  flora_storage_deleteall(+Predicate,+StorageName)

  Note: This predicate always succeeds.
*****************************************************************************/
flora_storage_deleteall(ThisModuleName,P,StorageName) :-
	findall(FLSYSDBUPDATE(P,StorageName),flora_db_find_base(StorageName,P),L),
	!,
	flora_storage_deletelist(ThisModuleName,L).


/*****************************************************************************
  fllibdeleteall(+List)
*****************************************************************************/
FLLIBDELETEALL(ThisModuleName,List) :- FLLIBDELETEALL(ThisModuleName,List,true).


/*****************************************************************************
  fllibdeleteall(+List,+Condition)

  The semantics is such that all the literals in "List" must be base facts
  in the storage to be deleted. This call always succeeds.
*****************************************************************************/
FLLIBDELETEALL(_ThisModuleName,[P],true) :-
	var(P),
	!,
	flora_abort('Uninstantiated goal in deleteall{...}').

FLLIBDELETEALL(ThisModuleName,[FLSYSDBUPDATE(P,StorageName,Module)],true) :-
	!,
        flora_storage_check_existence(Module),
        flora_check_export_registry(ThisModuleName,Module,P,FLEXPORT_UPDATABLE),
	flora_storage_deleteall(ThisModuleName,P,StorageName).

FLLIBDELETEALL(_ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Var,_ModuleName1,_Caller)|_T]) :-
        var(Var),
        !,
	flora_abort('Uninstantiated goal in deleteall{...}').

FLLIBDELETEALL(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,(First,Rest),ModuleName1,Caller)]) :-
	!,
        (First = FLLIBMODOBJ(_F,_A,_MN,_C,_O) ->
            %% For cases such as
            %% Y=a(), Z=main, X = ${pp(${Y@Z})}, deleteall{X}
            First
        ;
            %% For cases such as
            %% X = ','(a,b), deleteall{X}
            FLLIBDELETEALL(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,First,ModuleName1,Caller)])
        ),
        FLLIBDELETEALL(ThisModuleName,[FLLIBMODLIT(FL_LIBMOD,Rest,ModuleName1,Caller)]).

FLLIBDELETEALL(ThisModuleName,[FLLIBMODLIT(F,Args,ModuleName1,Caller)],true) :-
	!,
	P2H_CONVERSION(ModuleName,ModuleName1),
	flora_storage_check_module_name(ModuleName),
	get_canonical_form(FLLIBMODLIT(F,Args,ModuleName,Caller), FLCANON(_,_,_,P)),
        ( F == FL_LIBMOD -> 
	    convert_to_head_literal(P,HeadP)
        ;
            HeadP = P
        ),
	flora_fdb_storage_name(ModuleName,StorageName),
	!,
	flora_storage_deleteall(ThisModuleName,HeadP,StorageName).

FLLIBDELETEALL(ThisModuleName,List,Condition) :-
	flora_storage_convertlist(List,CallList,FactList),
	flora_list2conjunct([Condition|CallList],Goal),
	findall(FactList,Goal,FsList),
	!,
	flora_deleteall_facts(ThisModuleName,FsList).


/*****************************************************************************
  flora_deleteall_facts(+FactList,-DeletedFactList)

  Note: This predicate always succeeds.
*****************************************************************************/
flora_deleteall_facts(_ThisModuleName,[]) :- !.

flora_deleteall_facts(ThisModuleName,[Fs|FL]) :-
	flora_storage_deletelist(ThisModuleName,Fs),
	flora_deleteall_facts(ThisModuleName,FL).


/*****************************************************************************
  flliberase(+List)
*****************************************************************************/
FLLIBERASE(ThisModuleName,List) :- FLLIBERASE(ThisModuleName,List,true).


/*****************************************************************************
  flliberase(+List,+Condition)
*****************************************************************************/
FLLIBERASE(ThisModuleName,List,Condition) :-
	flora_storage_convertlist(List,CallList,FactList),
	flora_list2conjunct([Condition|CallList],Goal),
	call(Goal),
	flora_storage_deletelist(ThisModuleName,FactList),
	flora_erase_facts(ThisModuleName,FactList).


/*****************************************************************************
  flora_erase_facts(+List)
*****************************************************************************/
flora_erase_facts(_ThisModuleName,[]) :- !.

flora_erase_facts(ThisModuleName,[FLSYSDBUPDATE(P,StorageName)|Fs]) :-
	!,
	flora_decode_module_name(P,Module),
        flora_check_export_registry(ThisModuleName,Module,P,FLEXPORT_UPDATABLE),
	flora_erase_one_fact(ThisModuleName,P,StorageName),
	flora_erase_facts(ThisModuleName,Fs).

flora_erase_facts(ThisModuleName,[H|Fs]) :-
	!,
	flora_erase_facts(ThisModuleName,H),
	flora_erase_facts(ThisModuleName,Fs).


/*****************************************************************************
  flora_erase_one_fact(+Fact,+StorageName)

  Note: For an F-logic fact this predicate continues to trace links and
        delete other F-logic facts. It always succeeds. Works for 
        Flora user modules and system modules.
*****************************************************************************/
flora_erase_one_fact(ThisModuleName,Fact,StorageName) :-
	Fact =.. [Funct|Args],
	( flora_is_flogic_wrapper(Funct,ModuleName,Base) ->
	    ( Base == WRAP_ISA     -> Args=[O,_C,_Caller]
	    ; Base == WRAP_SUB     -> Args=[O,_C,_Caller]
	    ; Base == WRAP_FD      -> Args=[_S,_A,O,_Caller]
	    ; Base == WRAP_MVD     -> Args=[_S,_A,O,_Caller]
	    ; Base == WRAP_IFD     -> Args=[_S,_IA,O,_Caller]
	    ; Base == WRAP_IMVD    -> Args=[_S,_IA,O,_Caller]
	    ; Base == WRAP_METH    -> Args=[O,_M,_Caller]
	    ; Base == WRAP_IMETH   -> Args=[O,_IM,_Caller]
	    ; Base == WRAP_EXISTS  -> Args=[O,_Caller]
	    ; Base == WRAP_MVDDEF  -> Args=[O,_A,_Caller]
	    ; Base == WRAP_IMVDDEF -> Args=[O,_IA,_Caller]
	    ; Base == WRAP_TRAN    -> Args=[O,_T,_Caller]
	    ; Base == WRAP_FDSIG   -> Args=[_S,_AS,O,_Caller]
	    ; Base == WRAP_IFDSIG  -> Args=[_S,_IAS,O,_Caller]
	    ; Base == WRAP_MVDSIG  -> Args=[_S,_AS,O,_Caller]
	    ; Base == WRAP_IMVDSIG -> Args=[_S,_IAS,O,_Caller]
	    )
	;
	  true
        ),
	!,
	( var(O) ->
	    true
	;
	  flora_setup_flogic_fact_wrapper(ModuleName),
	  flora_erase_objects(ThisModuleName,[O],StorageName)
	).


/*****************************************************************************
  flora_erase_objects(+ObjectList,+StorageName)
*****************************************************************************/
flora_erase_objects(_ThisModuleName,[],_StorageName) :- !.

flora_erase_objects(ThisModuleName,[O|OList],StorageName) :-
	!,
	flora_flogic_fact_wrapper(WRAP_ISA,WSBisa),
	flora_flogic_fact_wrapper(WRAP_SUB,WSBsub),
	flora_flogic_fact_wrapper(WRAP_METH,WSBmeth),
	flora_flogic_fact_wrapper(WRAP_IMETH,WSBimeth),
	flora_flogic_fact_wrapper(WRAP_TRAN,WSBtran),
	flora_flogic_fact_wrapper(WRAP_FD,WSBfd),
	flora_flogic_fact_wrapper(WRAP_IFD,WSBifd),
	flora_flogic_fact_wrapper(WRAP_FDSIG,WSBfdsig),
	flora_flogic_fact_wrapper(WRAP_IFDSIG,WSBifdsig),
	flora_flogic_fact_wrapper(WRAP_MVD,WSBmvd),
	flora_flogic_fact_wrapper(WRAP_IMVD,WSBimvd),
	flora_flogic_fact_wrapper(WRAP_MVDSIG,WSBmvdsig),
	flora_flogic_fact_wrapper(WRAP_IMVDSIG,WSBimvdsig),
	flora_flogic_fact_wrapper(WRAP_EXISTS,WSBexists),
	flora_flogic_fact_wrapper(WRAP_MVDDEF,WSBmvddef),
	flora_flogic_fact_wrapper(WRAP_IMVDDEF,WSBimvddef),
	Wfd =.. [WSBfd,O,_,X,_],
	Wifd =.. [WSBifd,O,_,X,_],
	Wfdsig =.. [WSBfdsig,O,_,X,_],
	Wifdsig =.. [WSBifdsig,O,_,X,_],
	Wmvd =.. [WSBmvd,O,_,X,_],
	Wimvd =.. [WSBimvd,O,_,X,_],
	Wmvdsig =.. [WSBmvdsig,O,_,X,_],
	Wimvdsig =.. [WSBimvdsig,O,_,X,_],
	findall(X,( flora_db_find_base(StorageName,Wfd)
                  ; flora_db_find_base(StorageName,Wifd)
	          ; flora_db_find_base(StorageName,Wfdsig)
                  ; flora_db_find_base(StorageName,Wifdsig)
                  ; flora_db_find_base(StorageName,Wmvd)
                  ; flora_db_find_base(StorageName,Wimvd)
                  ; flora_db_find_base(StorageName,Wmvdsig)
                  ; flora_db_find_base(StorageName,Wimvdsig)
	          ),
                AddedOList
               ),
	append(OList,AddedOList,NewOList),
	sort(NewOList,NextOList),
	Pisa =.. [WSBisa,O,_,_],
	Psub =.. [WSBsub,O,_,_],
	Pmeth =.. [WSBmeth,O,_,_],
	Pimeth =.. [WSBimeth,O,_,_],
	Ptran =.. [WSBtran,O,_,_],
	Pfd =.. [WSBfd,O,_,_,_],
	Pifd =.. [WSBifd,O,_,_,_],
	Pfdsig =.. [WSBfdsig,O,_,_,_],
	Pifdsig =.. [WSBifdsig,O,_,_,_],
	Pmvd =.. [WSBmvd,O,_,_,_],
	Pimvd =.. [WSBimvd,O,_,_,_],
	Pmvdsig =.. [WSBmvdsig,O,_,_,_],
	Pimvdsig =.. [WSBimvdsig,O,_,_,_],
	Pexists =.. [WSBexists,O,_],
	Pmvddef =.. [WSBmvddef,O,_,_],
	Pimvddef =.. [WSBimvddef,O,_,_],
	!,
	flora_storage_deleteall(ThisModuleName,Pisa,StorageName),
	flora_storage_deleteall(ThisModuleName,Psub,StorageName),
	flora_storage_deleteall(ThisModuleName,Pmeth,StorageName),
	flora_storage_deleteall(ThisModuleName,Pimeth,StorageName),
	flora_storage_deleteall(ThisModuleName,Ptran,StorageName),
	flora_storage_deleteall(ThisModuleName,Pfd,StorageName),
	flora_storage_deleteall(ThisModuleName,Pifd,StorageName),
	flora_storage_deleteall(ThisModuleName,Pfdsig,StorageName),
	flora_storage_deleteall(ThisModuleName,Pifdsig,StorageName),
	flora_storage_deleteall(ThisModuleName,Pmvd,StorageName),
	flora_storage_deleteall(ThisModuleName,Pimvd,StorageName),
	flora_storage_deleteall(ThisModuleName,Pmvdsig,StorageName),
	flora_storage_deleteall(ThisModuleName,Pimvdsig,StorageName),
	flora_storage_deleteall(ThisModuleName,Pexists,StorageName),
	flora_storage_deleteall(ThisModuleName,Pmvddef,StorageName),
	flora_storage_deleteall(ThisModuleName,Pimvddef,StorageName),
	flora_erase_objects(ThisModuleName,NextOList,StorageName).


/*****************************************************************************
  flliberaseall(+List)
*****************************************************************************/
FLLIBERASEALL(ThisModuleName,List) :- FLLIBERASEALL(ThisModuleName,List,true).


/*****************************************************************************
  flliberaseall(+List,+Condition)
*****************************************************************************/
FLLIBERASEALL(ThisModuleName,List,Condition) :-
	flora_storage_convertlist(List,CallList,FactList),
	flora_list2conjunct([Condition|CallList],Goal),
	findall(FactList,Goal,FsList),
	!,
	flora_deleteall_facts(ThisModuleName,FsList),
	flora_eraseall_facts(ThisModuleName,FsList).


/*****************************************************************************
  flora_eraseall_facts(+FactList)

  Note: This predicate always succeeds.
*****************************************************************************/
flora_eraseall_facts(_ThisModuleName,[]) :- !.

flora_eraseall_facts(ThisModuleName,[Fs|FL]) :-
	!,
	flora_erase_facts(ThisModuleName,Fs),
	flora_eraseall_facts(ThisModuleName,FL).

flora_internal_db_insert(StorageName,Call) :-
	flora_db_insert_base(StorageName,Call),
	flora_refresh_tables(Call).

flora_internal_db_delete(StorageName,Call) :-
	flora_db_delete_base(StorageName,Call),
	flora_refresh_tables(Call).
