/* File:      flrshell.P  -- The Flora interactive shell
**
** Author(s): Bertram Ludaescher
**            Michael Kifer
**            Guizhen Yang
**
** Contact:   flora-users@lists.sourceforge.net
**
** Copyright (C) The Research Foundation of SUNY, 1999-2002
**
** FLORA-2 is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** FLORA-2 is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with FLORA-2; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
** $Id: flrshell.P,v 1.45 2002-04-07 23:09:15 kifer Exp $
**
*/


:- compiler_options([xpp_on]).
#include "extensions_xsb.h"

#include "flora_terms.flh"

#define PROLOG_PFILE_EXT	XSB_SRC_EXTENSION_ATOM_SANS_DOT
#define PROLOG_OFILE_EXT	XSB_OBJ_EXTENSION_ATOM_SANS_DOT
#define PROLOG_HFILE_EXT	XSB_HDR_EXTENSION_ATOM_SANS_DOT

/*********************************************************************/
flora_shell_loop :-
	flora_init_shell,
	push_abort_cutpoint,
	repeat,
	pop_abort_cutpoint,
	push_abort_cutpoint,
	flora_shell_prompt,
	flora_shell_command_line(Code,Options,Status),
	( flora_good_command(Status) ->
	    findall(SysLibOp,member(FLSYSLIB(SysLibOp),Options),SysLibList),
	    flora_load_library(SysLibList),
	    findall(SysModOp,member(FLSYSMOD(SysModOp),Options),SysModList),
	    flora_load_system_module(SysModList),
	    call(Code)
	;
	  true
        ),
	flora_loop_guard(Status).


/*********************************************************************/
flora_init_shell :-
	consult(flrundefhook),
	add_xsb_hook(xsb_undefined_predicate_hook(flora_undefined_predicate_hook(_))),
	consult(flrutils),
	flora_load_library(FLLIBSHELLANS),
	flora_load_library(FLLIBLOAD),
	flora_load_library(FLLIBDISPLAY),
	flloadtrailer(NONE),
	flora_welcome_msg,
	flAll,
	flChatter,
	flora_set_counter(flora_shell_loaded,1),
	!.


/*********************************************************************/
flora_welcome_msg :-
	package_configuration(version(flora),V),
	flora_stdmsg_string('~nFLORA-2 Version ~w~n~n',[V]),
	flora_stdmsg_line('Type `flHelp.'' to show the help message.'),
	flora_stdmsg_line('Type `flDemo(demoName).'' to run demos.'),
	flora_stdmsg_nl,
	!.


/*********************************************************************/
flora_shell_prompt :- flora_stdfdbk_string('flora2 ?- ').


/*********************************************************************/
flora_loop_guard(Status) :-
	( flora_end_of_input(Status) ->
	    ( pop_stdin(cmd_line_argument) ->
		%% end of input from -e option string to XSB
		flora_stdfdbk_string('~n'),
		abort
	    ;
	      flEnd
	    )
	;
	    abort
        ).


/*********************************************************************/
#mode save
#mode nostring "\!#'"
flHelp :-
	flora_stdmsg_line('~nFLORA shell commands:~n'),
	flora_stdmsg_line('o flHelp'),
	flora_stdmsg_line('    show this info'),
	flora_stdmsg_line('o FL_COMPILE FILE'),
	flora_stdmsg_line('    compile FILE.flr for the shell module `main'''),
	flora_stdmsg_line('o FL_COMPILE FILE>>Module'),
	flora_stdmsg_line('    compile FILE.flr for Module'),
	flora_stdmsg_line('o FL_LOAD FILE>>Module  OR  [FILE >> Module,...]'),
	flora_stdmsg_line('    load FILE.flr into Module'),
	flora_stdmsg_string('    specifying FILE.~w or FILE.~w loads these files;',
			    [PROLOG_PFILE_EXT,PROLOG_OFILE_EXT]),
	flora_stdmsg_nl,
	flora_stdmsg_line('    the [...] version can load a list of files'),
	flora_stdmsg_line('o FL_LOAD FILE  OR  [FILE,...]'),
	flora_stdmsg_line('    load FILE.flr into the shell module `main'''),
	flora_stdmsg_line('o flDemo(FILE)'),
	flora_stdmsg_line('    run a demo from FLORA demos directory'),
	flora_stdmsg_line('o FL_EQUALITY {none|basic|flogic}'),
	flora_stdmsg_line('    set support for the equality predicate in the shell module `main'' to'),
	flora_stdmsg_line('    none, standard first-order, or F-logic style'),
	flora_stdmsg_line('o FL_EXPUNGE Functor/Arity [, Functor/Arity]* [in Module]'),
	flora_stdmsg_line('    expunge predicate Functor/Arity in Module (`main'' is the default)'),
	flora_stdmsg_line('o abolish_all_tables'),
	flora_stdmsg_line('    flush all tabled data'),
	flora_stdmsg_line('o FL_FIRSTORDER Functor/Arity'),
	flora_stdmsg_line('    define Functor/Arity as first-order predicate in shell mode'),
	flora_stdmsg_line('o FL_ARGUMENTS Functor({FL_OID|FL_BODYFORMULA},...)'),
	flora_stdmsg_line('    define the predicate argument type in shell mode'),
	flora_stdmsg_line('o FL_OP(Precedence,Associativity,Operator)'),
	flora_stdmsg_line('    define an operator in shell mode'),
	flora_stdmsg_line('o flReset(FL_FIRSTORDER|FL_ARGUMENTS|FL_OP)'),
	flora_stdmsg_line('    clear all dynamic FL_FIRSTORDER/FL_ARGUMENTS/FL_OP declarations'),
	flora_stdmsg_line('    in the FLORA shell'),
	flora_stdmsg_line('o flAll'),
	flora_stdmsg_line('    show all solutions (default)'),
	flora_stdmsg_line('o flOne'),
	flora_stdmsg_line('    show solutions one by one'),
	flora_stdmsg_line('o flMaxerr(all|N)'),
	flora_stdmsg_line('    set/show the maximum number of errors FLORA reports'),
	flora_stdmsg_line('o flTrace/flNoTrace'),
	flora_stdmsg_line('    turn on/off FLORA trace'),
	flora_stdmsg_line('o flEnd'),
	flora_stdmsg_line('    say Ciao to FLORA, stay in XSB'),
	flora_stdmsg_line('o flHalt'),
	flora_stdmsg_line('    quit both FLORA and XSB').
#mode restore


/*********************************************************************/
flReset(X) :-
	var(X),
	!,
	flora_error_line("invalid argument to flReset"),
	fail.

%%flReset(FL_PROLOG) :- !, flora_shreset_prolog.
flReset(FL_FIRSTORDER) :- !, flora_shreset_firstorder.
flReset(FL_ARGUMENTS) :- !, flora_shreset_arguments.
flReset(FL_OP) :- !, flora_shreset_operator.

flReset(_) :-
	flora_error_line("invalid argument to flReset"),
	!,
	fail.


/*********************************************************************/
flAll :-
	( flora_switch(all) -> true
	;
	  assert(flora_switch(all))
	).

flOne :-
	retractall(flora_switch(all)).


/*********************************************************************/
flEnd :-
	remove_xsb_hook(xsb_undefined_predicate_hook(flora_undefined_predicate_hook(_))),
	unstrap_flora,
	flora_stdmsg_line('~n~nCiao!'),
	pop_abort_cutpoint,
	abort.


/*********************************************************************/
%% This is needed so we could designate this predicate as unprintable 
%% by the debugger and avoid traicing inside 
%% assert(flora_switch(low_level_trace))
flora_set_switch(Switch) :-
	assert(flora_switch(Switch)).
/*********************************************************************/
%% Enable Flora high-level debugging
flTrace :- flora_trace.
flNoTrace :- 
	make_trace_interactive,
	flora_notrace.

%% Dumps profile into File
flTrace(File) :-
	dump_trace_to_file(File),
	flTrace.

%% Enables low-level XSB debugging
flTraceLow :-
	flTrace,
	flora_set_switch(low_level_trace).
flTraceLow(File) :-
	flTrace(File),
	flora_set_switch(low_level_trace).
flNoTraceLow :-
	flNoTrace,
	retractall(flora_switch(low_level_trace)).


dump_trace_to_file(File) :-
	debug_ctl(prompt,off),
	debug_ctl(profile,on),
	debug_ctl(redirect,File).
make_trace_interactive :-
	debug_ctl(prompt,on),
	debug_ctl(profile,off),
	debug_ctl(redirect,tty).



/*********************************************************************/
flChatter :- flora_set_switch(chatter).
flNoChatter :- retractall(flora_switch(chatter)).


/*********************************************************************/
flDemo(File) :-
	flora_check_filename(File),
	package_configuration(dir(flora),FloraDir),
	flora_slash(Slash),
	%% Add the demo directory to library_directory
	flora_concat_atoms([FloraDir,Slash,'demos'],DemoDir),
	( library_directory(DemoDir) ->
	    true
	;
	  assert(library_directory(DemoDir))
	),
	cwd(CurrWorkingDir),
	cd(DemoDir),
	flora_load_module_internal(File),
	cd(CurrWorkingDir),
	retractall(library_directory(DemoDir)).
