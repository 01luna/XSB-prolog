/* File:      flrshell.P  -- The Flora interactive shell
**
** Author(s): Bertram Ludaescher
**            Michael Kifer
**            Guizhen Yang
**
** Contact:   xsb-contact@cs.sunysb.edu
**
** Copyright (C) The Research Foundation of SUNY, 1999-2001
**
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
** $Id: flrshell.P,v 1.25 2001-06-04 06:52:41 kifer Exp $
**
*/


:- compiler_options([xpp_on]).

#include "flora_terms.flh"

:- [flrcontrol].


/*********************************************************************/
flora_shell_loop :-
	flora_init_shell,
	push_abort_cutpoint,
	repeat,
	pop_abort_cutpoint,
	push_abort_cutpoint,
	%seen,
	%see(userin),
	%told,
	%tell(userout),
	flora_shell_prompt,
	flora_shell_command_line(Code,Options,Status),
	( flora_good_command(Status) ->
	    findall(Op,member(FLSYSLIB(Op),Options),OpList),
	    flora_load_library(OpList),
	    %writeln(Code),
	    call(Code)
	;
	  true
        ),
	flora_loop_guard(Status).


/*********************************************************************/
flora_init_shell :-
	consult(flrundefhook),
	add_xsb_hook(xsb_undefined_predicate_hook(flora_undefined_predicate_hook(_))),
	consult(flrutils),
	flora_load_library(FLLIBSHELLANS),
	flora_load_library(FLLIBLOAD),
	flora_load_library(FLLIBDISPLAY),
	flloadtrailer(NONE),
	flora_welcome_msg,
	flAll,
	flChatter,
	conset(flora_shell_loaded,1),
	!.


/*********************************************************************/
flora_welcome_msg :-
	package_configuration(version(flora),V),
	flora_stdmsg_string("\nFLORA-2 Version %S\n\n",args(V)),
	flora_stdmsg_line('Type `flHelp.'' to show the help message.'),
	flora_stdmsg_line('Type `flDemo(demoName).'' to run demos.'),
	flora_stdmsg_nl,
	!.


/*********************************************************************/
flora_shell_prompt :- flora_stdfdbk_string("flora2 ?- ").


/*********************************************************************/
flora_loop_guard(Status) :-
	( flora_end_of_input(Status) ->
	    ( pop_stdin(cmd_line_argument) ->
		%% end of input from -e option string to XSB
		flora_stdfdbk_string("\n"),
		abort
	    ;
	      flEnd
	    )
	;
	  abort
        ).


/*********************************************************************/
#mode save
#mode nostring "\!#'"
flHelp :-
	flora_stdmsg_line("\nFLORA shell commands:\n"),
	flora_stdmsg_line('o flHelp'),
	flora_stdmsg_line('    show this info'),
	flora_stdmsg_line('o FL_COMPILE FILE'),
	flora_stdmsg_line('    compile FILE.flr for the shell module `main'''),
	flora_stdmsg_line('o FL_COMPILE FILE>>Module'),
	flora_stdmsg_line('    compile FILE.flr for Module'),
	flora_stdmsg_line('o FL_LOAD FILE>>Module  OR  [FILE >> Module,...]'),
	flora_stdmsg_line('    load FILE.flr into Module'),
	flora_stdmsg_line('    specifying ''FILE.P'' or ''FILE.O'' loads these files'),
	flora_stdmsg_line('    the [...] version can load a list of files files'),
	flora_stdmsg_line('o FL_LOAD FILE  OR  [FILE,...]'),
	flora_stdmsg_line('    load FILE.flr into the shell module `main'''),
	flora_stdmsg_line('o flDemo(FILE)'),
	flora_stdmsg_line('    run a demo from FLORA demos directory'),
	flora_stdmsg_line('o FL_EQUALITY {none|basic|flogic}'),
	flora_stdmsg_line('    set support for the equality predicate in the shell module `main'' to'),
	flora_stdmsg_line('    none, standard first-order, or F-logic style'),
	flora_stdmsg_line('o FL_ABOLISH Functor/Arity [, Functor/Arity]* [in Module]'),
	flora_stdmsg_line('    abolish predicate Functor/Arity in Module (`main'' is the default)'),
	flora_stdmsg_line('o abolish_all_tables'),
	flora_stdmsg_line('    flush all tabled data'),
	flora_stdmsg_line('o FL_PROLOG Functor/Arity'),
	flora_stdmsg_line('    define Functor/Arity as Prolog predicate in shell mode'),
	flora_stdmsg_line('o FL_PREDICATE Functor/Arity'),
	flora_stdmsg_line('    define Functor/Arity as non-HiLog FLORA predicate in shell mode'),
	flora_stdmsg_line('o FL_SIGNATURE Functor({FL_OID|FL_BODYFORMULA},...)'),
	flora_stdmsg_line('    define the predicate meta signature in shell mode'),
	flora_stdmsg_line('o flOp(Precedence,Associativity,Operator)'),
	flora_stdmsg_line('    define an operator in shell mode'),
	flora_stdmsg_line('o flReset(flProlog|flPredicate|flSign|op)'),
	flora_stdmsg_line('    clear all dynamic flProlog/flPredicate/flSign/op declarations'),
	flora_stdmsg_line('    in the FLORA shell'),
	flora_stdmsg_line('o flAll'),
	flora_stdmsg_line('    show all solutions (default)'),
	flora_stdmsg_line('o flOne'),
	flora_stdmsg_line('    show solutions one by one'),
	flora_stdmsg_line('o flMaxerr(all|N)'),
	flora_stdmsg_line('    set/show the maximum number of errors FLORA reports'),
	flora_stdmsg_line('o flTrace/flNoTrace'),
	flora_stdmsg_line('    turn on/off FLORA trace'),
	flora_stdmsg_line('o flEnd'),
	flora_stdmsg_line('    say Ciao to FLORA, stay in XSB'),
	flora_stdmsg_line('o flHalt'),
	flora_stdmsg_line('    quit both FLORA and XSB').
#mode restore


/*********************************************************************/
flReset(X) :-
	var(X),
	!,
	flora_error_line("invalid argument to flReset"),
	fail.

flReset(FL_PROLOG) :- !, flora_shreset_prolog.
flReset(FL_PREDICATE) :- !, flora_shreset_nohilog.
flReset(FL_SIGNATURE) :- !, flora_shreset_signature.
flReset(FL_OP) :- !, flora_shreset_operator.

flReset(_) :-
	flora_error_line("invalid argument to flReset"),
	!,
	fail.


/*********************************************************************/
flAll :-
	( flora_switch(all) ->
	    true
	;
	  assert(flora_switch(all))
	).

flOne :-
	retractall(flora_switch(all)).


/*********************************************************************/
flEnd :-
	remove_xsb_hook(xsb_undefined_predicate_hook(flora_undefined_predicate_hook(_))),
	unstrap_flora,
	flora_stdmsg_line("\n\nCiao!"),
	pop_abort_cutpoint,
	abort.


/*********************************************************************/
flTrace :-
	( flora_debugger_data_loaded ->
	    true
	;
	  consult(flrdebugger)
	),
	flora_trace.

flNoTrace :-
	( flora_debugger_data_loaded ->
	    true
	;
	  consult(flrdebugger)
	),
	flora_notrace.


/*********************************************************************/
flChatter :- assert(flora_switch(chatter)).
flNoChatter :- retractall(flora_switch(chatter)).


/*********************************************************************/
flDemo(File) :-
	flora_check_filename(File),
	package_configuration(dir(flora),FloraDir),
	slash(Slash),
	%% Add the demo directory to library_directory
	fmt_write_string(DemoDir,'%s%sdemos',args(FloraDir,Slash)),
	( library_directory(DemoDir) ->
	    true
	;
	  assert(library_directory(DemoDir))
	),
	cwd(CurrWorkingDir),
	cd(DemoDir),
	flora_load_module_internal(File),
	cd(CurrWorkingDir),
	retractall(library_directory(DemoDir)).
