/***************************************************************************
  file: closure/flrtrailer.fli

  Author(s): Guizhen Yang

  Contact:   flora-users@lists.sourceforge.net

  Copyright (C) The Research Foundation of SUNY, 1999-2001

  This is a template file to generate the FLORA trailer that has no
  equality maintenance.

  The actual trailer, flrltrailer.flh,
   is generated by the predicate flCompileInclude/1 in
  flrutils.P. It is invoked by the makefile when Flora is built.
***************************************************************************/


#include "flora_wspredicates.flh"


:- import flora_warning_line/1 from flrprint.
:- import flora_funct_arity/2 from flrwrapper.


/***************************************************************************
  Note that all programs rules are translated with with a 'derived_' prefix.
  These rules are bridged to the trailer using wrapper predicates with a
  'inferred_' prefix.
***************************************************************************/


/***************************************************************************
  closure rules for X::Y

  The following rules for computing immediate superclasses are removed
  from the new trailer:

  super_sub(X,Y) :-
	inferred_sub(X,Z),
	inferred_sub(Z,Y).

  super_sub(X,Y) :-
	super_sub(X,Z),
	inferred_sub(Z,Y).


  immediate_sub(X,Y) :-
	inferred_sub(X,Y),
	sk_not(super_sub(X,Y)).
***************************************************************************/
sub(X,Y) :-
	inferred_sub(X,Y),
	(X=Y, flora_warning_line('cycle in class hierarchy'); true).

sub(X,Y) :-
	silent_sub(X,Z),
	inferred_sub(Z,Y),
	(X=Y, flora_warning_line('cycle in class hierarchy'); true).


/***************************************************************************
  closure rules for O:C

  The following rules for computing immediate superclasses are removed
  from the new trailer:

  super_isa(O,C) :-
	inferred_isa(O,S),
	sub(S,C).


  immediate_isa(O,C) :-
	inferred_isa(O,C),
	sk_not(super_isa(O,C)).
***************************************************************************/
isa(O,C) :-
	inferred_isa(O,C).

isa(O,C) :-
	silent_sub(S,C),
	inferred_isa(O,S).


/*****************************************************************************
  inheritable methods that are locally defined

  The argument "MethSign" is in the form of Funct/Arity/Type.
*****************************************************************************/
%% local_ifd/2
local_ifd(S,MethSign) :-
	var(MethSign),
	!,
	inferred_ifd(S,M,_V),
	%% Should have a cut here when XSB will allow
	flora_funct_arity(M,MethSign).

local_ifd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_ifd(S,M,_V).
	%% Should have a cut here when XSB will allow


%% local_fd/2
local_fd(S,MethSign) :-
	var(MethSign),
	!,
	inferred_fd(S,M,_V),
	%% Should have a cut here when XSB will allow
	flora_funct_arity(M,MethSign).

local_fd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_fd(S,M,_V).
	%% Should have a cut here when XSB will allow


%% local_imvd/2
local_imvd(S,MethSign) :-
	var(MethSign),
	inferred_imvd(S,M,_V),
	%% Should have a cut here when XSB will allow
	flora_funct_arity(M,MethSign).

local_imvd(S,MethSign) :-
	var(MethSign),
	!,
	inferred_imvddef(S,M),
	flora_funct_arity(M,MethSign).

local_imvd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_imvd(S,M,_V).
	%% Should have a cut here when XSB will allow

local_imvd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_imvddef(S,M).


%% local_mvd/2
local_mvd(S,MethSign) :-
	var(MethSign),
	inferred_mvd(S,M,_V),
	%% Should have a cut here when XSB will allow
	flora_funct_arity(M,MethSign).

local_mvd(S,MethSign) :-
	var(MethSign),
	!,
	inferred_mvddef(S,M),
	flora_funct_arity(M,MethSign).

local_mvd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_mvd(S,M,_V).
	%% Should have a cut here when XSB will allow

local_mvd(S,MethSign) :-
	flora_funct_arity(M,MethSign),
	inferred_mvddef(S,M).


/*****************************************************************************
  nonmonotonic multiple inheritance of scalar attributes
  from superclasses to subclasses
*****************************************************************************/
ifd(C,M,V) :-
	inferred_ifd(C,M,V).

ifd(C,M,V) :-
	var(M),
	!,
	candidate_class_ifd(Super,MethSign,C),
	flora_funct_arity(M,MethSign),
	inheritance_ifd(Super,M,V),
	sk_not(local_ifd(C,MethSign)),
	sk_not(multiple_class_ifd(Super,MethSign,C)).

ifd(C,M,V) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_ifd(C,MethSign)),
	candidate_class_ifd(Super,MethSign,C),
	inheritance_ifd(Super,M,V),
	sk_not(multiple_class_ifd(Super,MethSign,C)).


/*****************************************************************************
  nonmonotonic multiple inheritance of scalar attributes
  from superclasses to instances/members
*****************************************************************************/
fd(O,M,V) :-
	inferred_fd(O,M,V).

fd(O,M,V) :-
	var(M),
	!,
	candidate_object_ifd(Super,MethSign,O),
	flora_funct_arity(M,MethSign),
	inheritance_ifd(Super,M,V),
	sk_not(local_fd(O,MethSign)),
	sk_not(multiple_object_ifd(Super,MethSign,O)).

fd(O,M,V) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_fd(O,MethSign)),
	candidate_object_ifd(Super,MethSign,O),
	inheritance_ifd(Super,M,V),
	sk_not(multiple_object_ifd(Super,MethSign,O)).


/*****************************************************************************
  nonmonotonic multiple inheritance of multivalued attributes
  from superclasses to subclasses
*****************************************************************************/
imvd(C,M,V) :-
	inferred_imvd(C,M,V).

imvd(C,M,V) :-
	var(M),
	!,
	candidate_class_imvd(Super,MethSign,C),
	flora_funct_arity(M,MethSign),
	inheritance_imvd(Super,M,V),
	sk_not(local_imvd(C,MethSign)),
	sk_not(multiple_class_imvd(Super,MethSign,C)).

imvd(C,M,V) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_imvd(C,MethSign)),
	candidate_class_imvd(Super,MethSign,C),
	inheritance_imvd(Super,M,V),
	sk_not(multiple_class_imvd(Super,MethSign,C)).


imvddef(C,M) :-
	inferred_imvddef(C,M).

imvddef(C,M) :-
	var(M),
	!,
	candidate_class_imvd(Super,MethSign,C),
	flora_funct_arity(M,MethSign),
	inheritance_imvddef(Super,M),
	sk_not(local_imvd(C,MethSign)),
	sk_not(multiple_class_imvd(Super,MethSign,C)).

imvddef(C,M) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_imvd(C,MethSign)),
	candidate_class_imvd(Super,MethSign,C),
	inheritance_imvddef(Super,M),
	sk_not(multiple_class_imvd(Super,MethSign,C)).

imvddef(C,M) :-
	imvd(C,M,_V).
	%% Should have a cut here when XSB will allow


/*****************************************************************************
  nonmonotonic multiple inheritance of multivalued attributes
  from superclasses to instances/members
*****************************************************************************/
mvd(O,M,V) :-
	inferred_mvd(O,M,V).

mvd(O,M,V) :-
	var(M),
	!,
	candidate_object_imvd(Super,MethSign,O),
	flora_funct_arity(M,MethSign),
	inheritance_imvd(Super,M,V),
	sk_not(local_mvd(O,MethSign)),
	sk_not(multiple_object_imvd(Super,MethSign,O)).

mvd(O,M,V) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_mvd(O,MethSign)),
	candidate_object_imvd(Super,MethSign,O),
	inheritance_imvd(Super,M,V),
	sk_not(multiple_object_imvd(Super,MethSign,O)).


mvddef(O,M) :-
	inferred_mvddef(O,M).

mvddef(O,M) :-
	var(M),
	!,
	candidate_object_imvd(Super,MethSign,O),
	flora_funct_arity(M,MethSign),
	inheritance_imvddef(Super,M),
	sk_not(local_mvd(O,MethSign)),
	sk_not(multiple_object_imvd(Super,MethSign,O)).

mvddef(O,M) :-
	flora_funct_arity(M,MethSign),
	sk_not(local_mvd(O,MethSign)),
	candidate_object_imvd(Super,MethSign,O),
	inheritance_imvddef(Super,M),
	sk_not(multiple_object_imvd(Super,MethSign,O)).

mvddef(O,M) :-
	mvd(O,M,_V).
	%% Should have a cut here when XSB will allow


/*****************************************************************************
  monotonic inheritance of binary attributes (tabled methods)
*****************************************************************************/
meth(O,M) :-
	inferred_meth(O,M).

meth(O,M) :-
	isa(O,C),
	imeth(C,M).


imeth(C,M) :-
	inferred_imeth(C,M).

imeth(C,M) :-
	inheritance_sub(C,S),
	imeth(S,M).


/*****************************************************************************
  nontabled methods (transactions)
*****************************************************************************/
tran(O,T) :-
	inferred_tran(O,T).


/*****************************************************************************
  monotonic inheritance of signatures
*****************************************************************************/
fdsig(O,M,R) :-
	inferred_fdsig(O,M,R).

fdsig(O,M,R) :-
	isa(O,C),
	ifdsig(C,M,R).


fdsigdef(O,M) :-
	inferred_fdsigdef(O,M).

fdsigdef(O,M) :-
	isa(O,C),
	ifdsigdef(C,M).

fdsigdef(O,M) :-
	fdsig(O,M,_V).
	%% Should have a cut here when XSB will allow


ifdsig(C,M,R) :-
	inferred_ifdsig(C,M,R).

ifdsig(C,M,R) :-
	inheritance_sub(C,S),
	ifdsig(S,M,R).


ifdsigdef(C,M) :-
	inferred_ifdsigdef(C,M).

ifdsigdef(C,M) :-
	inheritance_sub(C,S),
	ifdsigdef(S,M).

ifdsigdef(C,M) :-
	ifdsig(C,M,_V).
	%% Should have a cut here when XSB will allow


mvdsig(O,M,R) :-
	inferred_mvdsig(O,M,R).

mvdsig(O,M,R) :-
	isa(O,C),
	imvdsig(C,M,R).


mvdsigdef(O,M) :-
	inferred_mvdsigdef(O,M).

mvdsigdef(O,M) :-
	isa(O,C),
	imvdsigdef(C,M).

mvdsigdef(O,M) :-
	mvdsig(O,M,_V).
	%% Should have a cut here when XSB will allow


imvdsig(C,M,R) :-
	inferred_imvdsig(C,M,R).

imvdsig(C,M,R) :-
	inheritance_sub(C,S),
	imvdsig(S,M,R).


imvdsigdef(C,M) :-
	inferred_imvdsigdef(C,M).

imvdsigdef(C,M) :-
	inheritance_sub(C,S),
	imvdsigdef(S,M).

imvdsigdef(C,M) :-
	imvdsig(C,M,_V).
	%% Should have a cut here when XSB will allow


/*****************************************************************************
  object existence
*****************************************************************************/
exists(X) :-
	inferred_exists(X).


/*****************************************************************************
  equality maintenance
*****************************************************************************/
diffobjeql(X,Y) :-
	inferred_objeql(X,Y),
	X \= Y.
