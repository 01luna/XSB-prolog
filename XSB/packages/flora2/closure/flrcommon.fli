/***************************************************************************
  file: closure/flrcommon.fli

  Author(s): Guizhen Yang

  Contact:   flora-users@lists.sourceforge.net

  Copyright (C) The Research Foundation of SUNY, 1999-2001
***************************************************************************/


#include "flora_wspredicates.flh"
#include "flora_porting.flh"

#include "flrtypes.fli"

:- WRAP_COPY_UNQUOTED 'FLORA_THIS_MODULE_NAME'.


/***************************************************************************
  inferred predicates

  These wrapper predicates with the 'inferred_' prefix are needed to make
  the trailer dynamically loadable. Note that the program rules use wrapper
  predicates with the 'derived_' prefix.
***************************************************************************/
inferred_sub(X,Y) :- derived_sub(X,Y,'FLORA_THIS_MODULE_NAME').
inferred_isa(O,C) :- derived_isa(O,C,'FLORA_THIS_MODULE_NAME').
inferred_fd(O,A,V) :- derived_fd(O,A,V,'FLORA_THIS_MODULE_NAME').
inferred_ifd(C,A,V) :- derived_ifd(C,A,V,'FLORA_THIS_MODULE_NAME').
inferred_mvd(O,A,V) :- derived_mvd(O,A,V,'FLORA_THIS_MODULE_NAME').
inferred_imvd(C,A,V) :- derived_imvd(C,A,V,'FLORA_THIS_MODULE_NAME').
inferred_mvddef(O,A) :- derived_mvddef(O,A,'FLORA_THIS_MODULE_NAME').
inferred_imvddef(C,A) :- derived_imvddef(C,A,'FLORA_THIS_MODULE_NAME').
inferred_fdsig(O,MethodArgs,R) :- derived_fdsig(O,MethodArgs,R,'FLORA_THIS_MODULE_NAME').
inferred_fdsigdef(O,MethodArgs) :- derived_fdsigdef(O,MethodArgs,'FLORA_THIS_MODULE_NAME').
inferred_ifdsig(O,MethodArgs,R) :- derived_ifdsig(O,MethodArgs,R,'FLORA_THIS_MODULE_NAME').
inferred_ifdsigdef(O,MethodArgs) :- derived_ifdsigdef(O,MethodArgs,'FLORA_THIS_MODULE_NAME').
inferred_mvdsig(O,MethodArgs,R) :- derived_mvdsig(O,MethodArgs,R,'FLORA_THIS_MODULE_NAME').
inferred_mvdsigdef(O,MethodArgs) :- derived_mvdsigdef(O,MethodArgs,'FLORA_THIS_MODULE_NAME').
inferred_imvdsig(O,MethodArgs,R) :- derived_imvdsig(O,MethodArgs,R,'FLORA_THIS_MODULE_NAME').
inferred_imvdsigdef(O,MethodArgs) :- derived_imvdsigdef(O,MethodArgs,'FLORA_THIS_MODULE_NAME').
inferred_meth(O,M) :- derived_meth(O,M,'FLORA_THIS_MODULE_NAME').
inferred_imeth(C,M) :- derived_imeth(C,M,'FLORA_THIS_MODULE_NAME').
inferred_tran(O,T) :- derived_tran(O,T,'FLORA_THIS_MODULE_NAME').
inferred_objeql(X,Y) :- derived_objeql(X,Y,'FLORA_THIS_MODULE_NAME').
inferred_exists(X) :- derived_exists(X,'FLORA_THIS_MODULE_NAME').


/*****************************************************************************
  inheritance book-keeping information for scalar methods

  Note that the argument "Class" here is considered as a class.
  It is about inheritance from superclasses to subclasses.
*****************************************************************************/
override_class_ifd(C,MethSign,Class) :-
	sub(X,C),
	sub(Class,X),
	X \= C,
	X \= Class,
	local_ifd(X,MethSign).


candidate_class_ifd(C,MethSign,Class) :-
	var(MethSign),
	!,
	sub(Class,C),
	local_ifd(C,MethSign),
	C \= Class,
	FLORA_TNOT(override_class_ifd(C,MethSign,Class)).

candidate_class_ifd(C,MethSign,Class) :-
	local_ifd(C,MethSign),
	sub(Class,C),
	C \= Class,
	FLORA_TNOT(override_class_ifd(C,MethSign,Class)).


multiple_class_ifd(C,MethSign,Class) :-
	candidate_class_ifd(X,MethSign,Class),
	X \= C.


/*****************************************************************************
  inheritance book-keeping information for scalar methods

  Note that the argument "Object" here is considered as an object.
  It is about inheritance from superclasses to instances/members.
*****************************************************************************/
override_object_ifd(C,MethSign,Object) :-
	sub(X,C),
	isa(Object,X),
	X \= C,
	X \= Object,
	local_ifd(X,MethSign).


candidate_object_ifd(C,MethSign,Object) :-
	var(MethSign),
	!,
	isa(Object,C),
	local_ifd(C,MethSign),
	C \= Object,
	FLORA_TNOT(override_object_ifd(C,MethSign,Object)).

candidate_object_ifd(C,MethSign,Object) :-
	local_ifd(C,MethSign),
	isa(Object,C),
	C \= Object,
	FLORA_TNOT(override_object_ifd(C,MethSign,Object)).


multiple_object_ifd(C,MethSign,Object) :-
	candidate_object_ifd(X,MethSign,Object),
	X \= C.


/*****************************************************************************
  inheritance book-keeping information for multivalued methods

  Note that the argument "Class" here is considered as a class.
  It is about inheritance from superclasses to subclasses.
*****************************************************************************/
override_class_imvd(C,MethSign,Class) :-
	sub(X,C),
	sub(Class,X),
	X \= C,
	X \= Class,
	local_imvd(X,MethSign).


candidate_class_imvd(C,MethSign,Class) :-
	var(MethSign),
	!,
	sub(Class,C),
	local_imvd(C,MethSign),
	C \= Class,
	FLORA_TNOT(override_class_imvd(C,MethSign,Class)).

candidate_class_imvd(C,MethSign,Class) :-
	local_imvd(C,MethSign),
	sub(Class,C),
	C \= Class,
	FLORA_TNOT(override_class_imvd(C,MethSign,Class)).


multiple_class_imvd(C,MethSign,Class) :-
	candidate_class_imvd(X,MethSign,Class),
	X \= C.


/*****************************************************************************
  inheritance book-keeping information for multivalued methods

  Note that the argument "Object" here is considered as an object.
  It is about inheritance from superclasses to instances/members.
*****************************************************************************/
override_object_imvd(C,MethSign,Object) :-
	sub(X,C),
	isa(Object,X),
	X \= C,
	X \= Object,
	local_imvd(X,MethSign).


candidate_object_imvd(C,MethSign,Object) :-
	var(MethSign),
	!,
	isa(Object,C),
	local_imvd(C,MethSign),
	C \= Object,
	FLORA_TNOT(override_object_imvd(C,MethSign,Object)).

candidate_object_imvd(C,MethSign,Object) :-
	local_imvd(C,MethSign),
	isa(Object,C),
	C \= Object,
	FLORA_TNOT(override_object_imvd(C,MethSign,Object)).


multiple_object_imvd(C,MethSign,Object) :-
	candidate_object_imvd(X,MethSign,Object),
	X \= C.


/***************************************************************************
  defined scalar attributes
***************************************************************************/
fddef(O,A) :- fd(O,A,_V).

ifddef(C,A) :- ifd(C,A,_V).


/***************************************************************************
  multivalued attributes inclusion
***************************************************************************/
mvdinc(_O,_M,[]) :- !.
mvdinc(O,M,[H|T]) :- mvd(O,M,H), mvdinc(O,M,T).


imvdinc(_O,_M,[]) :- !.
imvdinc(O,M,[H|T]) :- imvd(O,M,H), imvdinc(O,M,T).


/****************************************************************************
  multivalued attributes aggregation

  Note: (1) These two predicates always succeeds.
        (2) These two predicates are not tabled.
****************************************************************************/
mvdtolist(O,M,L) :-
	mvddef(O,M),
	findall(V,mvd(O,M,V),L).

mvdtolist(O,M,[]) :-
	FLORA_TNOT(mvddef(O,M)).


imvdtolist(O,M,L) :-
	imvddef(O,M),
	findall(V,imvd(O,M,V),L).
 
imvdtolist(O,M,[]) :-
	FLORA_TNOT(imvddef(O,M)).


/****************************************************************************
  exists/1
****************************************************************************/
exists(X) :- fd(X,_,_).
exists(X) :- fd(_,_,X).
exists(X) :- mvd(X,_,_).
exists(X) :- mvd(_,_,X).
exists(X) :- mvddef(X,_).
exists(X) :- ifd(X,_,_).
exists(X) :- ifd(_,_,X).
exists(X) :- imvd(X,_,_).
exists(X) :- imvd(_,_,X).
exists(X) :- imvddef(X,_).
exists(X) :- isa(X,_).
exists(X) :- isa(_,X).
exists(X) :- sub(X,_).
exists(X) :- sub(_,X).
exists(X) :- meth(X,_).
exists(X) :- imeth(X,_).


/****************************************************************************
  objeql/3
****************************************************************************/
objeql(X,Y,_Caller) :- X=Y.
%%objeql(X,Y,_Caller) :- (nonvar(X); nonvar(Y)), X=Y.
objeql(X,Y,_Caller) :- diffobjeql(X,Y).


/****************************************************************************
    Introduced in support of the debugger
****************************************************************************/
%% like sub, but the debugger won't trace it
silent_sub(C,S) :- sub(C,S).

%% like sub, but the debugger will print it specially and won't trace inside
inheritance_sub(C,S) :- sub(C,S).

%% Like inferred_* but the debugger will print it in a special way
inheritance_ifd(C,M,V) :- inferred_ifd(C,M,V).
inheritance_imvd(C,M,V) :- inferred_imvd(C,M,V).
inheritance_imvddef(C,M) :- inferred_imvddef(C,M).

%% Like inferred_* but the debugger (actually the decoder, flrdecode.P)
%% will print it in a special way
inheritance_ifd_eql(C,M,V) :- inferred_ifd_eql(C,M,V).
inheritance_imvd_eql(C,M,V) :- inferred_imvd_eql(C,M,V).
inheritance_imvddef_eql(C,M) :- inferred_imvddef_eql(C,M).

FL_SILENT_EQUAL(X,X).
