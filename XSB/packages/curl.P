/*
** File: packages/curl.P
** Author: Aneesh Ali
** Contact:   xsb-contact@cs.sunysb.edu
** 
** Copyright (C) The Research Foundation of SUNY, 2010
** 
** XSB is free software; you can redistribute it and/or modify it under the
** terms of the GNU Library General Public License as published by the Free
** Software Foundation; either version 2 of the License, or (at your option)
** any later version.
** 
** XSB is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
** FOR A PARTICULAR PURPOSE.  See the GNU Library General Public License for
** more details.
** 
** You should have received a copy of the GNU Library General Public License
** along with XSB; if not, write to the Free Software Foundation,
** Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
**
*/


:- import bootstrap_package/2 from packaging.
:- import xsb_configuration/2 from xsb_configuration.
:- import re_match/5, re_substring/4 from regmatch.

:- import pl_load_page/1, 
	pl_allocate_error_term/2,
	pl_finalize_warn/1 from curl2pl.

:- export 
	load_page/5, encode_url/3, encode_url/4.

:- xsb_configuration(regmatch_support, 'yes'), 
	bootstrap_package([regmatch, cc], regmatch),
	[regmtchconfig].

:- bootstrap_package([curl, cc], curl),
	[curlconfig].


load_page(_Source, _Document, _Options, _, _Warn) :- 
	catch(\+ curl_info:curl_info(support,yes),
	      _,
	      throw(error(existence_error, 'Error: open(url(...), ..., ...) - the package Curl is not configured'))),
	!,
	throw(error(existence_error, 'Error: open(url(...), ..., ...) - the package Curl is not configured')).

load_page(Source, Document, Options, [DirEnc, FileUnEnc], Warn) :- 
	load_page_warn(Source, Document, Options, [DirEnc, FileUnEnc], Warn),
	pl_finalize_warn(Warn).

load_page_warn(Source, Document, Options, [DirEnc, FileUnEnc], Warn) :-
	pl_allocate_error_term(Error, Warn),
	examine_open_options(Options, ValidOptions), 
        pl_load_page([options(ValidOptions), source(Source), document(DocTemp), encoded(DirEncTemp, FileUnEncTemp)]),
	handle_html_redir(ValidOptions, Source, DocTemp, DirEncTemp, FileUnEncTemp, Document, DirEnc, FileUnEnc), 
	throw_error(Error).

handle_html_redir(ValidOptions, _, Document, DirEnc, FileUnEnc, Document, DirEnc, FileUnEnc) :-
	member(redirect(false), ValidOptions), 
	!.
handle_html_redir(ValidOptions, _, DocTemp, _, _, Document, DirEnc, FileUnEnc) :-
	xsb_configuration(regmatch_support, 'yes'), 
	re_match('<meta[\n\ ]*http-equiv[^>]*content[\ ]*=[\ ]*\"[0-9]+[\ ]*;[\ ]*URL[\ ]*=[\ ]*(http[^>]*)\">', DocTemp, 0, _, [_, match(St, Fn)]), 
	re_substring(DocTemp, St, Fn, UrlNew), 
        pl_load_page([options(ValidOptions), source(url(UrlNew)), document(Document), encoded(DirEnc, FileUnEnc)]), 
	!.
handle_html_redir(_, _, Document, DirEnc, FileUnEnc, Document, DirEnc, FileUnEnc) :-
	!.

encode_url(Source, DirEnc, FileUnEnc) :-
	!, 
	encode_url(Source, [], DirEnc, FileUnEnc).

encode_url(Source, Options, DirEnc, FileUnEnc) :-
	!, 
	load_page(Source, _, Options, [DirEnc, FileUnEnc], _). 

examine_open_options([], []).
examine_open_options([redirect(Bool)|T], [redirect(Bool)|Options]):- !,
	examine_open_options(T, Options).
examine_open_options([secure(CrtName)|T], [secure(CrtName)|Options]):- !,
	examine_open_options(T, Options).
examine_open_options([auth(UserName, Password)|T], [auth(UserName, Password)|Options]):-
	!,
	examine_open_options(T, Options).
examine_open_options([_|T], Options):- 
	examine_open_options(T, Options).

throw_error(Error) :- var(Error), !.
throw_error(Error) :- throw(Error).

member(X, [X|_]).
member(X, [_|L]) :- member(X, L).

